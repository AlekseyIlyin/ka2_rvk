#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустой() Тогда
		ЗаполнитьЗначенияСвойств(Запись, Параметры.ЗначенияЗаполнения);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	СписокКодовНаименованийСделок = КонтролируемыеСделкиПовтИсп.СписокКодовНаименованийСделки();
	СписокКодовПоставки = КонтролируемыеСделкиПовтИсп.КодыУсловийПоставки();
	УстановитьПривилегированныйРежим(Ложь);
	
	Элементы.КодНаименованияСделки.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодовНаименованийСделок Цикл
		НовыйКод = Элементы.КодНаименованияСделки.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
	
	Элементы.КодУсловийПоставки.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодовПоставки Цикл
		НовыйКод = Элементы.КодУсловийПоставки.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
	
	ДоступностьКлючевыхЗаписей = Истина;
	Если Параметры.Свойство("ДоступностьКлючевыхЗаписей") Тогда
		ДоступностьКлючевыхЗаписей = Параметры.ДоступностьКлючевыхЗаписей;
	КонецЕсли;
	
	Элементы.Организация.ТолькоПросмотр 			= НЕ ДоступностьКлючевыхЗаписей;
	Элементы.ДоговорКонтрагента.ТолькоПросмотр 	= НЕ ДоступностьКлючевыхЗаписей;
	Элементы.Контрагент.ТолькоПросмотр 			= НЕ ДоступностьКлючевыхЗаписей;
	
	Элементы.КодНаименованияСделки.АвтоОтметкаНезаполненного 			= Не ДоступностьКлючевыхЗаписей;
	Элементы.ПризнакОпределенияЦеныСделки.АвтоОтметкаНезаполненного 	= Не ДоступностьКлючевыхЗаписей;
	
	Если ДоступностьКлючевыхЗаписей Тогда
		НастроитьДоговор();
	КонецЕсли;
	
	КонтролируемыеСделки.ЗаполнитьСписокГоловныхОрганизаций(Элементы.Организация.СписокВыбора);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	НастроитьДоговор();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьДоговор()
	
	Элементы.ДоговорКонтрагента.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Запись.Контрагент);
	
	Если ЗначениеЗаполнено(Запись.Контрагент) Тогда
		
		Если ТипЗнч(Запись.Контрагент) = Тип("СправочникСсылка.Организации") Тогда
			
			ТипПоляДоговор = Новый ОписаниеТипов("СправочникСсылка.ДоговорыМеждуОрганизациями");
			
		Иначе
			
			ТипыДоговоров = Новый Массив;
			ТипыДоговоров.Добавить(Тип("СправочникСсылка.ДоговорыКонтрагентов"));
			ТипыДоговоров.Добавить(Тип("СправочникСсылка.ДоговорыАренды"));
			ТипыДоговоров.Добавить(Тип("СправочникСсылка.ДоговорыКредитовИДепозитов"));
			ТипПоляДоговор = Новый ОписаниеТипов(ТипыДоговоров);
			
		КонецЕсли;
		
		Элементы.ДоговорКонтрагента.ОграничениеТипа = ТипПоляДоговор;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти