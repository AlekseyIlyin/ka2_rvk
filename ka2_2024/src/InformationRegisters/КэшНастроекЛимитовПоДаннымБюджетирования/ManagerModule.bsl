#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Кэширует настройки лимитов по данным бюджетирования путем записи в регистр сведений
// 
// Параметры:
//  НачалоПериода - Дата - начало периода действия настройки лимитов
//  КонецПериода - Дата - конец периода действия настройки лимитов
//  ВидДокумента - СправочникСсылка.ИдентификаторыОбъектовМетаданных - вид документа
//  ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция документа.
//  Значение - Структура - структура кэшируемых настроек лимитов:
//   *НастройкаПолученияФакта - Структура
//   *НастройкаПравилЛимитов - Структура
//
Процедура УстановитьЗначениеКэша(НачалоПериода, КонецПериода, ВидДокумента, ХозяйственнаяОперация, Значение) Экспорт
	
	Запись = РегистрыСведений.КэшНастроекЛимитовПоДаннымБюджетирования.СоздатьМенеджерЗаписи();
	
	Запись.Период = НачалоПериода;
	Запись.ВидДокумента = ВидДокумента;
	Запись.ХозяйственнаяОперация = ХозяйственнаяОперация;
	Для Каждого КлючИЗначение Из Значение Цикл
		Запись[КлючИЗначение.Ключ] = Новый ХранилищеЗначения(КлючИЗначение.Значение);
	КонецЦикла;
	Запись.ДействуетПо = КонецПериода;
	
	Запись.Записать();
	
КонецПроцедуры

// Возвращает сохраненные в кэше настройки лимитов по данным бюджетирования
// 
// Параметры:
//  Дата - Дата - дата, на которую необходимо получить данные кэша
//  ВидДокумента - СправочникСсылка.ИдентификаторыОбъектовМетаданных - вид документа
//  ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - хозяйственная операция документа.
//
// Возвращаемое значение:
//  Неопределено
//  Структура - структура настроек лимитов по данным бюджетирования.
//
Функция ПолучитьЗначениеКэша(Дата, ВидДокумента, ХозяйственнаяОперация) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КэшНастроекЛимитовПоДаннымБюджетирования.НастройкаПолученияФакта КАК НастройкаПолученияФакта,
	|	КэшНастроекЛимитовПоДаннымБюджетирования.НастройкаПравилЛимитов КАК НастройкаПравилЛимитов
	|ИЗ
	|	РегистрСведений.КэшНастроекЛимитовПоДаннымБюджетирования.СрезПоследних(&Дата, ВидДокумента = &ВидДокумента И ХозяйственнаяОперация = &ХозяйственнаяОперация) КАК КэшНастроекЛимитовПоДаннымБюджетирования
	|ГДЕ
	|	КэшНастроекЛимитовПоДаннымБюджетирования.ДействуетПо >= &Дата";
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ВидДокумента", ВидДокумента);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация", ХозяйственнаяОперация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Настройки = Новый Структура;
	Настройки.Вставить("НастройкаПолученияФакта", Выборка.НастройкаПолученияФакта.Получить());
	Настройки.Вставить("НастройкаПравилЛимитов", Выборка.НастройкаПравилЛимитов.Получить());
	
	Возврат Настройки;
	
КонецФункции

#КонецОбласти

#КонецЕсли