#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ПодготовитьДанныеДляФормированияЗаданийПередЗаписью();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляФормированияЗаданийПриЗаписи();
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьДанныеДляФормированияЗаданийПередЗаписью()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаПередЗаписью.Период КАК Период,
	|	ТаблицаПередЗаписью.Организация КАК Организация,
	|	ТаблицаПередЗаписью.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПередЗаписью.СостояниеБУ КАК СостояниеБУ,
	|	ТаблицаПередЗаписью.СостояниеНУ КАК СостояниеНУ,
	|	ТаблицаПередЗаписью.НачислятьАмортизациюБУ КАК НачислятьАмортизациюБУ,
	|	ТаблицаПередЗаписью.НачислятьАмортизациюНУ КАК НачислятьАмортизациюНУ,
	|	ТаблицаПередЗаписью.СтатьяРасходовБУ КАК СтатьяРасходовБУ,
	|	ТаблицаПередЗаписью.АналитикаРасходовБУ КАК АналитикаРасходовБУ,
	|	ТаблицаПередЗаписью.ПодразделениеБУ КАК ПодразделениеБУ,
	|	ТаблицаПередЗаписью.НаправлениеДеятельностиБУ КАК НаправлениеДеятельностиБУ,
	|	ТаблицаПередЗаписью.СтатьяРасходовНУ КАК СтатьяРасходовНУ,
	|	ТаблицаПередЗаписью.АналитикаРасходовНУ КАК АналитикаРасходовНУ,
	|	ТаблицаПередЗаписью.ПодразделениеНУ КАК ПодразделениеНУ,
	|	ТаблицаПередЗаписью.НаправлениеДеятельностиНУ КАК НаправлениеДеятельностиНУ,
	|	ТаблицаПередЗаписью.СтатьяРасходовАмортизационнойПремии КАК СтатьяРасходовАмортизационнойПремии,
	|	ТаблицаПередЗаписью.АналитикаРасходовАмортизационнойПремии КАК АналитикаРасходовАмортизационнойПремии,
	|	ТаблицаПередЗаписью.ПодразделениеАмортизационнойПремии КАК ПодразделениеАмортизационнойПремии,
	|	ТаблицаПередЗаписью.НаправлениеДеятельностиАмортизационнойПремии КАК НаправлениеДеятельностиАмортизационнойПремии,
	|	ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию КАК ПередаватьРасходыВДругуюОрганизацию,
	|	ТаблицаПередЗаписью.ОрганизацияПолучательРасходов КАК ОрганизацияПолучательРасходов,
	|	ТаблицаПередЗаписью.СпособОтраженияАмортизацииБУ КАК СпособОтраженияАмортизацииБУ,
	|	ТаблицаПередЗаписью.СпособОтраженияАмортизацииНУ КАК СпособОтраженияАмортизацииНУ,
	|	ТаблицаПередЗаписью.СпособОтраженияАмортизационнойПремии КАК СпособОтраженияАмортизационнойПремии,
	|	ТаблицаПередЗаписью.АмортизационнаяГруппа КАК АмортизационнаяГруппа,
	|	ТаблицаПередЗаписью.НедвижимоеИмущество КАК НедвижимоеИмущество
	|
	|ПОМЕСТИТЬ ПорядокУчетаОСБУПередЗаписью
	|
	|ИЗ
	|	РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПередЗаписью
	|
	|ГДЕ
	|	ТаблицаПередЗаписью.Регистратор = &Регистратор
	|	И ТаблицаПередЗаписью.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПодготовитьДанныеДляФормированияЗаданийПриЗаписи()
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Период           КАК Период,
	|	Таблица.Организация      КАК Организация,
	|	Таблица.ОсновноеСредство КАК ОсновноеСредство,
	|	ИСТИНА                   КАК ОтражатьВРеглУчете,
	|	ЛОЖЬ                     КАК ОтражатьВУпрУчете,
	|	&Регистратор             КАК Документ
	|
	|ПОМЕСТИТЬ ПорядокУчетаОСБУИзменение
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаПередЗаписью.Период            КАК Период,
	|		ТаблицаПередЗаписью.Организация       КАК Организация,
	|		ТаблицаПередЗаписью.ОсновноеСредство  КАК ОсновноеСредство
	|	ИЗ
	|		ПорядокУчетаОСБУПередЗаписью КАК ТаблицаПередЗаписью
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПриЗаписи
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|				И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|				И ТаблицаПриЗаписи.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|		ГДЕ
	|			(ТаблицаПриЗаписи.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.СостояниеБУ <> ТаблицаПередЗаписью.СостояниеБУ
	|				ИЛИ ТаблицаПриЗаписи.СостояниеНУ <> ТаблицаПередЗаписью.СостояниеНУ
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюБУ <> ТаблицаПередЗаписью.НачислятьАмортизациюБУ
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюНУ <> ТаблицаПередЗаписью.НачислятьАмортизациюНУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходовБУ <> ТаблицаПередЗаписью.СтатьяРасходовБУ
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходовБУ <> ТаблицаПередЗаписью.АналитикаРасходовБУ
	|				ИЛИ ТаблицаПриЗаписи.ПодразделениеБУ <> ТаблицаПередЗаписью.ПодразделениеБУ
	|				ИЛИ ТаблицаПриЗаписи.НаправлениеДеятельностиБУ <> ТаблицаПередЗаписью.НаправлениеДеятельностиБУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходовНУ <> ТаблицаПередЗаписью.СтатьяРасходовНУ
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходовНУ <> ТаблицаПередЗаписью.АналитикаРасходовНУ
	|				ИЛИ ТаблицаПриЗаписи.ПодразделениеНУ <> ТаблицаПередЗаписью.ПодразделениеНУ
	|				ИЛИ ТаблицаПриЗаписи.НаправлениеДеятельностиНУ <> ТаблицаПередЗаписью.НаправлениеДеятельностиНУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходовАмортизационнойПремии <> ТаблицаПередЗаписью.СтатьяРасходовАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходовАмортизационнойПремии <> ТаблицаПередЗаписью.АналитикаРасходовАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.ПодразделениеАмортизационнойПремии <> ТаблицаПередЗаписью.ПодразделениеАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.НаправлениеДеятельностиАмортизационнойПремии <> ТаблицаПередЗаписью.НаправлениеДеятельностиАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.ПередаватьРасходыВДругуюОрганизацию <> ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию
	|				ИЛИ ТаблицаПриЗаписи.ОрганизацияПолучательРасходов <> ТаблицаПередЗаписью.ОрганизацияПолучательРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияАмортизацииБУ <> ТаблицаПередЗаписью.СпособОтраженияАмортизацииБУ
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияАмортизацииНУ <> ТаблицаПередЗаписью.СпособОтраженияАмортизацииНУ
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияАмортизационнойПремии <> ТаблицаПередЗаписью.СпособОтраженияАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.АмортизационнаяГруппа <> ТаблицаПередЗаписью.АмортизационнаяГруппа
	|				ИЛИ ТаблицаПриЗаписи.НедвижимоеИмущество <> ТаблицаПередЗаписью.НедвижимоеИмущество
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаПриЗаписи.Период,
	|		ТаблицаПриЗаписи.Организация,
	|		ТаблицаПриЗаписи.ОсновноеСредство
	|	ИЗ
	|		РегистрСведений.ПорядокУчетаОСБУ КАК ТаблицаПриЗаписи
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПорядокУчетаОСБУПередЗаписью КАК ТаблицаПередЗаписью
	|			ПО ТаблицаПриЗаписи.ОсновноеСредство = ТаблицаПередЗаписью.ОсновноеСредство
	|				И ТаблицаПриЗаписи.Организация = ТаблицаПередЗаписью.Организация
	|		ГДЕ
	|			(ТаблицаПередЗаписью.Период ЕСТЬ NULL
	|				ИЛИ ТаблицаПриЗаписи.СостояниеБУ <> ТаблицаПередЗаписью.СостояниеБУ
	|				ИЛИ ТаблицаПриЗаписи.СостояниеНУ <> ТаблицаПередЗаписью.СостояниеНУ
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюБУ <> ТаблицаПередЗаписью.НачислятьАмортизациюБУ
	|				ИЛИ ТаблицаПриЗаписи.НачислятьАмортизациюНУ <> ТаблицаПередЗаписью.НачислятьАмортизациюНУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходовБУ <> ТаблицаПередЗаписью.СтатьяРасходовБУ
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходовБУ <> ТаблицаПередЗаписью.АналитикаРасходовБУ
	|				ИЛИ ТаблицаПриЗаписи.ПодразделениеБУ <> ТаблицаПередЗаписью.ПодразделениеБУ
	|				ИЛИ ТаблицаПриЗаписи.НаправлениеДеятельностиБУ <> ТаблицаПередЗаписью.НаправлениеДеятельностиБУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходовНУ <> ТаблицаПередЗаписью.СтатьяРасходовНУ
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходовНУ <> ТаблицаПередЗаписью.АналитикаРасходовНУ
	|				ИЛИ ТаблицаПриЗаписи.ПодразделениеНУ <> ТаблицаПередЗаписью.ПодразделениеНУ
	|				ИЛИ ТаблицаПриЗаписи.НаправлениеДеятельностиНУ <> ТаблицаПередЗаписью.НаправлениеДеятельностиНУ
	|				ИЛИ ТаблицаПриЗаписи.СтатьяРасходовАмортизационнойПремии <> ТаблицаПередЗаписью.СтатьяРасходовАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.АналитикаРасходовАмортизационнойПремии <> ТаблицаПередЗаписью.АналитикаРасходовАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.ПодразделениеАмортизационнойПремии <> ТаблицаПередЗаписью.ПодразделениеАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.НаправлениеДеятельностиАмортизационнойПремии <> ТаблицаПередЗаписью.НаправлениеДеятельностиАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.ПередаватьРасходыВДругуюОрганизацию <> ТаблицаПередЗаписью.ПередаватьРасходыВДругуюОрганизацию
	|				ИЛИ ТаблицаПриЗаписи.ОрганизацияПолучательРасходов <> ТаблицаПередЗаписью.ОрганизацияПолучательРасходов
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияАмортизацииБУ <> ТаблицаПередЗаписью.СпособОтраженияАмортизацииБУ
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияАмортизацииНУ <> ТаблицаПередЗаписью.СпособОтраженияАмортизацииНУ
	|				ИЛИ ТаблицаПриЗаписи.СпособОтраженияАмортизационнойПремии <> ТаблицаПередЗаписью.СпособОтраженияАмортизационнойПремии
	|				ИЛИ ТаблицаПриЗаписи.АмортизационнаяГруппа <> ТаблицаПередЗаписью.АмортизационнаяГруппа
	|				ИЛИ ТаблицаПриЗаписи.НедвижимоеИмущество <> ТаблицаПередЗаписью.НедвижимоеИмущество
	|				ИЛИ НАЧАЛОПЕРИОДА(ТаблицаПриЗаписи.Период, МЕСЯЦ) <> НАЧАЛОПЕРИОДА(ТаблицаПередЗаписью.Период, МЕСЯЦ))
	|			И ТаблицаПриЗаписи.Регистратор = &Регистратор
	|			И ТаблицаПриЗаписи.ДатаИсправления = ДАТАВРЕМЯ(1,1,1)
	|
	|	) КАК Таблица
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПорядокУчетаОСБУПередЗаписью
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ПорядокУчетаОСБУИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
