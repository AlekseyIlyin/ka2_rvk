#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет записи в регистр заданий.
//
// Параметры:
//  ОбъектРасчета	 - СправочникОбъект.ОбъектыРасчетов  - справочник, для которого необходимо добавить задание.
//  ПолныйРасчет	 - Булево							 - признак, необходимо рассчитать нормативную длительность этапов производства.
//  Автозапуск		 - Булево							 - автоматический запуск.
//
Процедура ДобавитьЗадание(ОбъектРасчета, ПолныйРасчет = Ложь, Автозапуск = Истина) Экспорт
	
	Задание = Новый Структура;
	Задание.Вставить("ОбъектРасчета", ОбъектРасчета);
	Задание.Вставить("ПолныйРасчет",  ПолныйРасчет);
	
	Если Автозапуск Тогда
		ОтложенныеЗадания.ОбработатьЗаданияОчереди("ЗаданияКРасчетуДлительностиПроизводства", Задание);
	Иначе
		ОтложенныеЗадания.ДобавитьЗаданияВОчередь("ЗаданияКРасчетуДлительностиПроизводства", Задание);
	КонецЕсли;
	
КонецПроцедуры

// Запускает планирование производства
//
// Параметры:
//  Отбор - Неопределено, см. ОтложенныеЗадания.ПолучитьЗадания.Отбор
//
Процедура ЗапуститьЗадание(Отбор = Неопределено) Экспорт
	
	Очередь = "ЗаданияКРасчетуДлительностиПроизводства";
	
	Если ЗначениеЗаполнено(Отбор) Тогда
		
		ТаблицаЗаданий = ОтложенныеЗадания.ИнициализироватьТаблицуЗаданий(Очередь);
		ТаблицаЗаданий[0].Задания = ОтложенныеЗадания.ПолучитьЗадания(Очередь, Отбор);
		
		ОтложенныеЗадания.Запустить(ТаблицаЗаданий);
		
	Иначе
		
		ОтложенныеЗадания.Запустить(Очередь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли