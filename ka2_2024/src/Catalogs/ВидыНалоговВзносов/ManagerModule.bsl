#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//++ Локализация

// Возвращает КБК с учетом вида налогового обязательства
//
// Параметры:
//   ТипНалога                  - СправочникСсылка.ВидыНалоговВзносов
//   ВидНалоговогоОбязательства - ПеречислениеСсылка.ВидыПлатежейВГосБюджет
//   Период                     - Дата
//
// Возвращаемое значение:
//   Строка
//
Функция КБКПоВидуНалоговогоОбязательства(ТипНалога, Знач ВидНалоговогоОбязательства = Неопределено, Знач Период = Неопределено) Экспорт
	
	КБК = КБКПоТипуНалога(ТипНалога);
	Если ПустаяСтрока(КБК) Тогда
		Возврат "";
	КонецЕсли;
	
	//++ НЕ УТ
	
	Если Не ЗначениеЗаполнено(ВидНалоговогоОбязательства) Тогда
		Если ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК) <> ПлатежиВБюджетКлиентСервер.ПустойКодПодвидаДоходов() Тогда
			Возврат КБК; // Сохраняем заданный код подвида доходов
		Иначе
			ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса();
	КонецЕсли;
	
	КодГлавногоАдминистратора    = ПлатежиВБюджетКлиентСервер.КодГлавногоАдминистратора(КБК);
	КодВидаДоходов               = ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК);
	КодПодвидаДоходов            = КодПодвидаДоходов(КБК, ТипНалога, ВидНалоговогоОбязательства, Период);
	КодОперацииСектораУправления = ПлатежиВБюджетКлиентСервер.КодОперацииСектораУправления(КБК);
	
	КБК = КодГлавногоАдминистратора + КодВидаДоходов + КодПодвидаДоходов + КодОперацииСектораУправления;
	
	//-- НЕ УТ
	
	Возврат КБК;
	
КонецФункции

// Возвращает вид налога по КБК, если определить не удалось возвращает ПрочиеНалогиИСборы
//
// Параметры:
//   КодБК - Строка
// 
// Возвращаемое значение:
//   СправочникСсылка.ВидыНалоговВзносов
//
Функция ТипНалогаПоКБК(Знач КодБК) Экспорт
	
	ВидНалога = ПрочиеНалогиИСборы;
	Если НЕ ПлатежиВБюджетКлиентСервер.КБКЗадан(КодБК) Тогда
		Возврат ВидНалога;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыНалоговВзносов.Ссылка
	|ИЗ
	|	Справочник.ВидыНалоговВзносов КАК ВидыНалоговВзносов
	|ГДЕ
	|	ВидыНалоговВзносов.Предопределенный";
	Выборка = Запрос.Выполнить().Выбрать();
	ШаблонСравнения = ШаблонСравненияКБК(КодБК);
	Пока Выборка.Следующий() Цикл
		Если ШаблонСравнения = ШаблонСравненияКБК(КБКПоТипуНалога(Выборка.Ссылка)) Тогда
			ВидНалога = Выборка.Ссылка;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ВидНалога;
	
КонецФункции

//-- Локализация

// Определяет настройки начального заполнения элементов
//
// Параметры:
//  Настройки - Структура - настройки заполнения:
//   * ПриНачальномЗаполненииЭлемента - Булево - Если Истина, то для каждого элемента будет
//      вызвана процедура индивидуального заполнения ПриНачальномЗаполненииЭлемента.
Процедура ПриНастройкеНачальногоЗаполненияЭлементов(Настройки) Экспорт
	
	Настройки.ПриНачальномЗаполненииЭлемента = Истина;
	
КонецПроцедуры

// Вызывается при начальном заполнении справочника.
//
// Параметры:
//  КодыЯзыков - Массив - список языков конфигурации. Актуально для мультиязычных конфигураций.
//  Элементы   - ТаблицаЗначений - данные заполнения. Состав колонок соответствует набору реквизитов справочника.
//  ТабличныеЧасти - Структура - Ключ - Имя табличной части объекта.
//                               Значение - Выгрузка в таблицу значений пустой табличной части.
//
Процедура ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти) Экспорт
	
	Элемент = Элементы.Добавить();
	Элемент.ИмяПредопределенныхДанных = "НДС";
	МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование", НаименованиеПредопределенногоЭлемента(Элемент.ИмяПредопределенныхДанных), КодыЯзыков);

	ВидыНалоговВзносовЛокализация.ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти);
	
КонецПроцедуры

// Вызывается при начальном заполнении создаваемого элемента.
//
// Параметры:
//  Объект                  - СправочникОбъект.НаборыУпаковок - заполняемый объект.
//  Данные                  - СтрокаТаблицыЗначений - данные заполнения.
//  ДополнительныеПараметры - Структура - Дополнительные параметры.
//
Процедура ПриНачальномЗаполненииЭлемента(Объект, Данные, ДополнительныеПараметры) Экспорт
	
	Возврат;
КонецПроцедуры

// Наименование предопределенного элемента.
// 
// Параметры:
//  ИмяПредопределенного - Строка - Имя предопределенного элемента
// 
// Возвращаемое значение:
//  Строка - Наименование предопределенного элемента
Функция НаименованиеПредопределенногоЭлемента(ИмяПредопределенного) Экспорт
	Если ИмяПредопределенного = "ЕдиныйНалоговыйПлатеж" Тогда
		Возврат "ru = 'Единый налоговый платеж'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ЕНВД" Тогда
		Возврат "ru = 'ЕНВД'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ЕСХН" Тогда
		Возврат "ru = 'ЕСХН'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ЗемельныйНалог" Тогда
		Возврат "ru = 'Земельный налог'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НалогНаИмущество" Тогда
		Возврат "ru = 'Налог на имущество организаций'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НалогНаПрибыль_РегиональныйБюджет" Тогда
		Возврат "ru = 'Налог на прибыль, региональный бюджет'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НалогНаПрибыль_ФедеральныйБюджет" Тогда
		Возврат "ru = 'Налог на прибыль, федеральный бюджет'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НалогНаПрибыль_НалоговыйАгент" Тогда
		Возврат "ru = 'Налог на прибыль при исполнении обязанностей налогового агента'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДС" Тогда
		Возврат "ru = 'НДС'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДС_ВвозимыеТовары" Тогда
		Возврат "ru = 'НДС при импорте товаров из ЕАЭС'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДС_НалоговыйАгент" Тогда
		Возврат "ru = 'НДС при исполнении обязанностей налогового агента'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДФЛ" Тогда
		Возврат "ru = 'НДФЛ при исполнении обязанностей налогового агента'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДФЛ_ИП" Тогда
		Возврат "ru = 'НДФЛ индивидуального предпринимателя '"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ПрочиеНалогиИСборы" Тогда
		Возврат "ru = 'Прочие налоги и сборы'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ТорговыйСбор" Тогда
		Возврат "ru = 'Торговый сбор'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ТранспортныйНалог" Тогда
		Возврат "ru = 'Транспортный налог организаций'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "УСН_Доходы" Тогда
		Возврат "ru = 'Налог при УСН (доходы)'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "УСН_ДоходыМинусРасходы" Тогда
		Возврат "ru = 'Налог при УСН (доходы минус расходы)'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "УСН_МинимальныйНалог" Тогда
		Возврат "ru = 'Минимальный налог УСН'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ПФРПоСуммарномуТарифу" Тогда
		Возврат "ru = 'ПФР - по суммарному тарифу с 1 января 2014 года'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ПФРСтраховаяЧасть" Тогда
		Возврат "ru = 'Страховые взносы в ПФР'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ПФРНакопительнаяЧасть" Тогда
		Возврат "ru = 'Страховые взносы в ПФР, накопительная часть (до 2014 г.)'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ПФРДополнительныйТарифЛЭ" Тогда
		Возврат "ru = 'Взносы на выплату доплат к пенсии членам летных экипажей'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ПФРДополнительныйТарифШахтеры" Тогда
		Возврат "ru = 'Взносы в ПФР (дополнительный тариф шахтеров)'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ПФРЗаЗанятыхНаПодземныхИВредныхРаботах" Тогда
		Возврат "ru = 'Взносы в ПФР (дополнительный тариф за занятых на работах с вредными условиями труда)'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах" Тогда
		Возврат "ru = 'Взносы в ПФР (дополнительный тариф за занятых на работах с тяжелыми условиями труда)'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ПФРСПревышения" Тогда
		Возврат "ru = 'ПФР - с базы сверх предельной величины, с 1 января 2016 года'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ФФОМС" Тогда
		Возврат "ru = 'Страховые взносы в ФФОМС'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ТФОМС" Тогда
		Возврат "ru = 'ТФОМС'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ФСС" Тогда
		Возврат "ru = 'Страховые взносы в ФСС'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ФССНС" Тогда
		Возврат "ru = 'Страховые взносы от несчастных случаев'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДФЛДоходыКонтрагентов" Тогда
		Возврат "ru = 'НДФЛ с доходов контрагентов'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДФЛДивиденды" Тогда
		Возврат "ru = 'НДФЛ с дивидендов'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДФЛДивидендыСотрудникам" Тогда
		Возврат "ru = 'НДФЛ с дивидендов работникам'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДФЛДоначисленныйПоРезультатамПроверки" Тогда
		Возврат "ru = 'НДФЛ, доначисленный по результатам проверки'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДФЛПередачаЗадолженностиВНалоговыйОрган" Тогда
		Возврат "ru = 'НДФЛ передача задолженности в налоговый орган'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДФЛПрочиеРасчетыСПерсоналом" Тогда
		Возврат "ru = 'НДФЛ с доходов по прочим операциям с персоналом'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДФЛСПревышения" Тогда
		Возврат "ru = 'НДФЛ с доходов свыше предельной величины, исчисленный налоговым агентом'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДФЛДивидендыСПревышения" Тогда
		Возврат "ru = 'НДФЛ с дивидендов свыше предельной величины'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ПФРДоПредельнойВеличины" Тогда
		Возврат "ru = 'ПФР - с базы до предельной величины'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "НДС_ОбратноеОбложение" Тогда
		Возврат "ru = 'НДС налогового агента по отдельным видам товаров'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "АУСН" Тогда
		Возврат "ru = 'Налог при АУСН'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "СтраховыеВзносыЕдиныйТариф" Тогда
		Возврат "ru = 'Страховые взносы по единому тарифу'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ОПСИностранныхРаботников" Тогда
		Возврат "ru = 'Взносы на пенсионное страхование иностранных работников'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ОМСИностранныхРаботников" Тогда
		Возврат "ru = 'Взносы на медицинское страхование иностранных работников'"; // @НСтр-1
	ИначеЕсли ИмяПредопределенного = "ОССИностранныхРаботников" Тогда
		Возврат "ru = 'Взносы на социальное страхование иностранных работников'"; // @НСтр-1
	КонецЕсли;
КонецФункции

// Получить ссылку предопределенного элемента.
// 
// Параметры:
//  ИмяПредопределенного - Строка - Имя предопределенного элемента
// 
// Возвращаемое значение:
//  СправочникСсылка.ВидыНалоговВзносов - Получить ссылку предопределенного элемента
Функция ПолучитьСсылкуПредопределенногоЭлемента(ИмяПредопределенного) Экспорт

	СправочникОбъект = Справочники.ВидыНалоговВзносов.СоздатьЭлемент();
	ИменаПредопределенных = СправочникОбъект.Метаданные().ПолучитьИменаПредопределенных();
	ЕстьПредопределенный = Ложь;
	Для Каждого Элемент Из ИменаПредопределенных Цикл
		Если НРег(Элемент) = НРег(ИмяПредопределенного) Тогда 
			ЕстьПредопределенный = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ЕстьПредопределенный Тогда
		Возврат Справочники.ВидыНалоговВзносов[ИмяПредопределенного];
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВидыНалоговВзносов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыНалоговВзносов КАК ВидыНалоговВзносов
	|ГДЕ
	|	НРЕГ(ВидыНалоговВзносов.ИдентификаторЭлемента) = &Идентификатор";
	Запрос.УстановитьПараметр("Идентификатор", НРег(ИмяПредопределенного));
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Ссылка = Выборка.Ссылка;
	Иначе
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.ВидыНалоговВзносов");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Наименование = НаименованиеПредопределенногоЭлемента(ИмяПредопределенного); // Строка
			//@skip-check bsl-nstr-string-literal-format
			СправочникОбъект.Наименование = НСтр(Наименование, ОбщегоНазначения.КодОсновногоЯзыка());
			СправочникОбъект.ИдентификаторЭлемента = ИмяПредопределенного;
			СправочникОбъект.Записать();
			Ссылка = СправочникОбъект.Ссылка;
			ЗафиксироватьТранзакцию();
		Исключение

			ОтменитьТранзакцию();

			ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось записать предопределенный элемент справочника ""Виды налогов и взносов"" %1:
					|%2';"),
				ИмяПредопределенного,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ВызватьИсключение ТекстИсключения;
		КонецПопытки;
	КонецЕсли;
	
	Возврат Ссылка;
КонецФункции

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьКлиентСервер.ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьКлиентСервер.ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
КонецПроцедуры

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка, Метаданные.Справочники.ВидыНалоговВзносов);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
КонецПроцедуры

#КонецЕсли

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращает имена блокируемых реквизитов для механизма блокирования реквизитов БСП.
// 
// Возвращаемое значение:
// 	Массив - имена блокируемых реквизитов:
//		* БлокируемыйРеквизит - Строка - Имя блокируемого реквизита.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("ПлательщикНалога");
	
	Возврат Результат;
	
КонецФункции

//++ Локализация

Функция КБКПоТипуНалога(ТипНалога)
	
	КодБК = "";
	Если ТипНалога = Справочники.ВидыНалоговВзносов.ЕдиныйНалоговыйПлатеж Тогда
		КодБК = "18201061201010000510";	// ЕдиныйНалоговыйПлатеж
	
	// ++ НЕ УТ
	
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ЕНВД Тогда
		КодБК = "18210502010020000110";	// ЕНВД
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ЕСХН Тогда
		КодБК = "18210503010010000110";	// ЕСХН
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.НалогНаИмущество Тогда
		КодБК = "18210602010020000110";	// НалогНаИмущество
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.НалогНаПрибыль_РегиональныйБюджет Тогда
		КодБК = "18210101012020000110";	// НалогНаПрибыль_РегиональныйБюджет
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.НалогНаПрибыль_ФедеральныйБюджет Тогда
		КодБК = "18210101011010000110";	// НалогНаПрибыль_ФедеральныйБюджет
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.НДС Тогда
		КодБК = "18210301000010000110";	// НДС
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.НДС_НалоговыйАгент Тогда
		КодБК = "18210301000010000110";	// НДС
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.НДС_ВвозимыеТовары Тогда
		КодБК = "18210401000010000110";	// НДС_ВвозимыеТовары
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.НДФЛ Тогда
		КодБК = "18210102010010000110";	// НДФЛ
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.НДФЛСПревышения Тогда
		КодБК = "18210102080010000110";	// НДФЛ_ДоходыСвышеПредельнойВеличины, НДФЛ_ИП_НалоговаяБазаСвышеПредельнойВеличины
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.НДФЛДивиденды Тогда
		КодБК = "18210102130010000110";	// НДФЛ_Дивиденды
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.НДФЛДивидендыСПревышения Тогда
		КодБК = "18210102140010000110";	// НДФЛ_Дивиденды_ДоходыСвышеПредельнойВеличины
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.НДФЛ_ИП Тогда
		КодБК = "18210102010010000110";	// НДФЛ
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ПФРДополнительныйТарифЛЭ Тогда
		КодБК = "18210208000060000160";	// ДополнительныеВзносы_ПФР_ЛетныеЭкипажи
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ПФРДополнительныйТарифШахтеры Тогда
		КодБК = "18210209000060000160";	// ДополнительныеВзносы_ПФР_Шахтеры
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах Тогда
		КодБК = "18210204010010000160";	// ДополнительныеВзносы_ПФР_ВредныеУсловия
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах Тогда
		КодБК = "18210204020010000160";	// ДополнительныеВзносы_ПФР_ТяжелыеУсловия
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ПФРНакопительнаяЧасть Тогда
		КодБК = "18210202020060000160";	// СтраховыеВзносы_ПФР_НакопительнаяЧасть
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ПФРСтраховаяЧасть Тогда
		КодБК = "18210214010060000160";	// СтраховыеВзносы_ПФР_СтраховаяЧасть
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ТорговыйСбор Тогда
		КодБК = "18210505010020000110";	// ТорговыйСбор
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ТранспортныйНалог Тогда
		КодБК = "18210604011020000110";	// ТранспортныйНалог
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.УСН_Доходы Тогда
		КодБК = "18210501011010000110";	// УСН_Доходы
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.УСН_ДоходыМинусРасходы Тогда
		КодБК = "18210501021010000110";	// УСН_ДоходыМинусРасходы
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.УСН_МинимальныйНалог Тогда
		КодБК = "18210501050010000110";	// УСН_МинимальныйНалог
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ФСС Тогда
		КодБК = "18210214020060000160";	// СтраховыеВзносы_ФСС
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ФССНС Тогда
		КодБК = "79710212000060000160";	// СтраховыеВзносы_ФСС_НСиПЗ
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ФФОМС Тогда
		КодБК = "18210214030080000160";	// СтраховыеВзносы_ФФОМС
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.СтраховыеВзносыЕдиныйТариф Тогда
		КодБК = "18210201000010000160";	// СтраховыеВзносыЕдиныйТариф
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ОПСИностранныхРаботников Тогда
		КодБК = "18210215010060000160";	// СтраховыеВзносы_ОПС_ИностранныеРаботники
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ОМСИностранныхРаботников Тогда
		КодБК = "18210215030080000160";	// СтраховыеВзносы_ОМС_ИностранныеРаботники
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ОССИностранныхРаботников Тогда
		КодБК = "18210215020060000160";	// СтраховыеВзносы_ОСС_ИностранныеРаботники

	//-- НЕ УТ
	
	КонецЕсли;
	
	Возврат КодБК;
	
КонецФункции

//++ НЕ УТ

Функция КодПодвидаДоходов(КБК, ТипНалога, ВидНалоговогоОбязательства, Знач Период)
	
	ЭтоНалог    = ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.Налог;
	ЭтоПени     = Перечисления.ВидыПлатежейВГосБюджет.ЭтоПени(ВидНалоговогоОбязательства);
	ЭтоПроценты = Перечисления.ВидыПлатежейВГосБюджет.ЭтоПроценты(ВидНалоговогоОбязательства);
	ЭтоШтраф    = Перечисления.ВидыПлатежейВГосБюджет.ЭтоШтраф(ВидНалоговогоОбязательства);
	
	ЭтоВзносыСвышеПредела = ВидНалоговогоОбязательства = Перечисления.ВидыПлатежейВГосБюджет.ВзносыСвышеПредела;
	
	ЭтоТаможенныйПлатеж = ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяТаможеннымиОрганами(КБК);
	КодПодвидаДоходаПоУмолчанию = "0000";
	ТекущийКодПодвидаДохода = ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК);
	ЭтоОсобыйКодПодвидаДохода = СтрНачинаетсяС(ТекущийКодПодвидаДохода, "0")
		И Не СтрЗаканчиваетсяНа(ТекущийКодПодвидаДохода, "00");
	
	СоответствиеСтраховыхДо2023 = СоответствиеКБКСтраховыхВзносов2022ГодаВ2023Году();
	КБКПриведенный = ПлатежиВБюджетКлиентСервер.ШаблонКБК(КБК);
	ВидСтраховогоВзносаДо2023 = СоответствиеСтраховыхДо2023[КБКПриведенный];
	ЭтоСтарыйСтраховойВзносДо2023 = ЗначениеЗаполнено(ВидСтраховогоВзносаДо2023);
	
	Если ТипНалога = Справочники.ВидыНалоговВзносов.ФФОМС Тогда
		
		Если ПлатежиВБюджетКлиентСервер.ДействуетФедеральныйЗакон263ФЗ(Период) // дата платежа больше 2023 года
			И Не ЭтоСтарыйСтраховойВзносДо2023 Тогда
			КодПодвидаДоходов = "1001";
		Иначе
			КодПодвидаДоходов = "1013";
			Если ЭтоПени Тогда
				КодПодвидаДоходов = "2013";
			ИначеЕсли ЭтоПроценты Тогда
				КодПодвидаДоходов = "2213";
			ИначеЕсли ЭтоШтраф Тогда
				КодПодвидаДоходов = "3013";
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.СтраховыеВзносыЕдиныйТариф Тогда
		
		// Пени и проценты по этим фиксированным страховым не предусмотрены
		Если ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		Иначе
			КодПодвидаДоходов = "1";
		КонецЕсли;
		
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ОПСИностранныхРаботников
		Или ТипНалога = Справочники.ВидыНалоговВзносов.ОМСИностранныхРаботников
		Или ТипНалога = Справочники.ВидыНалоговВзносов.ОССИностранныхРаботников Тогда
		
		// Пени и проценты по этим фиксированным страховым не предусмотрены
		Если ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		Иначе
			КодПодвидаДоходов = "1";
		КонецЕсли;
		
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ПФРЗаЗанятыхНаПодземныхИВредныхРаботах
		ИЛИ ТипНалога = Справочники.ВидыНалоговВзносов.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботах Тогда
		
		КодПодвидаДоходаДоИзменения = ПлатежиВБюджетКлиентСервер.КодПодвидаДоходов(КБК);
		НеЗависитОтСпецОценки = СтрЗаканчиваетсяНа(КодПодвидаДоходаДоИзменения, "10");
		Если ЭтоНалог Тогда
			КодПодвидаДоходов = "102";
		ИначеЕсли ЭтоПени Тогда
			Если НеЗависитОтСпецОценки Тогда
				КодПодвидаДоходов = "211";
			Иначе
				КодПодвидаДоходов = "210";
			КонецЕсли;
		ИначеЕсли ЭтоПроценты Тогда
			Если НеЗависитОтСпецОценки Тогда
				КодПодвидаДоходов = "221";
			Иначе
				КодПодвидаДоходов = "220";
			КонецЕсли;
		ИначеЕсли ЭтоШтраф Тогда
			Если НеЗависитОтСпецОценки Тогда
				КодПодвидаДоходов = "301";
			Иначе
				КодПодвидаДоходов = "300";
			КонецЕсли
		Иначе
			КодПодвидаДоходов = "101";
		КонецЕсли;
		
	ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ПФРДополнительныйТарифЛЭ
		ИЛИ ТипНалога = Справочники.ВидыНалоговВзносов.ПФРДополнительныйТарифШахтеры Тогда
		
		Если ЭтоНалог Тогда
			КодПодвидаДоходов = "1";
		ИначеЕсли ЭтоПени Или ЭтоПроценты Тогда
			КодПодвидаДоходов = "21";
		ИначеЕсли ЭтоШтраф Тогда
			КодПодвидаДоходов = "3";
		КонецЕсли;
		
	ИначеЕсли ЭтоТаможенныйПлатеж И ЭтоАвансовыйПлатежТаможня(КБК) Тогда
		
		КодПодвидаДоходов = "1000";
		
	ИначеЕсли ПлатежиВБюджетКлиентСервер.ЭтоЕдиныйНалоговыйПлатеж(КБК) Тогда
		
		КодПодвидаДоходов = КодПодвидаДоходаПоУмолчанию;
		
	ИначеЕсли (ЭтоПениПоВременнойНетрудоспособности(КБК) Или ЭтоШтрафыПоПлатежу(КБК))
		И ТекущийКодПодвидаДохода = КодПодвидаДоходаПоУмолчанию Тогда
		
		КодПодвидаДоходов = ТекущийКодПодвидаДохода;
		
	ИначеЕсли ЭтоПроцентыПоПлатежу(КБК) Тогда
		
		КодПодвидаДоходов = "2";
		
	Иначе
		
		КодПодвидаДоходов = "1";
		Если ТипНалога = Справочники.ВидыНалоговВзносов.ПФРСтраховаяЧасть Тогда
			
			Если ПлатежиВБюджетКлиентСервер.ДействуетФедеральныйЗакон263ФЗ(Период) // дата платежа больше 2023 года
				И Не ЭтоСтарыйСтраховойВзносДо2023 Тогда
				КодПодвидаДоходов = "1001";
			Иначе
				Если ЭтоНалог Тогда
					КодПодвидаДоходов = "101";
				ИначеЕсли ЭтоПени Тогда
					КодПодвидаДоходов = "211";
				ИначеЕсли ЭтоПроценты Тогда
					КодПодвидаДоходов = "221";
				ИначеЕсли ЭтоШтраф Тогда
					КодПодвидаДоходов = "301";
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ТипНалога = Справочники.ВидыНалоговВзносов.ФСС Тогда
			
			Если ПлатежиВБюджетКлиентСервер.ДействуетФедеральныйЗакон263ФЗ(Период) Тогда
				Если ТекущийКодПодвидаДохода = "1101" Тогда
					КодПодвидаДоходов = ТекущийКодПодвидаДохода;
				Иначе
					КодПодвидаДоходов = "1001";
				КонецЕсли;
			Иначе
				Если ЭтоНалог Тогда
					КодПодвидаДоходов = "101";
				ИначеЕсли ЭтоПени Тогда
					КодПодвидаДоходов = "211";
				ИначеЕсли ЭтоПроценты Тогда
					КодПодвидаДоходов = "221";
				ИначеЕсли ЭтоШтраф Тогда
					КодПодвидаДоходов = "301";
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ЭтоОсобыйКодПодвидаДохода Тогда
			
			КодПодвидаДоходов = ТекущийКодПодвидаДохода;
			
		Иначе
			
			Если ЭтоВзносыСвышеПредела Тогда
				КодПодвидаДоходов = "12";
			ИначеЕсли ЭтоПени ИЛИ ЭтоПроценты Тогда
				КодПодвидаДоходов = "2";
				Если ПлатежиВБюджетКлиентСервер.ПениПроцентыРаздельно(КБК, Период) Тогда
					КодПодвидаДоходов = ?(ЭтоПени, "21", "22");
				КонецЕсли;
			ИначеЕсли ЭтоШтраф Тогда
				КодПодвидаДоходов = "3";
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КодПодвидаДоходов) Тогда
		КодПодвидаДоходов = "1"; // по умолчанию
	КонецЕсли;
	
	РасположениеЭлемента = ПлатежиВБюджетКлиентСервер.РасположениеЭлементаКБК("КодПодвидаДоходов");
	Для Счетчик = СтрДлина(КодПодвидаДоходов) + 1 По РасположениеЭлемента.Длина Цикл
		КодПодвидаДоходов = КодПодвидаДоходов + "0";
	КонецЦикла;
	
	Возврат КодПодвидаДоходов;
	
КонецФункции

Функция ЭтоАвансовыйПлатежТаможня(КБК)
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = "1100900001";
	
КонецФункции

Функция ЭтоПениПоВременнойНетрудоспособности(КБК)
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = "1161902001";
	
КонецФункции

Функция ЭтоПроцентыПоПлатежу(КБК)
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = "1162000001";
	
КонецФункции

Функция ЭтоШтрафыПоПлатежу(КБК)
	
	Возврат ПлатежиВБюджетКлиентСервер.КодВидаДоходов(КБК) = "1162102006";
	
КонецФункции

Функция СоответствиеКБКСтраховыхВзносов2022ГодаВ2023Году()
	
	Результат = Новый Соответствие;
	
	Результат.Вставить("18210202010060000160", Справочники.ВидыНалоговВзносов.ПФРСтраховаяЧасть);
	Результат.Вставить("18210202101080000160", Справочники.ВидыНалоговВзносов.ФФОМС);
	Результат.Вставить("18210202090070000160", Справочники.ВидыНалоговВзносов.ФСС);
	Результат.Вставить("18210201020010000160", Справочники.ВидыНалоговВзносов.ФСС);
	Результат.Вставить("39310202050070000160", Справочники.ВидыНалоговВзносов.ФССНС);
	
	Возврат Результат;
	
КонецФункции

//-- НЕ УТ

Функция ШаблонСравненияКБК(КБК)
	
	ГраницыАдминистратораКБК  = ПлатежиВБюджетКлиентСервер.РасположениеЭлементаКБК("КодГлавногоАдминистратора");
	ГраницыПеременнойЧастиКБК = ПлатежиВБюджетКлиентСервер.РасположениеЭлементаКБК("КодПодвидаДоходов");
	ШаблонКБК = СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов("_", ГраницыАдминистратораКБК.Длина)
		+ Сред(КБК, ГраницыАдминистратораКБК.Начало + ГраницыАдминистратораКБК.Длина);
	ШаблонКБК = Лев(ШаблонКБК, ГраницыПеременнойЧастиКБК.Начало - 1)
		+ СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов("_", ГраницыПеременнойЧастиКБК.Длина)
		+ Сред(ШаблонКБК, ГраницыПеременнойЧастиКБК.Начало + ГраницыПеременнойЧастиКБК.Длина);
	
	Возврат ШаблонКБК;
	
КонецФункции

//-- Локализация

#Область ОбновлениеИнформационнойБазы

// Заполняет сведения об обработчиках обновления.
// 
// Параметры:
//  Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.ВидыНалоговВзносов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.20.33";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("0b42d254-ccd3-453d-91c0-a524c04084ac");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.ВидыНалоговВзносов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	
	МассивКомментариев = Новый Массив(); // Массив из Строка
	МассивКомментариев.Добавить(НСтр("ru = 'Очищает предопределенные элементы справочника ""Виды налогов и взносов для WE""'"));
	Обработчик.Комментарий = СтрСоединить(МассивКомментариев, " ");
	
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	
	Читаемые = Новый Массив; // Массив из Строка
	Читаемые.Добавить(Метаданные.Справочники.ВидыНалоговВзносов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив; // Массив из Строка
	Изменяемые.Добавить(Метаданные.Справочники.ВидыНалоговВзносов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив; // Массив из Строка
	Блокируемые.Добавить(Метаданные.Справочники.ВидыНалоговВзносов.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = СоздатьЭлемент().Метаданные().ПолноеИмя();
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Данные.Ссылка
	|ИЗ
	|	Справочник.ВидыНалоговВзносов КАК Данные
	|ГДЕ
	|	Данные.ИдентификаторЭлемента = """"";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры,
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

// Обработчик обновления.
// 
// Параметры:
//  Параметры - Структура - параметры обработчика:
//   * ВерсияПодсистемыНаНачалоОбновления - Строка - версия подсистемы.
//   * ИмяОбработчика - Строка - имя обработчика.
//   * ОбновляемыеДанные - Структура.
//   * ОбработкаЗавершена - Булево, Неопределено - признак завершения обработки.
//   * Очередь - Число - очередь.
//   * ПрогрессВыполнения - Структура:
//     ** ВсегоОбъектов - Число - всего обработано объектов.
//     ** ОбработаноОбъектов - Число - обработано объектов.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		
		Параметры.ОбработкаЗавершена =
			ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
		
	КонецЕсли;
	
	ОбъектовОбработано = 0;
	
	Для Каждого СтрокаТаблицы Из ОбновляемыеДанные Цикл
		
		ОписаниеИзменений = Новый Массив; // Массив из Строка
		ОписаниеИзменений.Добавить(НСтр("ru = 'При обновлении системы выполнены следующие операции:'"));
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаТаблицы.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			СправочникОбъект = СтрокаТаблицы.Ссылка.ПолучитьОбъект(); // СправочникОбъект.ВидыНалоговВзносов
			
			Если СправочникОбъект <> Неопределено Тогда
				СправочникОбъект.ИдентификаторЭлемента = СправочникОбъект.ИмяПредопределенныхДанных;
			КонецЕсли;
			ПредставлениеСсылки = Строка(СправочникОбъект);
			
			Если СправочникОбъект.Модифицированность() Тогда
				
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
				
			Иначе
				
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(СтрокаТаблицы.Ссылка);
				
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(
				ИнформацияОбОшибке(),
				СтрокаТаблицы.Ссылка,
				ПредставлениеСсылки);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
