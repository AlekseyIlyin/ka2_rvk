#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Текущее состояние набора помещается во временную таблицу "ВзаиморасчетыПоЗаймамСотрудникамПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый", ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, ДЕНЬ) КАК Период,
	|	Таблица.Организация КАК Организация,
	|	Таблица.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Таблица.ДоговорЗайма КАК ДоговорЗайма,
	|	Таблица.СуммаЗайма КАК СуммаЗайма
	|ПОМЕСТИТЬ ВзаиморасчетыПоЗаймамСотрудникамПередЗаписью
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыПоЗаймамСотрудникам КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И Таблица.СуммаЗайма <> 0
	|	И (НЕ &ЭтоНовый)";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаИзменений.Период) КАК Период,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.ФизическоеЛицо,
	|	ТаблицаИзменений.ДоговорЗайма
	|ПОМЕСТИТЬ ВзаиморасчетыПоЗаймамСотрудникамИзменениеТелаЗайма
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.Организация КАК Организация,
	|		Таблица.ФизическоеЛицо КАК ФизическоеЛицо,
	|		Таблица.ДоговорЗайма КАК ДоговорЗайма,
	|		Таблица.СуммаЗайма КАК СуммаЗайма
	|	ИЗ
	|		ВзаиморасчетыПоЗаймамСотрудникамПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(Таблица.Период, ДЕНЬ),
	|		Таблица.Организация,
	|		Таблица.ФизическоеЛицо,
	|		Таблица.ДоговорЗайма,
	|		-Таблица.СуммаЗайма
	|	ИЗ
	|		РегистрНакопления.ВзаиморасчетыПоЗаймамСотрудникам КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор
	|		И Таблица.СуммаЗайма <> 0) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.ФизическоеЛицо,
	|	ТаблицаИзменений.ДоговорЗайма
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.СуммаЗайма) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВзаиморасчетыПоЗаймамСотрудникамПередЗаписью";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет()[0];
	Выборка = РезультатЗапроса.Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ВзаиморасчетыПоЗаймамСотрудникамИзменениеТелаЗайма", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
