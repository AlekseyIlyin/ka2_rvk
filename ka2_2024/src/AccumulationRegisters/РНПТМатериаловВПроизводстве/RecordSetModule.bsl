#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	ОбщегоНазначенияУТ.СвернутьНаборЗаписей(ЭтотОбъект, Истина);
	
	Если ОбменДанными.Загрузка
		Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.ПартияПроизводства КАК ПартияПроизводства,
	|	Таблица.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Таблица.Организация КАК Организация,
	|	Таблица.НомерГТД КАК НомерГТД,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.Количество
	|		ИНАЧЕ -Таблица.Количество
	|	КОНЕЦ КАК КоличествоПередЗаписью,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.КоличествоПоРНПТ
	|		ИНАЧЕ -Таблица.КоличествоПоРНПТ
	|	КОНЕЦ КАК КоличествоПоРНПТПередЗаписью
	|ПОМЕСТИТЬ ДвиженияРНПТМатериаловВПроизводствеПередЗаписью
	|ИЗ
	|	РегистрНакопления.РНПТМатериаловВПроизводстве КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.ПартияПроизводства КАК ПартияПроизводства,
	|	ТаблицаИзменений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.НомерГТД КАК НомерГТД,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение,
	|	СУММА(ТаблицаИзменений.КоличествоПоРНПТИзменение) КАК КоличествоПоРНПТИзменение
	|ПОМЕСТИТЬ ДвиженияРНПТМатериаловВПроизводствеИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ПартияПроизводства КАК ПартияПроизводства,
	|		Таблица.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Таблица.Организация КАК Организация,
	|		Таблица.НомерГТД КАК НомерГТД,
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение,
	|		Таблица.КоличествоПоРНПТПередЗаписью КАК КоличествоПоРНПТИзменение
	|	ИЗ
	|		ДвиженияРНПТМатериаловВПроизводствеПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ПартияПроизводства КАК ПартияПроизводства,
	|		Таблица.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Таблица.Организация КАК Организация,
	|		Таблица.НомерГТД КАК НомерГТД,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.Количество
	|			ИНАЧЕ Таблица.Количество
	|		КОНЕЦ КАК КоличествоИзменение,
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -Таблица.КоличествоПоРНПТ
	|			ИНАЧЕ Таблица.КоличествоПоРНПТ
	|		КОНЕЦ КАК КоличествоПоРНПТИзменение
	|	ИЗ
	|		РегистрНакопления.РНПТМатериаловВПроизводстве КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор
	|	) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ПартияПроизводства,
	|	ТаблицаИзменений.АналитикаУчетаНоменклатуры,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.НомерГТД
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|		ИЛИ СУММА(ТаблицаИзменений.КоличествоПоРНПТИзменение) < 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияРНПТМатериаловВПроизводствеПередЗаписью
	|";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет()[0]; // РезультатЗапроса
	Выборка = РезультатЗапроса.Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияРНПТМатериаловВПроизводствеИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
