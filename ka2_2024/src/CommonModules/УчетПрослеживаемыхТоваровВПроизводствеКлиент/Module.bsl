
#Область ПрограммныйИнтерфейс

#Область СозданиеНаОсновании

// Оформить перенос РНПТ материалов на продукцию на основании.
// 
// Параметры:
//  МассивСсылок - Массив Из ДокументСсылка
//  ПараметрыВыполнения - см. ПодключаемыеКомандыКлиент.ПараметрыВыполненияКоманды
//
Процедура ОформитьПереносРНПТМатериаловНаПродукцию(МассивСсылок, ПараметрыВыполнения) Экспорт
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("ОбщийМодуль.УчетПрослеживаемыхТоваровВПроизводствеКлиент.ОформитьПереносРНПТМатериаловНаПродукцию");
	
	ОчиститьСообщения();
	
	ДлительнаяОперация =
		УчетПрослеживаемыхТоваровВПроизводствеВызовСервера.ОформитьПереносРНПТМатериаловНаПродукциюВФоне(
			МассивСсылок,
			ПараметрыВыполнения.Форма.УникальныйИдентификатор);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ПараметрыВыполнения.Форма);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ДлительнаяОперация,
		Новый ОписаниеОповещения("ОформитьПереносРНПТМатериаловНаПродукциюЗавершение", ЭтотОбъект),
		ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СозданиеНаОсновании

Процедура ОформитьПереносРНПТМатериаловНаПродукциюЗавершение(Результат, ДополнительныеСвойства) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(, Результат.КраткоеПредставлениеОшибки);
	Иначе
		
		РезультатВыполненияВФоне = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		
		ПараметрыСозданныхДокументов = РезультатВыполненияВФоне.ПараметрыСозданныхДокументов;
		Если Не ПараметрыСозданныхДокументов.Параметры = Неопределено Тогда
			НакладныеКлиент.ОткрытьФормуСозданныхДокументов(ПараметрыСозданныхДокументов, ЭтотОбъект);
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(РезультатВыполненияВФоне.СписокОшибок);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
