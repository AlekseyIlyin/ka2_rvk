
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Организация", Организация);
	Если НЕ Параметры.Свойство("ДатаОстатков", ДатаОстатков) Тогда
		ДатаОстатков = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если Параметры.Свойство("МножественныйВыбор") И Параметры.МножественныйВыбор Тогда
		МножественныйВыбор = Истина;
	КонецЕсли;
	
	ЗаполнитьТаблицуВыбора();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбранныеЗначения = Новый Соответствие;
	
	Для Каждого СтрокаТаблицы Из Налоги Цикл
		Если Не СтрокаТаблицы.Пометка Тогда Продолжить; КонецЕсли;
		Если ВыбранныеЗначения.Получить(СтрокаТаблицы.ТипНалога) = Неопределено Тогда
			ВыбранныеЗначения.Вставить(СтрокаТаблицы.ТипНалога, Новый Массив);
		КонецЕсли;
		ВыбранныеЗначения[СтрокаТаблицы.ТипНалога].Добавить(СтрокаТаблицы.СчетУчета);
	КонецЦикла;
	
	Если ВыбранныеЗначения.Количество() > 0 Тогда
		ОповеститьОВыборе(ВыбранныеЗначения);
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отметить(Команда)
	УстановитьПометку(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметку(Команда)
	УстановитьПометку(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТаблицуВыбора()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ПоддерживаемыеВидыНалоговВзносов = Новый Массив;
	ПоддерживаемыеВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.НДС);
	ПоддерживаемыеВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.НДС_НалоговыйАгент);
	ПоддерживаемыеВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.НалогНаПрибыль_РегиональныйБюджет);
	ПоддерживаемыеВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.НалогНаПрибыль_ФедеральныйБюджет);
	ПоддерживаемыеВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.ЗемельныйНалог);
	ПоддерживаемыеВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.ТранспортныйНалог);
	ПоддерживаемыеВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.НалогНаИмущество);
	ПоддерживаемыеВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.ТорговыйСбор);
	
	ПараметрыУчетнойПолитики = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
				"НастройкиУчетаУСН",
				Организация,
				ДатаОстатков,
				Ложь);
				
	Если ПараметрыУчетнойПолитики <> Неопределено Тогда

		Если ПараметрыУчетнойПолитики.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.Доходы Тогда 
			ПоддерживаемыеВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.УСН_Доходы);
		ИначеЕсли ПараметрыУчетнойПолитики.ОбъектНалогообложенияУСН = Перечисления.ОбъектыНалогообложенияПоУСН.ДоходыМинусРасходы Тогда 
			ПоддерживаемыеВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.УСН_ДоходыМинусРасходы);
			ПоддерживаемыеВидыНалоговВзносов.Добавить(Справочники.ВидыНалоговВзносов.УСН_МинимальныйНалог);
		КонецЕсли;
		
	КонецЕсли;
	Запрос.УстановитьПараметр("ПоддерживаемыеТипыНалогов", ПоддерживаемыеВидыНалоговВзносов);
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ТипыНалогов.Ссылка КАК ТипНалога,
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетУчета,
	|	ВЫБОР ТипыНалогов.Ссылка
	|		КОГДА ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.НалогНаПрибыль_ФедеральныйБюджет)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.УровниБюджетов.ФедеральныйБюджет)
	|		КОГДА ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.НалогНаПрибыль_РегиональныйБюджет)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.УровниБюджетов.РегиональныйБюджет)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК УровеньБюджета,
	|	ЛОЖЬ КАК Используется
	|ИЗ
	|	Справочник.ВидыНалоговВзносов КАК ТипыНалогов";
	ТипыНалогов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТаблицы Из ТипыНалогов Цикл
		
		СтрокаТаблицы.СчетУчета = РеглУчетСервер.СчетУчетаПоТипуНалога(СтрокаТаблицы.ТипНалога);
		СтрокаТаблицы.Используется = ЗначениеЗаполнено(СтрокаТаблицы.СчетУчета);
		
	КонецЦикла;
	
	СтрокаСчет68 = ТипыНалогов.Добавить();
	СтрокаСчет68.СчетУчета    = ПланыСчетов.Хозрасчетный.РасчетыПоНалогам;
	СтрокаСчет68.ТипНалога    = Справочники.ВидыНалоговВзносов.ПрочиеНалогиИСборы;
	СтрокаСчет68.Используется = Истина;
	
	СтрокаСчет69 = ТипыНалогов.Добавить();
	СтрокаСчет69.СчетУчета    = ПланыСчетов.Хозрасчетный.РасчетыПоСоциальномуСтрахованию;
	СтрокаСчет69.ТипНалога    = Справочники.ВидыНалоговВзносов.ПрочиеНалогиИСборы;
	СтрокаСчет69.Используется = Истина;
	
	СчетаИсключения = Новый Массив;
	СчетаИсключения.Добавить(ПланыСчетов.Хозрасчетный.ЕдиныйНалоговыйСчет);
	
	Запрос.УстановитьПараметр("ТипыНалогов", ТипыНалогов);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаОстатков", Новый Граница(ДатаОстатков, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("СчетаНалогов", ТипыНалогов.ВыгрузитьКолонку("СчетУчета"));
	Запрос.УстановитьПараметр("СчетаИсключения", СчетаИсключения);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.ТипНалога,
	|	Таблица.СчетУчета,
	|	Таблица.УровеньБюджета,
	|	Таблица.Используется
	|ПОМЕСТИТЬ ТипыНалогов
	|ИЗ &ТипыНалогов КАК Таблица
	|;
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСБюджетом)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОстатки.Субконто1) = ТИП(Перечисление.УровниБюджетов)
	|			ТОГДА ХозрасчетныйОстатки.Субконто1
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОстатки.Субконто2) = ТИП(Перечисление.УровниБюджетов)
	|			ТОГДА ХозрасчетныйОстатки.Субконто2
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОстатки.Субконто3) = ТИП(Перечисление.УровниБюджетов)
	|			ТОГДА ХозрасчетныйОстатки.Субконто3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК УровеньБюджета,
	|	СУММА(-ХозрасчетныйОстатки.СуммаОстаток) КАК Сумма
	|ПОМЕСТИТЬ НалогиКУплате
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаОстатков,
	|			Счет В ИЕРАРХИИ (&СчетаНалогов)
	|				И НЕ Счет В ИЕРАРХИИ (&СчетаИсключения),,
	|			Организация = &Организация
	|		) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Счет,
	|	ВЫБОР
	|		КОГДА ХозрасчетныйОстатки.Счет <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.РасчетыСБюджетом)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОстатки.Субконто1) = ТИП(Перечисление.УровниБюджетов)
	|			ТОГДА ХозрасчетныйОстатки.Субконто1
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОстатки.Субконто2) = ТИП(Перечисление.УровниБюджетов)
	|			ТОГДА ХозрасчетныйОстатки.Субконто2
	|		КОГДА ТИПЗНАЧЕНИЯ(ХозрасчетныйОстатки.Субконто3) = ТИП(Перечисление.УровниБюджетов)
	|			ТОГДА ХозрасчетныйОстатки.Субконто3
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|;
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ТипыНалогов.ТипНалога,
	|		ЗНАЧЕНИЕ(Справочник.ВидыНалоговВзносов.ПрочиеНалогиИСборы)) КАК ТипНалога,
	|	НалогиКУплате.Счет КАК СчетУчета,
	|	НалогиКУплате.Сумма
	|ИЗ
	|	НалогиКУплате КАК НалогиКУплате
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТипыНалогов КАК ТипыНалогов
	|		ПО НалогиКУплате.Счет= ТипыНалогов.СчетУчета
	|		И НалогиКУплате.УровеньБюджета = ТипыНалогов.УровеньБюджета
	|
	|ГДЕ
	|	ЕСТЬNULL(ТипыНалогов.Используется, ИСТИНА)";
	
	Налоги.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометку(Значение)
	
	Для Каждого СтрокаТаблицы Из Налоги Цикл
		СтрокаТаблицы.Пометка = Значение;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
