#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

&После("ОбработкаУдаленияПроведения")
Процедура усОбработкаУдаленияПроведения(Отказ)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	усОстаткиПоПартиям.Регистратор,
		|	усОстаткиПоПартиям.МоментВремени КАК МоментВремени
		|ИЗ
		|	РегистрНакопления.усОстаткиПоПартиям КАК усОстаткиПоПартиям
		|ГДЕ
		|	усОстаткиПоПартиям.Партия = &Партия
		|
		|УПОРЯДОЧИТЬ ПО
		|	МоментВремени";
	
	Запрос.УстановитьПараметр("Партия", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	НачатьТранзакцию();
	Попытка
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				Регистратор = ВыборкаДетальныеЗаписи.Регистратор;
				усУправлениеСебестоимостью.ДобавитьДвиженияОстаткиПоПартиям(Регистратор);
				усУправлениеПродажами.ДобавитьДвиженияПродажи(Регистратор);
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ОбщегоНазначения.СообщитьПользователю("Не удалось восстановить последовательность по причине: " + ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли