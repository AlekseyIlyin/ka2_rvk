#Область ПрограммныйИнтерфейс

Процедура УстановитьГраницуПоследовательности(Период) Экспорт
	
	Параметр = ПланыВидовХарактеристик.усПараметрыПодсистемы.ГраницаПоследовательности;
	
	НаборЗаписей = РегистрыСведений.усПараметрыПодсистемы.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Параметр.Установить(Параметр);
	Запись = НаборЗаписей.Добавить();
	Запись.Параметр = Параметр;
	Запись.Значение = Период;
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция ГраницаПоследовательности() Экспорт

	Результат = '00010101';

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	усПараметрыПодсистемы.Значение
		|ИЗ
		|	РегистрСведений.усПараметрыПодсистемы КАК усПараметрыПодсистемы
		|ГДЕ
		|	усПараметрыПодсистемы.Параметр = ЗНАЧЕНИЕ(ПланВидовХарактеристик.усПараметрыПодсистемы.ГраницаПоследовательности)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Результат = ВыборкаДетальныеЗаписи.Значение;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ВосстановитьПоследовательность() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СебестоимостьТоваровОбороты.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров.Обороты(&ДатаНачала,, Регистратор,) КАК СебестоимостьТоваровОбороты
		|
		|УПОРЯДОЧИТЬ ПО
		|	Регистратор.МоментВремени";
	
	Период = ГраницаПоследовательности();
	Запрос.УстановитьПараметр("ДатаНачала", Новый Граница(Период, ВидГраницы.Исключая));
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Регистратор = ВыборкаДетальныеЗаписи.Регистратор;
		НачатьТранзакцию();
		Попытка
			усУправлениеСебестоимостью.ДобавитьДвиженияОстаткиПоПартиям(Регистратор);
			усУправлениеПродажами.ДобавитьДвиженияПродажи(Регистратор);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ОбщегоНазначения.СообщитьПользователю("Не удалось восстановить последовательность по причине: " + ОписаниеОшибки());
			Прервать;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Функция ГраницаПоследовательностиРаньше(Период) Экспорт
	
	ГраницаПоследовательности = ГраницаПоследовательности();
	Результат = ГраницаПоследовательности < Период;
		
	Возврат Результат;
	
КонецФункции

Функция ЕстьДвиженияПосле(Период) Экспорт
	
	Результат = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	МАКСИМУМ(СебестоимостьТоваров.Период) КАК Период
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Период > Период;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти