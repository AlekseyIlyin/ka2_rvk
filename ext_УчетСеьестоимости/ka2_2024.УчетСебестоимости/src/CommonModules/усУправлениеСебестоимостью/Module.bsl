
#Область ПрограммныйИнтерфейс


// Добавить движения остатки по партиям.
//
// Параметры:
//  Регистратор - ДокументСсылка
//
// Возвращаемое значение:
//  Структура - Добавить движения остатки по партиям:
// * Отказ - Булево -
// * ТекстОшибки - Строка -
Функция ДобавитьДвиженияОстаткиПоПартиям(Регистратор) Экспорт

	Результат = Новый Структура();
	Результат.Вставить("Отказ", Ложь);
	Результат.Вставить("ТекстОшибки", "");

	НаборЗаписей = РегистрыНакопления.усОстаткиПоПартиям.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Записать();

	ТаблицаДвижений = ТаблицаДвижений(Регистратор);

	Если ЗначениеЗаполнено(ТаблицаДвижений) Тогда
		НаборЗаписей.Загрузить(ТаблицаДвижений);
		УстановитьГраницуПоследовательности(ТаблицаДвижений[0].Период);
	КонецЕсли;

	Попытка
		НаборЗаписей.Записать();

	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		усЖурналРегистрацииВызовСервера.ЗаписатьОшибку(ПодробноеПредставлениеОшибки, , ИмяСобытия());
		Результат.Отказ = Истина;
		Результат.ТекстОшибки = СтрШаблон("Ошибка записи движений Остатки по партиям (Учет себестоимости)" + Символы.ПС + "Тип регистратора: %1" + Символы.ПС + "По причине: %2",
			ТипЗнч(Регистратор),
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));

	КонецПопытки;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяСобытия()
	Возврат "Управление себестоимостью";
КонецФункции

Процедура УстановитьГраницуПоследовательности(Период)

	ГраницаПоследовательностиНарушена = ГраницаПоследовательностиНарушена(Период);

	Если Не ГраницаПоследовательностиНарушена Или
			(ГраницаПоследовательностиНарушена И Не ГраницаПоследовательностиРаньшеЭтогоРегистратора(Период)) Тогда

		усУправлениеПоследовательностью.УстановитьГраницуПоследовательности(Период);
	КонецЕсли;

КонецПроцедуры

Функция ГраницаПоследовательностиРаньшеЭтогоРегистратора(Регистратор)
	Результат = усУправлениеПоследовательностью.ГраницаПоследовательностиРаньше(Регистратор);
	Возврат Результат;
КонецФункции

Функция ГраницаПоследовательностиНарушена(Период)
	Возврат усУправлениеПоследовательностью.ЕстьДвиженияПосле(Период);
КонецФункции

Функция ТаблицаДвижений(Регистратор)

	НаборЗаписейУчетСебестоимости = РегистрыНакопления.СебестоимостьТоваров.СоздатьНаборЗаписей();
	НаборЗаписейУчетСебестоимости.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписейУчетСебестоимости.Прочитать();

	Колонки = "Период,ВидДвижения,АналитикаУчетаНоменклатуры,Партия,ИдентификаторСтроки,Количество,Стоимость";

	ТаблицаДвиженийУчетСебестоимости = НаборЗаписейУчетСебестоимости.Выгрузить(, Колонки);
	ТаблицаДвижений = ТаблицаДвиженийУчетСебестоимости.СкопироватьКолонки();

	Если ЗначениеЗаполнено(ТаблицаДвиженийУчетСебестоимости) Тогда

		ПерваяСтрока = НаборЗаписейУчетСебестоимости[0];

		Период = ПерваяСтрока.Период;
		МоментВремени = КонецДня(ПерваяСтрока.Период);

		ОтборСтрок = Новый Структура("ВидДвижения");

		// Списание
		ОтборСтрок.ВидДвижения = ВидДвиженияНакопления.Расход;
		ОстаткиПоПартиямСписание = ТаблицаДвиженийУчетСебестоимости.Скопировать(ОтборСтрок);
		ФормироватьСписаниеПоПартиям = ЗначениеЗаполнено(ОстаткиПоПартиямСписание);
		Если ФормироватьСписаниеПоПартиям Тогда
			Отказ = Ложь;
			ЗаблокироватьРесурсыРегистраОстаткиПоПартиям(ОстаткиПоПартиямСписание, Отказ);
			Если Отказ Тогда
				ВызватьИсключение "Не удалось сформировать движения, объект заблокирован: " + Регистратор;
			КонецЕсли;
			НоваяТаблицаСписания = НоваяТаблицаСписания(МоментВремени, Период, ОстаткиПоПартиямСписание);
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(НоваяТаблицаСписания, ТаблицаДвижений);
		КонецЕсли;

		// Поступление
		ОтборСтрок.ВидДвижения = ВидДвиженияНакопления.Приход;
		НоваяТаблицаПоступления = ТаблицаДвиженийУчетСебестоимости.Скопировать(ОтборСтрок);
		Если ЗначениеЗаполнено(НоваяТаблицаПоступления) Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(НоваяТаблицаПоступления, ТаблицаДвижений);
		КонецЕсли;

	КонецЕсли;

	Возврат ТаблицаДвижений;

КонецФункции

Процедура ЗаблокироватьРесурсыРегистраОстаткиПоПартиям(ОстаткиПоПартиямСписание, Отказ)
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.усОстаткиПоПартиям");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ОстаткиПоПартиямСписание;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("АналитикаУчетаНоменклатуры", "АналитикаУчетаНоменклатуры");
	//Попытка
		Блокировка.Заблокировать();
	//Исключение
		//Отказ = Истина;
	//КонецПопытки;

КонецПроцедуры

Функция НоваяТаблицаСписания(МоментВремени, Период, ТаблицаСписания)

	КонтролироватьОстаток = усПараметрыПодсистемыПовтИсп.ЗначениеПараметра(
		ПланыВидовХарактеристик.усПараметрыПодсистемы.КонтролироватьОстаток, Ложь);

	ТаблицаСписания.Свернуть("АналитикаУчетаНоменклатуры,ИдентификаторСтроки", "Количество");

	НоваяТаблицаСписания = ТаблицаСписания.СкопироватьКолонки();
	НоваяТаблицаСписания.Колонки.Добавить("Партия", Метаданные.ОпределяемыеТипы.ДокументПартии.Тип);
	НоваяТаблицаСписания.Колонки.Добавить("Стоимость", Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Любой)));
	НоваяТаблицаСписания.Колонки.Добавить("Период");
	НоваяТаблицаСписания.Колонки.Добавить("ВидДвижения");

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаСписания.АналитикаУчетаНоменклатуры,
		|	ТаблицаСписания.ИдентификаторСтроки,
		|	ТаблицаСписания.Количество
		|ПОМЕСТИТЬ ВТ_ТаблицаСписания
		|ИЗ
		|	&ТЗ_ТаблицаСписания КАК ТаблицаСписания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ТаблицаСписания.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ВТ_ТаблицаСписания.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	ус_ОстаткиПоПартиямОстатки.Партия,
		|	ВТ_ТаблицаСписания.Количество КАК Количество,
		|	ус_ОстаткиПоПартиямОстатки.КоличествоОстаток КАК КоличествоОстаток,
		|	ус_ОстаткиПоПартиямОстатки.СтоимостьОстаток КАК СтоимостьОстаток
		|ИЗ
		|	ВТ_ТаблицаСписания КАК ВТ_ТаблицаСписания
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.усОстаткиПоПартиям.Остатки(&МомВремени, АналитикаУчетаНоменклатуры В
		|			(ВЫБРАТЬ
		|				ВТ_ТаблицаСписания.АналитикаУчетаНоменклатуры
		|			ИЗ
		|				ВТ_ТаблицаСписания КАК ВТ_ТаблицаСписания)) КАК ус_ОстаткиПоПартиямОстатки
		|		ПО ВТ_ТаблицаСписания.АналитикаУчетаНоменклатуры = ус_ОстаткиПоПартиямОстатки.АналитикаУчетаНоменклатуры
		|ГДЕ
		|	ус_ОстаткиПоПартиямОстатки.КоличествоОстаток > 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	ус_ОстаткиПоПартиямОстатки.Партия.МоментВремени
		|ИТОГИ
		|	МАКСИМУМ(Количество) КАК Количество,
		|	СУММА(КоличествоОстаток),
		|	СУММА(СтоимостьОстаток)
		|ПО
		|	АналитикаУчетаНоменклатуры,
		|	ИдентификаторСтроки";

	Запрос.УстановитьПараметр("МомВремени", МоментВремени);
	Запрос.УстановитьПараметр("ТЗ_ТаблицаСписания", ТаблицаСписания);

	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаАналитикаУчетаНоменклатуры = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаАналитикаУчетаНоменклатуры.Следующий() Цикл
		ВыборкаИдентификаторСтроки = ВыборкаАналитикаУчетаНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаИдентификаторСтроки.Следующий() Цикл

			ОстатокКоличество = ВыборкаИдентификаторСтроки.Количество;
			ВыборкаПартии = ВыборкаИдентификаторСтроки.Выбрать();
			Пока ОстатокКоличество > 0 И ВыборкаПартии.Следующий() Цикл

				КоличествоСписания = Мин(ОстатокКоличество, ВыборкаПартии.КоличествоОстаток);
				СтоимостьСписания = ?(КоличествоСписания = ВыборкаПартии.КоличествоОстаток,
					ВыборкаПартии.СтоимостьОстаток,
					ВыборкаПартии.СтоимостьОстаток / ВыборкаПартии.КоличествоОстаток * КоличествоСписания);

				НоваяСтрокаСписания = НоваяТаблицаСписания.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСписания, ВыборкаПартии, "АналитикаУчетаНоменклатуры,ИдентификаторСтроки,Партия");
				НоваяСтрокаСписания.Количество = КоличествоСписания;
				НоваяСтрокаСписания.Стоимость = СтоимостьСписания;
				ОстатокКоличество = ОстатокКоличество - КоличествоСписания;
			КонецЦикла;

			Если КонтролироватьОстаток И ОстатокКоличество > 0 Тогда
				//@skip-check bsl-legacy-check-string-literal
				ВызватьИсключение СтрШаблон("Не удалось распределить партии по номенклатуре: %1 в количестве: %2",
					ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыборкаИдентификаторСтроки.АналитикаУчетаНоменклатуры, "Номенклатура"),
					ОстатокКоличество);
			КонецЕсли;

		КонецЦикла;
	КонецЦикла;

	НоваяТаблицаСписания.ЗаполнитьЗначения(Период, "Период");
	НоваяТаблицаСписания.ЗаполнитьЗначения(ВидДвиженияНакопления.Расход, "ВидДвижения");

	Возврат НоваяТаблицаСписания;

КонецФункции

#КонецОбласти
