////////////////////////////////////////////////////////////////////////////////
// Содержит события форм, вызываемые на сервере
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Процедура, вызываемая из одноименного обработчика события формы.
//
// Параметры:
//  Форма					- ФормаКлиентскогоПриложения - форма, из обработчика события которой происходит вызов процедуры.
//  Отказ					- Булево - признак отказа от создания формы.
//  СтандартнаяОбработка	- Булево - признак выполнения стандартной (системной) обработки события
//  ДополнительныеПараметры	- Структура - дополнительные параметры.
//
&После("ПриСозданииНаСервере")
Процедура усПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	Если Не усОбслуживаниеФормВызовСервера.усЭтоФормаДляОтображенияСтоимости(Форма.ИмяФормы) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеТиповЧисло = Метаданные.ОпределяемыеТипы.ДенежнаяСуммаЛюбогоЗнака.Тип;
	
	СписокДобавляемыхРеквизитов = Новый Массив;
	СписокДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы("усУчетнаяЦена", ОписаниеТиповЧисло, "Объект.Товары", "Учетная Цена"));
	СписокДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы("усМаржа", ОписаниеТиповЧисло, "Объект.Товары", "Маржа"));
	СписокДобавляемыхРеквизитов.Добавить(Новый РеквизитФормы("усПроцентМаржи", ОписаниеТиповЧисло, "Объект.Товары", "% Маржи"));
	Форма.ИзменитьРеквизиты(СписокДобавляемыхРеквизитов);
	
	Элементы = Форма.Элементы;

	Элемент = Элементы.Вставить("усУчетнаяЦена", Тип("ПолеФормы"), Элементы.Товары, Элементы.ТоварыЦена);
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "Объект.Товары.усУчетнаяЦена";
	Элемент.Ширина = 9;
	Элемент.ТолькоПросмотр = Истина;	
	
	Элемент = Элементы.Добавить("усМаржа", Тип("ПолеФормы"), Элементы.Товары);
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "Объект.Товары.усМаржа";
	Элемент.Ширина = 9;
	Элемент.ТолькоПросмотр = Истина;
	Элемент.ВыделятьОтрицательные = Истина;

	Элемент = Элементы.Добавить("усПроцентМаржи", Тип("ПолеФормы"), Элементы.Товары);
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "Объект.Товары.усПроцентМаржи";
	Элемент.Ширина = 6;
	Элемент.ТолькоПросмотр = Истина;
	Элемент.ВыделятьОтрицательные = Истина;
	
	ДанныеСебестоимостиПоСтрокам = усОбслуживаниеФормВызовСервера.усОбновитьСебестоимостьМаржу(Форма.Объект);
	Для Каждого ДанныеСебестоимости Из ДанныеСебестоимостиПоСтрокам Цикл
		СтрокаТовары = Форма.Объект.Товары.НайтиПоИдентификатору(ДанныеСебестоимости.ИдентификаторСтрокиСебестоимость);
		Если СтрокаТовары <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтрокаТовары, ДанныеСебестоимости);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

&После("ПриЧтенииНаСервере")
Процедура усПриЧтенииНаСервере(Форма, ТекущийОбъект, ДополнительныеПараметры) Экспорт
	
	Если усОбслуживаниеФормВызовСервера.усЭтоФормаДляОтображенияСтоимости(Форма.ИмяФормы) Тогда
		РеквизитыТаблицы = Форма.ПолучитьРеквизиты("Объект.Товары");
		Для Каждого Реквизит Из РеквизитыТаблицы Цикл
			Если Реквизит.Имя = "усУчетнаяЦена" Тогда
				
				ДанныеСебестоимостиПоСтрокам = усОбслуживаниеФормВызовСервера.усОбновитьСебестоимостьМаржу(Форма.Объект);
				Для Каждого ДанныеСебестоимости Из ДанныеСебестоимостиПоСтрокам Цикл
					СтрокаТовары = Форма.Объект.Товары.НайтиПоИдентификатору(ДанныеСебестоимости.ИдентификаторСтрокиСебестоимость);
					Если СтрокаТовары <> Неопределено Тогда
						ЗаполнитьЗначенияСвойств(СтрокаТовары, ДанныеСебестоимости);
					КонецЕсли;
				КонецЦикла;
				
				Прервать;		
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Серверная переопределяемая процедура, вызываемая из обработчика события элемента.
//
// Параметры:
//   Форма                   - ФормаКлиентскогоПриложения - форма, из которой происходит вызов процедуры.
//   Элемент                 - Строка           - имя элемента-источника события "При изменении".
//   ДополнительныеПараметры - Структура        - значения дополнительных параметров влияющих на обработку.
//
&После("ПриИзмененииЭлемента")
Процедура усПриИзмененииЭлемента(Форма, Элемент, ДополнительныеПараметры) Экспорт
	
	Если усОбслуживаниеФормВызовСервера.усЭтоФормаДляОтображенияСтоимости(Форма.ИмяФормы) Тогда
		усОбслуживаниеФормВызовСервера.усОбновитьСебестоимостьМаржу(Форма.Объект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти