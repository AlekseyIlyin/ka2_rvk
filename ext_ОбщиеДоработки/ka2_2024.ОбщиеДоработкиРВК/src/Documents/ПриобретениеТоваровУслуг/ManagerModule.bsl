#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура рвк_СформироватьПоРеализации(ДанныеЗаполнения) Экспорт

	ОбменДаннымиСервер.Пауза(3);

	Отказ =  Ложь;

	Реализация = ДанныеЗаполнения.Ссылка;

	РежимЗаписи = ДанныеЗаполнения.РежимЗаписи;

	ПриобретениеGUID = Документы.РеализацияТоваровУслуг.рвк_ПриобретениеИнтеркампани(ДанныеЗаполнения.Ссылка);
	Если ЗначениеЗаполнено(ПриобретениеGUID) Тогда
		Приобретение = ПолучитьСсылку(Новый УникальныйИдентификатор(ПриобретениеGUID));
		ПриобретениеОбъект = Неопределено;
		Если ЗначениеЗаполнено(Приобретение) Тогда
			ПриобретениеОбъект = Приобретение.ПолучитьОбъект();
		КонецЕсли;
		СсылкаЛиквидна = ПриобретениеОбъект <> Неопределено;
		Если СсылкаЛиквидна Тогда
			Если ДанныеЗаполнения.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				Если Не ПриобретениеОбъект.Проведен И ПриобретениеОбъект.ПометкаУдаления Тогда
					ПриобретениеОбъект.ПометкаУдаления = Ложь;
				КонецЕсли;
			ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.Запись И ПриобретениеОбъект.Проведен Тогда
				Попытка
					ПриобретениеОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				Исключение
					ТекстСообщения = СтрШаблон("Не удалось отменить проведение для Приобретения: %1 по причине: %2",
						Приобретение,
						ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					рвк_ЖурналРегистрацииВызовСервера.ЗаписатьОшибку("Интеркампани", ТекстСообщения, Реализация, Отказ);
					Возврат;
				КонецПопытки;
			КонецЕсли;
			Если ДанныеЗаполнения.ПометкаУдаления И Не ПриобретениеОбъект.ПометкаУдаления Тогда
				ПриобретениеОбъект.ПометкаУдаления = Истина;
			КонецЕсли;
		Иначе
			ПриобретениеОбъект = СоздатьДокумент();
		КонецЕсли;
	Иначе // Нет приобретения
		Если ДанныеЗаполнения.ПометкаУдаления Тогда
			Возврат;
		Иначе
			ПриобретениеОбъект = СоздатьДокумент();
		КонецЕсли;
	КонецЕсли;

	ПриобретениеОбъект.Заполнить(ДанныеЗаполнения.СоглашениеСПоставщиком);
	ПриобретениеОбъект.Дата = НачалоДня(ДанныеЗаполнения.Дата);
	ПриобретениеОбъект.ДатаВходящегоДокумента = ДанныеЗаполнения.Дата;
	ПриобретениеОбъект.НомерВходящегоДокумента = ДанныеЗаполнения.Номер;
	ПриобретениеОбъект.ЗакупкаПодДеятельность = Перечисления.ТипыНалогообложенияНДС.ПродажаПоПатенту;

	Склад = ПриобретениеОбъект.Склад;
	ТаблицаТоваров = ДанныеЗаполнения.ТаблицаТоваров;
	ТаблицаТоваров.ЗаполнитьЗначения(Склад, "Склад");

	ПриобретениеОбъект.Товары.Загрузить(ТаблицаТоваров);
	ПриобретениеОбъект.ЗаполнитьУсловияЗакупокПоСоглашению(Ложь);
	ПриобретениеОбъект.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаТолькоПоНакладным;
	ПриобретениеОбъект.Согласован = Истина;

//	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
//		ПриобретениеОбъект.ДополнительныеСвойства.Вставить("ОтключитьКонтрольПриПроведении", Истина);
//		ПриобретениеОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
//	КонецЕсли;

	//ПриобретениеОбъект.ДополнительныеСвойства.Вставить("ОтключитьКонтрольОстатковПоОрганизации", Истина);

	Попытка
		ПриобретениеОбъект.ДополнительныеСвойства.Вставить("ОтключитьКонтрольПриПроведении", Истина);
		ПриобретениеОбъект.Записать(РежимЗаписи);
		Приобретение = ПриобретениеОбъект.Ссылка;
		Документы.РеализацияТоваровУслуг.рвк_ОбновитьСсылкуНаПриобретение(Реализация, Приобретение);
		ТекстСообщения = СтрШаблон("Обновлено: %1 с режимом записи: %2",
			Приобретение,
			РежимЗаписи);
		рвк_ЖурналРегистрацииВызовСервера.ЗаписатьИнформацию("Интеркампани", ТекстСообщения, Реализация);

	Исключение
		ТекстСообщения = СтрШаблон("Не удалось записать: %1 с режимом записи: %2 по причине: %3",
			"" + Приобретение + Символы.ПС,
			"" + РежимЗаписи + Символы.ПС,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		рвк_ЖурналРегистрацииВызовСервера.ЗаписатьОшибку("Интеркампани", ТекстСообщения, Реализация, Отказ);
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;

КонецПроцедуры

//Процедура рвк_ПровестиДокумент(Реализация) Экспорт
//
//	ПриобретениеGUID = Документы.РеализацияТоваровУслуг.рвк_ПриобретениеИнтеркампани(Реализация);
//	Если Не ЗначениеЗаполнено(ПриобретениеGUID) Тогда
//		Возврат;
//	КонецЕсли;
//
//	Приобретение = ПолучитьСсылку(Новый УникальныйИдентификатор(ПриобретениеGUID));
//	Если Не ЗначениеЗаполнено(Приобретение) Тогда
//		Возврат;
//	КонецЕсли;
//
//	ПриобретениеОбъект = Приобретение.ПолучитьОбъект();
//	ПриобретениеОбъект.ДополнительныеСвойства.Вставить("ОтключитьКонтрольПриПроведении", Истина);
//	ПриобретениеОбъект.Записать(РежимЗаписиДокумента.Проведение);
//
//КонецПроцедуры

Функция рвк_НовыеДанныеЗаполнения() Экспорт

	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Ссылка");
	ДанныеЗаполнения.Вставить("ПометкаУдаления", Ложь);
	ДанныеЗаполнения.Вставить("Проведен", Истина);
	ДанныеЗаполнения.Вставить("ТаблицаТоваров");
	ДанныеЗаполнения.Вставить("Дата");
	ДанныеЗаполнения.Вставить("Номер");
	ДанныеЗаполнения.Вставить("СоглашениеСПоставщиком");
	ДанныеЗаполнения.Вставить("РежимЗаписи");

	Возврат ДанныеЗаполнения;

КонецФункции

#КонецОбласти

#КонецЕсли
