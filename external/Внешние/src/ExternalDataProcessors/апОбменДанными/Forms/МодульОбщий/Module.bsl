#Область ОписаниеПеременных

&НаКлиенте
Перем ГЭтоLinuxКлиент; // для ускорения работы функции ЭтоLinuxКлиент
&НаКлиенте
Перем ГМодульМТ; // общий клиентский модуль для работы с виртуальными объектами

// ОписаниеПеременных
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтррКонтекст = Новый Структура("ПутьКФорме,ВХОбщиеПараметры,СпрТоргТочки");
	ЗаполнитьЗначенияСвойств(СтррКонтекст, Параметры);
	
КонецПроцедуры

// ОбработчикиСобытийФормы
#КонецОбласти
 
#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункции_СовместимостьСПлатформой_8_3_5

// Разбивает строку на несколько строк по разделителю. Разделитель может иметь любую длину.
//
// Параметры:
//  Строка                 - Строка - текст с разделителями;
//  Разделитель            - Строка - разделитель строк текста, минимум 1 символ;
//  ПропускатьПустыеСтроки - Булево - признак необходимости включения в результат пустых строк.
//    Если параметр не задан, то функция работает в режиме совместимости со своей предыдущей версией:
//       т.е. для разделителя-пробела пустые строки не включаются в результат, для остальных разделителей пустые строки
//       включаются в результат.
//    Если параметр Строка не содержит значащих символов или не содержит ни одного символа (пустая строка), то в
//       случае разделителя-пробела результатом функции будет массив, содержащий одно значение "" (пустая строка), а
//       при других разделителях результатом функции будет пустой массив.
//  СокращатьНепечатаемыеСимволы - Булево - сокращать непечатаемые символы по краям каждой из найденных подстрок.
//
// Возвращаемое значение:
//  Массив Из Строка - массив строк.
&НаКлиентеНаСервереБезКонтекста 
Функция СтрРазделить_(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено, СокращатьНепечатаемыеСимволы = Ложь)
	
	Результат = Новый Массив;
	
	// Для обеспечения обратной совместимости.
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	//@skip-check use-non-recommended-method
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Если СокращатьНепечатаемыеСимволы Тогда
				Результат.Добавить(СокрЛП(Подстрока));
			Иначе
				Результат.Добавить(Подстрока);
			КонецЕсли;
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		//@skip-check use-non-recommended-method
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Если СокращатьНепечатаемыеСимволы Тогда
			Результат.Добавить(СокрЛП(Строка));
		Иначе
			Результат.Добавить(Строка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//   Булево
&НаКлиентеНаСервереБезКонтекста 
Функция СтрНачинаетсяС_(Строка, СтрокаПоиска)
	//@skip-check use-non-recommended-method
	Возврат Найти(Строка, СтрокаПоиска) = 1;
КонецФункции

// СлужебныеПроцедурыИФункции_СовместимостьСПлатформой_8_3_5
#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ОпределениеКонфигурации
// Аналогичные функции объявлены в модуле обработки (ПоколениеКонфигурации, СравнитьВерсии, ВерсияКонфигурацииСервер).

// Функция сравнивает идентификатор текущей конфигурации (в глобальной переменной гБазоваяКонфигурация) с проверяемым 
// идентификатором конфигурации (в параметре СтрКонфигурация).
// Возвращает Истина, если идентификатор текущей конфигурации соответствует идентификатору проверяемой конфигурации и 
// и операция сравнения версии (переданная вместе с идентификатором) верна.
// Формат идентификатора конфигурации: "P_V1.V2.V3.V4", где P - префикс конфигурации, V1-V4 - вресия конфигурации.
// Версия может быть неполной. Примеры: "УТ_11.1.15.120", "УТ_11.1"
// Параметры:
//   СтрКонфигурация - Строка - операция сравнеия и идентификатор сравниваемой конфигурации.
// Возвращаемое значение:
//   Булево
// Примечание: есть аналогичная функция в модуле обработки.	
&НаКлиенте
Функция ПоколениеКонфигурации(СтрКонфигурация) Экспорт 
	
	Если Не СтррКонтекст.Свойство("КэшСравненияКонфигураций") Тогда
	    СтррКонтекст.Вставить("КэшСравненияКонфигураций", Новый Соответствие);
		СтррВерсияКонфигурации = ВерсияКонфигурацииСервер();
		СтррКонтекст.Вставить("БазоваяКонфигурация", СтррВерсияКонфигурации.БазоваяКонфигурация); 
	КонецЕсли; 
	
	КэшСравненияКонфигураций = СтррКонтекст.КэшСравненияКонфигураций;	
	БазоваяКонфигурация 	 = СтррКонтекст.БазоваяКонфигурация;	
	
	Значение = КэшСравненияКонфигураций.Получить(СтрКонфигурация);
	
	Если Значение = Неопределено Тогда
		
		//@skip-check use-non-recommended-method
		Индекс1 = Найти(БазоваяКонфигурация, "_");
		//@skip-check use-non-recommended-method
		Индекс2 = Найти(СтрКонфигурация, "_");
		
		Если Индекс2 = 0 Тогда
			ВызватьИсключение("Функция ПоколениеКонфигурации(): указан параметр без префикса ""_""! Параметр = """ + СтрКонфигурация + """.");
		КонецЕсли; 
		
		Префикс1 = Лев(БазоваяКонфигурация, Индекс1);
		Префикс2 = Лев(СтрКонфигурация, Индекс2);
		
		ЗнакСравнения = "";
		Для Индекс = 1 По 2 Цикл
			Символ = Сред(Префикс2, Индекс, 1);
			Если КодСимвола(Символ) >= 60 И КодСимвола(Символ) <= 62 Тогда // знаки "<=>"
				ЗнакСравнения = ЗнакСравнения + Символ;
			КонецЕсли;
		КонецЦикла;
		
		Если ЗнакСравнения = "" Тогда
			ЗнакСравнения = "=";
		Иначе
			Префикс2 = Сред(Префикс2, СтрДлина(ЗнакСравнения) + 1);
		КонецЕсли;
		
		Если Префикс1 <> Префикс2 Тогда // сравниваем префиксы конфигураций
			
			Значение = Ложь;
			
		Иначе
			
			Сравнение = СравнитьВерсии(Сред(БазоваяКонфигурация, Индекс1+1), Сред(СтрКонфигурация, Индекс2+1));
			
			Если ЗнакСравнения = ">=" Тогда
				Значение = (Сравнение >= 0);
			ИначеЕсли ЗнакСравнения = "<=" Тогда
				Значение = (Сравнение <= 0);
			ИначеЕсли ЗнакСравнения = "<" Тогда
				Значение = (Сравнение < 0);
			ИначеЕсли ЗнакСравнения = ">" Тогда
				Значение = (Сравнение > 0);
			ИначеЕсли ЗнакСравнения = "=" Тогда
				Значение = (Сравнение = 0);
			Иначе
				ВызватьИсключение("Функция ПоколениеКонфигурации() - неизвестный оператор сравнения: " + ЗнакСравнения);
			КонецЕсли;
			
		КонецЕсли;
		
		КэшСравненияКонфигураций.Вставить(СтрКонфигурация, Значение);
		
	КонецЕсли;
		
	Возврат Значение;
	
КонецФункции

// Сравнивает две строки версий. Если передана не полная версия, то сравнивается только общая начальная часть версий.
//
// Параметры:
//  СтрокаВерсии1  - Строка - номер версии в формате РР.{П|ПП}.ЗЗ.СС.
//  СтрокаВерсии2  - Строка - второй сравниваемый номер версии.
//
// Возвращаемое значение:
//   Число   - больше 0, если СтрокаВерсии1 > СтрокаВерсии2; 0, если версии равны.
//
&НаКлиентеНаСервереБезКонтекста
Функция СравнитьВерсии(СтрокаВерсии1, СтрокаВерсии2)
	
	МВерсия1 = СтрРазделить_(?(ПустаяСтрока(СтрокаВерсии1), "0.0.0.0", СтрокаВерсии1), ".");
	МВерсия2 = СтрРазделить_(?(ПустаяСтрока(СтрокаВерсии2), "0.0.0.0", СтрокаВерсии2), ".");
	
	ВГраница = Мин(МВерсия1.Количество(), МВерсия2.Количество()) - 1;
	
	Результат = 0;
	Для Разряд = 0 По ВГраница Цикл
		Результат = Число(МВерсия1[Разряд]) - Число(МВерсия2[Разряд]);
		Если Результат <> 0 Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ВерсияКонфигурацииСервер()
	
	Возврат РеквизитФормыВЗначение("Объект").ВерсияКонфигурации();
	
КонецФункции

// СлужебныеПроцедурыИФункции_ОпределениеКонфигурации
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_ФайловыеОперацииФайловыеОперации

&НаКлиенте
Функция СоздатьПодкаталогиДанных(КаталогДанных) Экспорт

	СтррРезультат = Новый Структура("ЕстьОшибки,Сообщения", Ложь, Новый Массив);
	
	Если ПустаяСтрока(КаталогДанных) Тогда
		Текст = "Не указан каталог в параметре ""Каталог данных"".";
		ВывестиСообщение(СтррРезультат, Текст, Истина, "Объект.КаталогДанных");
		Возврат СтррРезультат;
	КонецЕсли;
	
	Каталог = СокрЛП(КаталогДанных);
	Если Не ПроверитьИСоздатьКаталог(Каталог) Тогда
		Текст = "Не удалось создать каталог, указанный в параметре ""Каталог данных"": " + Каталог;
		ВывестиСообщение(СтррРезультат, Текст, Истина, "Объект.КаталогДанных");
		Возврат СтррРезультат;
	КонецЕсли;
	
	//Результат = Истина;
	
	ПолучитьПодкаталогиДанныхСервер();
	
	КаталогСл = ДополнитьСлешВПуть(Каталог);
	
	Для Каждого Элемент Из СтррКонтекст.ПодкаталогиДанных Цикл
		ПодКаталог = КаталогСл + Элемент.Значение;
		Если Не ПроверитьИСоздатьКаталог(ПодКаталог) Тогда
			Текст = "Не удалось создать подкаталог назначения """ + Элемент.Ключ + """: " + ПодКаталог;
			ВывестиСообщение(СтррРезультат, Текст, Истина);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтррРезультат;
	
КонецФункции 

// Проверяет существование каталога, если он не существует, то создается новый.
&НаКлиенте
Функция ПроверитьИСоздатьКаталог(ПутьККаталогу)

	Если КаталогСуществует(ПутьККаталогу) Тогда
		Возврат Истина;
	Иначе
		//{{gi_240730 обернуто в Попытка / Исключение
		Попытка
			СоздатьКаталог(ПутьККаталогу);
			Возврат КаталогСуществует(ПутьККаталогу);
		Исключение
			Возврат Ложь;
		КонецПопытки;
		//}}gi_240730
	КонецЕсли;

КонецФункции

&НаКлиенте
Функция КаталогСуществует(ПутьККаталогу)
	
	//{{gi_240730 обернуто в Попытка / Исключение
	Попытка 	
		КаталогОбъект = Новый Файл(ПутьККаталогу);
		Возврат КаталогОбъект.Существует() И КаталогОбъект.ЭтоКаталог();
	Исключение
		Возврат Ложь;
	КонецПопытки;
	//}}gi_240730
	
КонецФункции

&НаКлиенте
Функция ФайлСуществует(ПутьКФайлу) Экспорт

	//{{gi_240730 обернуто в Попытка / Исключение
	Попытка 	
		ФайлОбъект = Новый Файл(ПутьКФайлу);
		Возврат ФайлОбъект.Существует();
	Исключение
		Возврат Ложь;
	КонецПопытки;
	//}}gi_240730
	
КонецФункции

&НаКлиенте
Функция ДополнитьСлешВПуть(Каталог)

	Слеш = ?(ЭтоLinuxКлиент(), "/", "\");
	
	Если Прав(Каталог, 1) <> Слеш Тогда
		Возврат Каталог + Слеш;
	Иначе
		Возврат Каталог;		
	КонецЕсли;

КонецФункции 

&НаСервере
Процедура ПолучитьПодкаталогиДанныхСервер()
	
	Если Не СтррКонтекст.Свойство("ПодкаталогиДанных") Тогда
		СтррКонтекст.Вставить("ПодкаталогиДанных", РеквизитФормыВЗначение("Объект").ПодкаталогиДанных());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ИмяПодкаталогаДанных(ИмяСвойства, Каталог = Неопределено) Экспорт
	
	Если Не СтррКонтекст.Свойство("ПодкаталогиДанных") Тогда
		ПолучитьПодкаталогиДанныхСервер();
	КонецЕсли;
	
	Результат = Неопределено;
	
	Если Не СтррКонтекст.ПодкаталогиДанных.Свойство(ИмяСвойства, Результат) Тогда
		ВызватьИсключение("Функция ИмяПодкаталогаДанных(), неизвестный параметр ИмяСвойства = " + ИмяСвойства);
	КонецЕсли;
	
	Если Каталог <> Неопределено Тогда
		Если ЭтоLinuxКлиент() Тогда
			Путь = СтрЗаменить(Каталог, "\", "/");
		Иначе
			Путь = СтрЗаменить(Каталог, "/", "\");
		КонецЕсли; 
		Результат = ДополнитьСлешВПуть(Путь) + Результат;
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

// СлужебныеПроцедурыИФункции_ФайловыеОперации
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_Прочие
&НаКлиенте
Процедура ВывестиСообщение(СтррРезультат, Текст, ЕстьОшибки = Неопределено, Путь = "")
	
	СтррСообщение = Новый Структура("Описание,Путь", Текст, Путь);
	СтррРезультат.Сообщения.Добавить(СтррСообщение);
	Если ЕстьОшибки <> Неопределено Тогда
		СтррРезультат.ЕстьОшибки = ЕстьОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте 
Функция ЭтоLinuxКлиент() Экспорт
	
	Если ГЭтоLinuxКлиент = Неопределено Тогда
		СистемнаяИнформация = Новый СистемнаяИнформация;
		ГЭтоLinuxКлиент = СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86
		             Или СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Linux_x86_64;
	КонецЕсли; 
				 
	Возврат ГЭтоLinuxКлиент;
	
КонецФункции

&НаКлиенте
Функция МодульМТ()

	Если ГМодульМТ = Неопределено Тогда
	    //@skip-check use-non-recommended-method
	    //@skip-check form-self-reference
	    ГМодульМТ = ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульОбщийМТ", СтррКонтекст, ЭтаФорма, "АгентПлюсМодульОбщийМТ"); // в СтррКонтекст есть заполненное свойство "ВХОбщиеПараметры"
	КонецЕсли; 
	
	Возврат ГМодульМТ;

КонецФункции 

&НаКлиенте
Процедура ПоказатьНаКартеОбъект(Ссылка, Описание) Экспорт
	МодульМТ().ПоказатьНаКартеОбъект(Ссылка, Описание);
КонецПроцедуры

// Процедура открывает форму элемента виртуального объекта (справочника, документа).
//  Параметры:
//		Параметр - Структура, Строка - системные свойства виртуального объекта (Структура) или вид виртульного объекта (Строка)
//		GUID 		 - УникальныйИдентификатор - идентификатор виртуального объекта.
&НаКлиенте
Процедура ВООткрытьФорму(Параметр, GUID = Неопределено) Экспорт
	МодульМТ().ВООткрытьФорму(Параметр, GUID);
КонецПроцедуры

&НаКлиенте
Процедура КомандаСправка(РазделСправки) Экспорт
	ОткрытьФормуОбработки("МодульСправки").ВнешнийВызовОткрытьСправку(РазделСправки);
КонецПроцедуры

// Функция открывает форму обработки. Обязательно открывать формы обработки только этой функцией!
// Параметры:
//   ИмяФормы - Строка
//   СтррПараметры - Структура - для передачи параметров в открываемую форму
//   Уникальность - Произвольный
//   Оповещение - ОписаниеОповещения
// Возвращаемое значение:
//   Форма 	
&НаКлиенте
Функция ОткрытьФормуОбработки(ИмяФормы, СтррПараметры = Неопределено, Уникальность = Неопределено, Оповещение = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(СтррКонтекст.ВХОбщиеПараметры) Тогда
		ВызватьИсключение("Модуль был загружен без параметра со свойством ""ВХОбщиеПараметры"" - используется для контроля уникальности запуска обработки.");
	КонецЕсли; 
	Если Не ЗначениеЗаполнено(СтррКонтекст.ПутьКФорме) Тогда
		ВызватьИсключение("Модуль был загружен без параметра со свойством ""ПутьКФорме"" - используется для открытия форм обрботки.");
	КонецЕсли; 
	Если СтррПараметры = Неопределено Тогда
		СтррПараметры = Новый Структура;
	КонецЕсли; 
	СтррПараметры.Вставить("ВХОбщиеПараметры", СтррКонтекст.ВХОбщиеПараметры); // свойство "ВХОбщиеПараметры" используется для контроля уникальности запуска обработки
	Возврат ОткрытьФорму(СтррКонтекст.ПутьКФорме + ИмяФормы, СтррПараметры, Неопределено, Уникальность,,, Оповещение);
	
КонецФункции

// Универсальная команда открытия форм (форм обработки и форм типовых объектов конфигурации).
// Параметры:
//   Элемент - Произвольный - Строка Или ЭлементФормы
//   Модуль - Форма
&НаКлиенте
Процедура КомандаВыполнить(Элемент, Модуль = Неопределено) Экспорт
	
	ЭлементИмя = ?(ТипЗнч(Элемент) = Тип("Строка"), Элемент, Элемент.Имя);
	
	//@skip-check use-non-recommended-method
	Если Найти(ЭлементИмя, "_N") <> 0 Тогда 
		// Отсекаем финальную часть строки, начиная с символа _N - т.к. на форме могут быть одинаковые по назначению
		// и поведению элементы с именами, к примеру, "КомандаУдалить" И "КомандаУдалить_N1".
		ЭлементИмя = Лев(ЭлементИмя, Найти(ЭлементИмя, "_N") - 1);
	КонецЕсли; 
	
	Если ЭлементИмя = "ПоказатьГлавнуюФорму" Тогда
		ОткрытьФормуОбработки("ГлавнаяФорма");
	ИначеЕсли ЭлементИмя = "ПоказатьПартнеров" Тогда
		СтррКонтекстМодуля = ?(Модуль <> Неопределено, Модуль.СтррКонтекст, СтррКонтекст);
		Если СтррКонтекстМодуля.Свойство("СпрТоргТочки") И ТипЗнч(СтррКонтекстМодуля.СпрТоргТочки) = Тип("Структура")  Тогда
			ВидСправочника = СтррКонтекстМодуля.СпрТоргТочки.ВидСправочника;
		Иначе
			ВидСправочника = "Партнеры";
		КонецЕсли; 
		ОткрытьФорму("Справочник." + ВидСправочника + ".ФормаСписка");
	ИначеЕсли ЭлементИмя = "Справка" Тогда
		ОткрытьФормуОбработки("МодульСправки").ВнешнийВызовОткрытьСправку(Модуль.ЭтаФорма.ИмяФормы);
	ИначеЕсли СтрНачинаетсяС_(ЭлементИмя, "Показать") Тогда
		ОткрытьФормуОбработки(Сред(ЭлементИмя, 9));
	ИначеЕсли СтрНачинаетсяС_(ЭлементИмя, "ДокументыУТ") Тогда
		ОткрытьФорму("Документ." + Сред(ЭлементИмя, 12) + ".ФормаСписка");
	ИначеЕсли СтрНачинаетсяС_(ЭлементИмя, "СправочникУТ") Тогда
		ОткрытьФорму("Справочник." + Сред(ЭлементИмя, 13) + ".ФормаСписка");
	ИначеЕсли СтрНачинаетсяС_(ЭлементИмя, "ОбработкаУТ") Тогда
		ОткрытьФорму("Обработка." + Сред(ЭлементИмя, 12) + ".Форма");
	ИначеЕсли СтрНачинаетсяС_(ЭлементИмя, "ОтчетУТ") Тогда
		ОткрытьФорму("Отчет." + Сред(ЭлементИмя, 8) + ".Форма");
	ИначеЕсли СтрНачинаетсяС_(ЭлементИмя, "Справочник") Или СтрНачинаетсяС_(ЭлементИмя, "Документы") Тогда
		ОткрытьФормуОбработки(ЭлементИмя);
	ИначеЕсли ЭлементИмя = "ПрикрепленныеФотографии" Тогда
	   ОткрытьФормуОбработки(ЭлементИмя, Модуль.СтррКонтекст);	
	Иначе
		ВызватьИсключение("Процедура КомандаВыполнить() - неизвестная команда Элемент.Имя = " + ЭлементИмя);
	КонецЕсли; 
	
КонецПроцедуры


// Функция возвращает последний элемент строки-выражения.
// Параметры:
//   СтрВыражение - Строка - строка-выражение
//   СимволРазделитель - Строка - символ-разделитель в строке-выражении.
// Возвращаемое значение:
//   Строка	
&НаКлиенте
Функция ПоследнийЭлементСтроки(СтрВыражение, СимволРазделитель = ".") Экспорт

	//@skip-check use-non-recommended-method
	Если Найти(СтрВыражение, СимволРазделитель) <> 0 Тогда 
		Результат = "";
		Индекс = СтрДлина(СтрВыражение);
		Символ = Сред(СтрВыражение, Индекс, 1);
		Пока Символ <> СимволРазделитель Цикл // в цикле ищем справа налево первый символ-разделительр
			Результат = Символ + Результат;
			Индекс = Индекс - 1;
			Символ = Сред(СтрВыражение, Индекс, 1);
		КонецЦикла;
	Иначе
		Результат = СтрВыражение;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// СлужебныеПроцедурыИФункции_Прочие
#КонецОбласти

// СлужебныеПроцедурыИФункции
#КонецОбласти
