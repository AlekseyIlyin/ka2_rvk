#Область ОписаниеПеременных

&НаКлиенте
Перем ГМодульК;  // общий клиентский модуль

// ОписаниеПеременных
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	//@skip-check structure-consructor-too-many-keys
	СтррКонтекст = Новый Структура("СтатусАгентов,СтатусНастройкиОбмена,СписокВсеВидыДокументов,СписокОчищаемыеСправочники,ПервыйЗапускАвтообмена");
	
	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.КонтекстФормыИнициализировать(СтррКонтекст, Параметры);
	
	ТекОбъект.ЗаполнитьСпособыОбмена(Элементы.СпособОбмена.СписокВыбора);
	СтррКонтекст.СписокВсеВидыДокументов 	= ТекОбъект.ПолучитьВсеВидыОбъектовДляМУ("Документ", Истина);
	СтррКонтекст.СписокОчищаемыеСправочники = ТекОбъект.ПолучитьВсеВидыОбъектовДляМУ("Справочник", Истина);

	СправочникАгентовПрочитатьИзНастроекГотовностьКОбмену();
	НастройкиОбменаПрочитатьИзНастроекГотовностьКОбмену();
	
	ПрочестьНастройкиСервер();
	Если Объект.ОчищатьДокументыПередЗагрузкой Тогда  // Этот флаг должен быть снят при открытии окна обработки обмена,
		Объект.ОчищатьДокументыПередЗагрузкой = Ложь; // чтобы в МУ не отправились команды удаления документов без ведома оператора.
		ТекОбъект = РеквизитФормыВЗначение("Объект"); // чтобы сохранился измененый реквизит, повторно реквизит формы Объект конфертируем в Значение.
		ТекОбъект.СохранитьЗначенияНастроекОбработки("ОчищатьДокументыПередЗагрузкой");
	КонецЕсли; 
	
	Объект.ВыбАгент = Справочники.Пользователи.ПустаяСсылка();
	ЗаполнитьДокументыОчистки();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Элементы.ГруппаКоманднаяПанель.ЦветФона = СтррКонтекст.Цвета.ФонРаздела;	
	
	ЗаполнитьВидыВыгрузки(Элементы.ВидВыгрузки.СписокВыбора);
	УстановитьДоступностьСтартовыхНомеров();
	ОбновитьФормуДляАвтообмена();
	ПодключитьАвтообмен();
	УстановитьДоступностьОчищаемыхДокументов();
	УстановитьДоступностьОчищаемыхСправочников();
	
	ОбновитьСтатусГотовности(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "АПНастройкиАгентовИзмененияЗаписаны" Тогда
		
		ОбновитьСтатусГотовности(Истина);
		ПеревыбратьАгента();
		
	ИначеЕсли ИмяСобытия = "АПНастройкиПараметровОбменаИзмененияЗаписаны" Тогда
		
		ПрочестьНастройкиСервер();
			
		НастройкиОбменаПрочитатьИзНастроекГотовностьКОбмену();
		ОбновитьСтатусГотовности(Ложь);
		ОбновитьФормуДляАвтообмена();
		ПодключитьАвтообмен();
		
	ИначеЕсли ИмяСобытия = "АПНастройкиСброшены" Тогда
		
		Закрыть();
		
	ИначеЕсли ИмяСобытия = "АППроверкаУникальностиЗапускаОбработкиОбмена" Тогда
		
		Если Параметр <> СтррКонтекст.ВХОбщиеПараметры Тогда // второй экземпляр обработки справшивает - уже открыта обработка или нет
			Оповестить("АПЗакрытьОбработкуОбменДанными", Параметр); // шлем событие закрытия обработки с конкретным ключем сеанса
		КонецЕсли; 
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	// запоминаем изменения, выбранные пользователем на форме
	ПопыткаЗаписатьИзмененияВХранилище();

КонецПроцедуры

// ОбработчикиСобытийФормы
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыгружатьСтартовыеНомераПриИзменении(Элемент)

	УстановитьМодифицированостьФормы(Истина);
	УстановитьДоступностьСтартовыхНомеров();

КонецПроцедуры

&НаКлиенте
Процедура ОчищатьСправочникиПередЗагрузкойПриИзменении(Элемент)

	УстановитьМодифицированостьФормы(Истина);	
	УстановитьДоступностьОчищаемыхСправочников();

КонецПроцедуры

&НаКлиенте
Процедура ОчищатьДокументыПередЗагрузкойПриИзменении(Элемент)

	УстановитьМодифицированостьФормы(Истина);
	УстановитьДоступностьОчищаемыхДокументов();

КонецПроцедуры

&НаКлиенте
Процедура СписокПомеченныхНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ИмяЭлемента = Элемент.Имя;	
	
	Если ИмяЭлемента = "СписокОчищаемыхСправочников" Тогда
		СписокДляПометки = СтррКонтекст.СписокОчищаемыеСправочники.Скопировать();
		ЗаголовокОкнаВыбора = НСтр("ru = 'Выберите очищаемые справочники'");
	ИначеЕсли ИмяЭлемента = "СписокОчищаемыхДокументов" Тогда
		СписокДляПометки = СтррКонтекст.СписокВсеВидыДокументов.Скопировать(); 
		ЗаголовокОкнаВыбора = НСтр("ru = 'Выберите очищаемые документы'");
	Иначе
		ВызватьИсключение("Неизвестный элемент формы для помеченных значений списка: " + ИмяЭлемента);
	КонецЕсли;
	
	Список = Объект[ИмяЭлемента];
	
	Для Каждого ЭлементСписка Из СписокДляПометки Цикл 
		ЭлементСписка.Пометка = (Неопределено <> Список.НайтиПоЗначению(ЭлементСписка.Значение)); 
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения("ОповещениеОтметкиЭлементовСписка", ЭтотОбъект, ИмяЭлемента);
	СписокДляПометки.ПоказатьОтметкуЭлементов(Оповещение, ЗаголовокОкнаВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбАгентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	Если СтррКонтекст.СтатусАгентов.КодСостояния = "СправочникПустой" Тогда
		СтррПараметры = Новый Структура("Предупреждение", "ПопыткаВыбораИзПустогоСправочника");
		МодульК().КомандаВыполнить("ПоказатьНастройкиАгентов");
		Оповестить("АПНастройкиАгентов_Оповещение", СтррПараметры);
	Иначе
		ПараметрыВыбора = Новый Структура("Пользователь,Ключ,КодСтатусаАгента", 
			Объект.ВыбАгент, "ОбменДаннымиВыбАгент", 1); // код статуса агента 1 - выбор агента только с зеленой галкой
		СтррПараметры = Новый Структура("ПараметрыВыбора", ПараметрыВыбора);
		Оповещение = Новый ОписаниеОповещения("ВыбАгентВыборЗавершение", ЭтотОбъект);
		//@skip-check form-self-reference
		МодульК().ОткрытьФормуОбработки("ВыборАгента", СтррПараметры, ЭтаФорма.КлючУникальности, Оповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбАгентОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Объект.ВыбАгент) Тогда
		МодульК().КомандаВыполнить("ПоказатьНастройкиАгентов");
		Оповестить("АПНастройкиАгентов_Оповещение", Новый Структура("Пользователь", Объект.ВыбАгент));
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьСтатусНажатие(Элемент)
	
	Если Элемент.Имя = "НадписьГотовностьАгентов" Тогда
		МодульК().КомандаВыполнить("ПоказатьНастройкиАгентов");
		Оповестить("АПНастройкиАгентов_Оповещение", Новый Структура("ВыделитьПервогоАгентаСОшибкой", Истина));				
	ИначеЕсли Элемент.Имя = "НадписьГотовностьНастроекОбмена" Тогда
		МодульК().КомандаВыполнить("ПоказатьНастройкиМодуля");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАвтообменПриИзменении(Элемент)
	
	ПопыткаЗаписатьИзмененияВХранилище();
	ОбновитьФормуДляАвтообмена();
	ПодключитьАвтообмен();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлятьДокументыПриЗагрузкеПриИзменении(Элемент)
	
	УстановитьМодифицированостьФормы(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогОбменаОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗапуститьПриложение("explorer.exe """ + Объект.КаталогОбмена + """");
	
КонецПроцедуры

&НаКлиенте
Процедура РасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	//@skip-check use-non-recommended-method
	Если Найти(НРег(НавигационнаяСсылка), ".htm") = 0 Тогда
		СтандартнаяОбработка = Ложь;
		Если НавигационнаяСсылка= "ОбщиеНастройкиОбмена_КаналОбмена" Тогда
			МодульК().КомандаВыполнить("ПоказатьНастройкиМодуля");
			Оповестить("АПНастройкиМодуля_ПоказатьНаФорме", Новый Структура("Закладка", "ГруппаОбменДанными"));
		ИначеЕсли НавигационнаяСсылка = "ОбщиеНастройкиОбмена_КаталогОбмена" Тогда
			МодульК().КомандаВыполнить("ПоказатьНастройкиМодуля");
			Оповестить("АПНастройкиМодуля_ПоказатьНаФорме", Новый Структура("Закладка", "ГруппаКаталоги"));
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

// ОбработчикиСобытийЭлементовШапкиФормы
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаСНачалаСтартовыеНомера(Команда)

	ТекстВопроса = НСтр("ru = 'Установить все стартовые номера в начало отсчета?'");
	ОписаниеОповещенияНумераторов = Новый ОписаниеОповещения("НумераторыСНачалаЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещенияНумераторов, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновитьСтартовыеНомера(Команда)

	ЗаполнитьТаблицуСтартовыхНомеров();

КонецПроцедуры

&НаКлиенте
Процедура КомандаВыгрузить(Команда)

	ОчиститьСообщения();
	ПопыткаЗаписатьИзмененияВХранилище();
	
	ЗапуститьВыгрузкуДанныхСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗагрузить(Команда)

	ОчиститьСообщения();
	ПопыткаЗаписатьИзмененияВХранилище();
	
	КоличествоЗагруженныхАгентов = ЗапуститьЗагрузкуДанныхНаСервере();
	Если КоличествоЗагруженныхАгентов > 0 Тогда
		ЧислоАгентов = ЧислоЦифрамиПредметИсчисленияПрописью(КоличествоЗагруженныхАгентов, НСтр("ru = 'агента, агентов, агентов'"));
		Текст = СтрШаблон_(НСтр("ru = 'Загружены новые данные от %1.'"), ЧислоАгентов);
		//@skip-check use-non-recommended-method
		Сообщить(Текст);
		Оповестить("АПЗагруженыНовыеДанныеИзМУ", Неопределено);  // оповещаем все формы о загрузке из МУ новых данных 
	КонецЕсли;

КонецПроцедуры

#Область ОбработчикиКомандФормы_НавигацияПоФормам

// ОбработчикиКомандФормы_НавигацияПоФормам
#КонецОбласти

&НаКлиенте
Процедура КомандаВыполнить(Команда)
	
	МодульК().КомандаВыполнить(Команда, ЭтотОбъект)
	
КонецПроцедуры

// ОбработчикиКомандФормы
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункции_СовместимостьСПлатформой_8_3_5

// Подставляет параметры в строку. Максимально возможное число параметров - 5.
// Параметры в строке задаются как %<номер параметра>. Нумерация параметров начинается с единицы.
//
// Параметры:
//  СтрокаПодстановки  - Строка - шаблон строки с параметрами (вхождениями вида "%ИмяПараметра");
//  Параметр1 - Строка - подставляемый параметр.
//  Параметр2 - Строка - подставляемый параметр.  
//  Параметр3 - Строка - подставляемый параметр.
//  Параметр4 - Строка - подставляемый параметр.
//  Параметр5 - Строка - подставляемый параметр.
//
// Возвращаемое значение:
//  Строка   - текстовая строка с подставленными параметрами.
//
//@skip-check method-too-many-params
&НаКлиентеНаСервереБезКонтекста
Функция СтрШаблон_(СтрокаПодстановки,
	Параметр1, Параметр2 = Неопределено, Параметр3 = Неопределено, Параметр4 = Неопределено, Параметр5 = Неопределено)
	
	Результат = СтрЗаменить(СтрокаПодстановки, "%1", Параметр1);
	Результат = СтрЗаменить(Результат, "%2", Параметр2);
	Результат = СтрЗаменить(Результат, "%3", Параметр3);
	Результат = СтрЗаменить(Результат, "%4", Параметр4);
	Результат = СтрЗаменить(Результат, "%5", Параметр5);
	
	Возврат Результат;
	
КонецФункции

// Разбивает строку на несколько строк по разделителю. Разделитель может иметь любую длину.
//
// Параметры:
//  Строка                 - Строка - текст с разделителями;
//  Разделитель            - Строка - разделитель строк текста, минимум 1 символ;
//  ПропускатьПустыеСтроки - Булево - признак необходимости включения в результат пустых строк.
//    Если параметр не задан, то функция работает в режиме совместимости со своей предыдущей версией:
//       т.е. для разделителя-пробела пустые строки не включаются в результат, для остальных разделителей пустые строки
//       включаются в результат.
//    Если параметр Строка не содержит значащих символов или не содержит ни одного символа (пустая строка), то в
//       случае разделителя-пробела результатом функции будет массив, содержащий одно значение "" (пустая строка), а
//       при других разделителях результатом функции будет пустой массив.
//  СокращатьНепечатаемыеСимволы - Булево - сокращать непечатаемые символы по краям каждой из найденных подстрок.
//
// Возвращаемое значение:
//  Массив Из Строка - массив строк.
&НаКлиентеНаСервереБезКонтекста 
Функция СтрРазделить_(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено, СокращатьНепечатаемыеСимволы = Ложь)
	
	Результат = Новый Массив;
	
	// Для обеспечения обратной совместимости.
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	//@skip-check use-non-recommended-method
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Если СокращатьНепечатаемыеСимволы Тогда
				Результат.Добавить(СокрЛП(Подстрока));
			Иначе
				Результат.Добавить(Подстрока);
			КонецЕсли;
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		//@skip-check use-non-recommended-method
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Если СокращатьНепечатаемыеСимволы Тогда
			Результат.Добавить(СокрЛП(Строка));
		Иначе
			Результат.Добавить(Строка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// СлужебныеПроцедурыИФункции_СовместимостьСПлатформой_8_3_5
#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ХранилищеНастроек

&НаСервере
Процедура СохранитьНастройкиСервер()

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	// запоминаем изменения, выбранные пользователем на форме в закладке БыстрыеНастройки.
	ИсключаемыеРеквизиты = "ВыбАгент,ИспользоватьАвтообмен,СпособОбмена,КаталогОбмена";
	ТекОбъект.СохранитьЗначенияНастроекОбработки(НастройкиФормы(ТекОбъект, ИсключаемыеРеквизиты));

КонецПроцедуры

&НаСервере
Процедура ПрочестьНастройкиСервер()

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.ВосстановитьЗначенияНастроекОбработки(НастройкиФормы(ТекОбъект));
	ЗначениеВРеквизитФормы(ТекОбъект, "Объект");
	Элементы.ГруппаНеавтономныйОбмен.Видимость 	= Не ТекОбъект.ИспользоватьАвтообмен;
	Элементы.КаталогОбмена.Видимость = (ТекОбъект.СпособОбмена = "КаталогОбмена" Или ТекОбъект.СпособОбмена = "АПСОД");

КонецПроцедуры

// Функция возвращает строку в виде списка названий реквизитов объекта для загрузки их значений из хранилища.
&НаСервере
Функция НастройкиФормы(ТекОбъект, ИсключаемыеРеквизиты = "ВыбАгент")

	СтрРеквизиты = "ИнтервалАвтообмена,";
	МИсключаемыеРеквизиты = СтрРазделить_(ИсключаемыеРеквизиты, ",", Ложь);
	Для Каждого ЭлементМетаданных Из ТекОбъект.Метаданные().Реквизиты Цикл
		Имя = ЭлементМетаданных.Имя;
		//@skip-check form-self-reference
		Если МИсключаемыеРеквизиты.Найти(Имя) = Неопределено И ЭтаФорма.Элементы.Найти(Имя) <> Неопределено Тогда 
			СтрРеквизиты = СтрРеквизиты + Имя + ","; 
		КонецЕсли;
	КонецЦикла;

	Возврат Лев(СтрРеквизиты, СтрДлина(СтрРеквизиты)-1);
	
КонецФункции 

&НаКлиенте
Процедура ПопыткаЗаписатьИзмененияВХранилище()

	// запоминаем изменения, выбранные пользователем на форме
	//@skip-check form-self-reference
	Если ЭтаФорма.Модифицированность Тогда
		СохранитьНастройкиСервер();
		УстановитьМодифицированостьФормы(Ложь);
	КонецЕсли;

КонецПроцедуры

// СлужебныеПроцедурыИФункции_ХранилищеНастроек
#КонецОбласти

#Область СлужебныеПроцедурыИФункции_ОбменДанными

&НаСервере
Процедура ЗапуститьВыгрузкуДанныхСервер() 

	РеквизитФормыВЗначение("Объект").ВыгрузитьДанные(Объект.ВыбАгент);

КонецПроцедуры

&НаСервере
Функция ЗапуститьЗагрузкуДанныхНаСервере()

	Если Объект.ИспользоватьАвтообмен Тогда
		Возврат РеквизитФормыВЗначение("Объект").ЗагрузитьДанные(Справочники.Пользователи.ПустаяСсылка());
	Иначе
		Возврат РеквизитФормыВЗначение("Объект").ЗагрузитьДанные(Объект.ВыбАгент);
	КонецЕсли;
	
КонецФункции

// СлужебныеПроцедурыИФункции_ОбменДанными
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_Нумераторы

&НаКлиенте
Процедура УстановитьДоступностьСтартовыхНомеров()

	Элементы.СтартовыеНомера.Видимость = Объект.ВыгружатьСтартовыеНомера;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуСтартовыхНомеров(УстановитьНумерациюСначала = Ложь)

	Объект.ТабСтартовыеНомера.Очистить();
	
	Если Объект.ВыбАгент.Пустая() Тогда
		//@skip-check object-deprecated
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не выбран пользователь.'"), , "ВыбАгент");
		Возврат;
	КонецЕсли;
	
	Нумераторы = НумераторыДокументов();
	Настройки = НастройкиДляТекущегоАгента();
	ВыбНастройкиАгента = Настройки.ВыбНастройкиАгента;

	Для Каждого ЭлементНумератор Из Нумераторы Цикл
		ДокументОбъект = Документы[ЭлементНумератор.ИмяДокумента].СоздатьДокумент();
		//@skip-check use-non-recommended-method
		ДокументОбъект.Дата = ТекущаяДата();
		//НомерУстановлен = Ложь;
		
		//в цикле ниже определяем первый свободный номер в ЦБД с префиксом для каждой фирмы
		//все полученные свободные номера заносятся в таблицу значений
		Для Каждого ОрганизацияСсылка Из Настройки.Организации Цикл
			НужныйПрефикс = СокрЛП(ВыбНастройкиАгента.Префикс);
			Если Не ДокументОбъект.Метаданные().Реквизиты.Найти("Организация") = Неопределено Тогда
				ДокументОбъект.Организация = ОрганизацияСсылка;
			КонецЕсли;

			ДокументОбъект.УстановитьНовыйНомер(НужныйПрефикс); 
			СтартНомерДок = ДокументОбъект.Номер;

			//Получили стартовый номер документа для нумератора с учетом префикса организации
			Стр = Объект.ТабСтартовыеНомера.Добавить();
			Стр.Наименование = ЭлементНумератор.Нумератор;
			Стр.Организация = ОрганизацияСсылка;
			Стр.СтартовыйНомер = СтартНомерДок;
			
			Префикс = Лев(СтартНомерДок, 7);
			Стр.Префикс = Префикс;
			Стр.СтартовыйНомер = Прав(СтартНомерДок, СтрДлина(СтартНомерДок)- СтрДлина(Префикс));
			Если УстановитьНумерациюСначала Тогда
				СформироватьСтартовыйНомер(Стр.СтартовыйНомер);
			КонецЕсли;
			
			//НомерУстановлен = Истина;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

// Функция устанавливает номер вида "00001", учитывая длину переданного в параметре номера.
&НаСервере
Процедура СформироватьСтартовыйНомер(НомерДокумента)

	ДлинаНомера = СтрДлина(НомерДокумента) - 2;
	НомерДокумента = "";
	Для индекс = 0 По ДлинаНомера Цикл
		НомерДокумента = НомерДокумента + "0";
	КонецЦикла;
	НомерДокумента = НомерДокумента + "1";

КонецПроцедуры

&НаСервере
Функция НумераторыДокументов()

	ТЗНумераторы = Новый ТаблицаЗначений;
	ТЗНумераторы.Колонки.Добавить("Нумератор");
	ТЗНумераторы.Колонки.Добавить("ИмяДокумента");
	ТЗНумераторы.Колонки.Добавить("ИдНумератора");
	ТЗНумераторы.Колонки.Добавить("ИдОбъекта");
	
	СтрокаТ = ТЗНумераторы.Добавить();
	СтрокаТ.Нумератор = "Заказ клиента";
	СтрокаТ.ИмяДокумента = "ЗаказКлиента";
	СтрокаТ.ИдНумератора = "293ff118-c039-4977-bc7b-1a73764237e0";
	СтрокаТ.ИдОбъекта = "e01e1f5c-d6e4-46e8-b923-3758b0d79bde";
	
	СтрокаТ = ТЗНумераторы.Добавить();
	СтрокаТ.Нумератор = "Реализация товаров и услуг";
	СтрокаТ.ИмяДокумента = "РеализацияТоваровУслуг";
	СтрокаТ.ИдНумератора = "bd6460d1-5b99-41fe-b86d-34fdd8ce98c0";
	СтрокаТ.ИдОбъекта = "7ffb418c-73c9-4883-91c5-827fa5145a3a";

	СтрокаТ = ТЗНумераторы.Добавить();
	СтрокаТ.ИмяДокумента = "ПриходныйКассовыйОрдер";
	СтрокаТ.Нумератор = "Приходный кассовый ордер";
	СтрокаТ.ИдНумератора = "b04b9dde-1868-4a80-9507-d66ecfeae942";
	СтрокаТ.ИдОбъекта = "749be2e0-9b00-4d7b-9d4d-88ca53327511";

	СтрокаТ = ТЗНумераторы.Добавить();
	СтрокаТ.ИмяДокумента = "РасходныйКассовыйОрдер";
	СтрокаТ.Нумератор = "Расходный кассовый ордер";
	СтрокаТ.ИдНумератора = "a695650b-7c77-4426-a33f-84d336a5c476";
	СтрокаТ.ИдОбъекта = "3890d435-96ba-4481-abc0-23782e15b32f";

	СтрокаТ = ТЗНумераторы.Добавить();
	СтрокаТ.ИмяДокумента = РеквизитФормыВЗначение("Объект").ВидДокументаПоступлениеТоваровУслуг();
	СтрокаТ.Нумератор = "Поступление товаров и услуг";
	СтрокаТ.ИдНумератора = "ad10c572-3439-49b7-8986-7dc61e267309";
	СтрокаТ.ИдОбъекта = "c9850d61-bc15-445a-8b1a-e328cc45ddae";

	Возврат ТЗНумераторы;

КонецФункции

&НаКлиенте
Процедура НумераторыСНачалаЗавершение(РезультатВопроса, ДополнительныеПараметры)Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТаблицуСтартовыхНомеров(Истина);

КонецПроцедуры

&НаСервере
Функция НастройкиДляТекущегоАгента()

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	
	Возврат ТекОбъект.НастройкиДляТекущегоАгента();
	
КонецФункции 

// СлужебныеПроцедурыИФункции_Нумераторы
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_Статусы

&НаСервере
Процедура СправочникАгентовПрочитатьИзНастроекГотовностьКОбмену()
	
	СтррКонтекст.СтатусАгентов = РеквизитФормыВЗначение("Объект").ГотовностьСправочников("НастройкиАгентов");
	
КонецПроцедуры

&НаСервере
Процедура НастройкиОбменаПрочитатьИзНастроекГотовностьКОбмену()
	
	СтррКонтекст.СтатусНастройкиОбмена = РеквизитФормыВЗначение("Объект").ГотовностьНастроекОбмена();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусГотовности(ПрочитатьИзНастроек)
	
	Если ПрочитатьИзНастроек Тогда
		СправочникАгентовПрочитатьИзНастроекГотовностьКОбмену();
	КонецЕсли;
	
	СтррСтатус = СтррКонтекст.СтатусАгентов;
	Если СтррСтатус.Готово Тогда
		Элементы.НадписьГотовностьАгентов.Видимость = Ложь;
	Иначе
		Элементы.НадписьГотовностьАгентов.Видимость = Истина;
		Элементы.НадписьГотовностьАгентов.Заголовок = СтррСтатус.Описание;
	КонецЕсли;
	
	СтррСтатус = СтррКонтекст.СтатусНастройкиОбмена;
	Если СтррСтатус.Готово Тогда
		Элементы.НадписьГотовностьНастроекОбмена.Видимость = Ложь;
	Иначе
		//ЕстьЗамечания = Истина;
		Элементы.НадписьГотовностьНастроекОбмена.Видимость = Истина;
		Элементы.НадписьГотовностьНастроекОбмена.Заголовок = СтррСтатус.Описание;
	КонецЕсли;

КонецПроцедуры

// СлужебныеПроцедурыИФункции_Статусы
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_Прочие

&НаКлиенте
Функция МодульК()

	Если ГМодульК = Неопределено Тогда
	    //@skip-check use-non-recommended-method
	    ГМодульК = ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульОбщий", СтррКонтекст);
	КонецЕсли; 
	
	Возврат ГМодульК;

КонецФункции 

&НаКлиенте
Процедура УстановитьДоступностьОчищаемыхСправочников()

	Элементы.СписокОчищаемыхСправочников.Доступность = Объект.ОчищатьСправочникиПередЗагрузкой;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьОчищаемыхДокументов()

	Элементы.СписокОчищаемыхДокументов.Доступность  	= Объект.ОчищатьДокументыПередЗагрузкой;
	Элементы.РежимыОчистки.Доступность 					= Объект.ОчищатьДокументыПередЗагрузкой;
	Элементы.НадписьПредупреждениеУдалениеДокументов.Видимость = Объект.ОчищатьДокументыПередЗагрузкой;
	
	Если Объект.ОчищатьДокументыПередЗагрузкой Тогда
	    Элементы.ГруппаДополнительно.Картинка  = Элементы.ДекорацияПредупреждлениеОчищатьДокументы.Картинка;
		Элементы.ГруппаДополнительно.Подсказка = НСтр("ru='Внимание! Включен режим удаления документов в МУ!'");
		Элементы.ДекорацияПредупреждлениеОчищатьДокументы.Видимость = Истина; 
	Иначе
		Элементы.ГруппаДополнительно.Картинка = Новый Картинка;
		Элементы.ГруппаДополнительно.Подсказка = НСтр("ru='Дополнительные настройки обмена'");
		Элементы.ДекорацияПредупреждлениеОчищатьДокументы.Видимость = Ложь; 
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВидыВыгрузки(Режимы)

	Режимы.Добавить("ВсеДанные", 		НСтр("ru='Все данные (с расчетом истории продаж)'"));
	Режимы.Добавить("ИсторияПродаж", 	НСтр("ru='Только история продаж'"));	
	Режимы.Добавить("ВсеДанныеБезИсторииПродаж", НСтр("ru='Все данные (без истории продаж)'"));

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументыОчистки()

	Если Объект.СписокОчищаемыхДокументов.Количество() = 0 Тогда
		ТекОбъект = РеквизитФормыВЗначение("Объект");
		Список = ТекОбъект.ПолучитьВсеВидыОбъектовДляМУ("Документ");
		Для Каждого Элемент Из Список Цикл
			Объект.СписокОчищаемыхДокументов.Добавить(Элемент.Значение, Элемент.Представление, Ложь);
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СуществуетАгент(Агент)
	
	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТЗн = ТекОбъект.ПрочитатьЗначениеНастройки("НастройкиАгентов");
	МСтроки = ТЗн.НайтиСтроки(Новый Структура("Пользователь", Агент));
	
	Возврат МСтроки.Количество() > 0;
	
КонецФункции

&НаКлиенте
Процедура ПеревыбратьАгента()
	
	Если ЗначениеЗаполнено(Объект.ВыбАгент) И Не СуществуетАгент(Объект.ВыбАгент) Тогда
		Объект.ВыбАгент = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает текстовое представление числа с единицей измерения в правильном склонении и числе.
//
// Параметры:
//  Число                       - Число  - любое целое число.
//  ПараметрыПредметаИсчисления - Строка - варианты написания единицы измерения в родительном падеже для одной,
//                                         для двух и для пяти единиц, разделитель - запятая.
//  ДобавлятьЧислоКРезультату   - Булево - выводить значение числа в начале текстового представления.
//
// Возвращаемое значение:
//  Строка - текстовое представление количества единиц, число записывается цифрами.
//
// Примеры:
//  ЧислоЦифрамиПредметИсчисленияПрописью(23,  "минуту,минуты,минут") = "23 минуты";
//  ЧислоЦифрамиПредметИсчисленияПрописью(15,  "минуту,минуты,минут") = "15 минут".
&НаКлиентеНаСервереБезКонтекста 
Функция ЧислоЦифрамиПредметИсчисленияПрописью(Знач Число, Знач ПараметрыПредметаИсчисления,
	Знач ДобавлятьЧислоКРезультату = Истина)
	
	Результат = ?(ДобавлятьЧислоКРезультату, Формат(Число, "ЧН=0") + " ", "");
	ПредставленияПредмета = Новый Массив;
	
	ПараметрыПредметаИсчисления = СтрРазделить_(ПараметрыПредметаИсчисления, ",");
	Для Каждого Параметр Из ПараметрыПредметаИсчисления Цикл
		ПредставленияПредмета.Добавить(СокрЛП(Параметр));
	КонецЦикла;
	
	Число = Число % 100;
	Если Число > 20 Тогда
		Число = Число % 10;
	КонецЕсли;
	
	Индекс = ?(Число = 1, 0, ?(Число > 1 И Число < 5, 1, 2));
	Результат = Результат + ПредставленияПредмета[Индекс];
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьФормуДляАвтообмена(Интерактивно = Ложь)
		
	Элементы.ГруппаНеавтономныйОбмен.Видимость    = Не Объект.ИспользоватьАвтообмен;
	Элементы.ВыгружатьСтартовыеНомера.Доступность = Не Объект.ИспользоватьАвтообмен;
	
	Если Объект.ИспользоватьАвтообмен И Объект.ВыгружатьСтартовыеНомера Тогда
		Объект.ВыгружатьСтартовыеНомера = Ложь;
		УстановитьДоступностьСтартовыхНомеров();
	КонецЕсли;
	
	Если НЕ Объект.ИспользоватьАвтообмен Тогда 
		Элементы.ИспользоватьАвтообмен.Заголовок = НСтр("ru = 'Использовать автообмен'");
	Иначе
		Интервал = ИнтервалАвтообмена();
		Если Интервал % 60 = 0 Тогда
			СтрИнтервал = ЧислоЦифрамиПредметИсчисленияПрописью(Интервал / 60, НСтр("ru = 'минуту, минуты, минут'"), Истина);
		Иначе
			СтрИнтервал = ЧислоЦифрамиПредметИсчисленияПрописью(Интервал, НСтр("ru = 'секунду, секунды, секунд'"), Истина);
		КонецЕсли;
		Элементы.ИспользоватьАвтообмен.Заголовок = СтрШаблон_(НСтр("ru = 'Автообмен каждые %1'"), СтрИнтервал);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ИнтервалАвтообмена()
	
	Возврат ?(Объект.ИнтервалАвтообмена = 0, 3 * 60, Объект.ИнтервалАвтообмена);
	
КонецФункции

&НаКлиенте
процедура ПодключитьАвтообмен()
	
	// первый запуск автообмена после открытия окна или установки флажка Автообмен
	Если Объект.ИспользоватьАвтообмен Тогда
		ПодключитьОбработчикОжидания("Автообмен", 5, Истина); //Однократно	
	Иначе
		ОтключитьОбработчикОжидания("Автообмен");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Автообмен() Экспорт
	
	//@skip-check object-deprecated
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("**************************************************************************");
	Если НЕ ПроверкаАвтообмен() Тогда
		Возврат;
	КонецЕсли;	
	
	//@skip-check use-non-recommended-method
	Текст = СтрШаблон_(НСтр("ru = '%1. Автообмен - проверка новых данных.' "), ТекущаяДата());
	//@skip-check object-deprecated
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
	
	КоличествоЗагруженныхАгентов = ЗапуститьЗагрузкуДанныхНаСервере();
	Если КоличествоЗагруженныхАгентов = 0 Тогда
		Текст = НСтр("ru = 'Новых данных не поступало.'");
	Иначе
		ЧислоАгентов = ЧислоЦифрамиПредметИсчисленияПрописью(КоличествоЗагруженныхАгентов, НСтр("ru = 'агента, агентов, агентов'"));
		Текст = СтрШаблон_(НСтр("ru = 'Загружены новые данные от %1.'"), ЧислоАгентов);
	КонецЕсли;
	//@skip-check object-deprecated
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);	
	
	Интервал = ИнтервалАвтообмена();
	ПодключитьОбработчикОжидания("Автообмен", Интервал, Истина); //Однократно
		
КонецПроцедуры

&НаКлиенте
Функция ПроверкаАвтообмен()
	
	Если НЕ Объект.ИспользоватьАвтообмен Тогда
		ОбновитьФормуДляАвтообмена();
		Возврат Ложь;
	КонецЕсли;
	
	Если ПустаяСтрока(Объект.КаталогОбмена) Тогда
		Текст = НСтр("ru = 'В настройках не задан ""Каталог обмена"". Автообмен не будет производиться!'");
		//@skip-check object-deprecated
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
		Объект.ИспользоватьАвтообмен = Ложь;
		ОбновитьФормуДляАвтообмена();
		Возврат Ложь;
	КонецЕсли;

	Возврат Истина;
КонецФункции

&НаКлиенте
Процедура УстановитьМодифицированостьФормы(Режим)
	
	//@skip-check form-self-reference
	ЭтаФорма.Модифицированность = Режим;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОтметкиЭлементовСписка(Список, ДополнительныеПараметры) Экспорт
	
	Если Список = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры = "СписокОчищаемыхДокументов" Или ДополнительныеПараметры = "СписокОчищаемыхСправочников" Тогда
	
		СписокПолучатель = Объект[ДополнительныеПараметры];
		СписокПолучатель.Очистить();
		
		Для Каждого Элемент из Список Цикл
			Если Элемент.Пометка Тогда					
				СписокПолучатель.Добавить(Элемент.Значение, Элемент.Представление);
			КонецЕсли;
		КонецЦикла;   			
		
		УстановитьМодифицированостьФормы(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбАгентВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("СправочникСсылка.Пользователи") Тогда
		Объект.ВыбАгент = Результат;
		Если Объект.ВыгружатьСтартовыеНомера И Объект.ТабСтартовыеНомера.Количество() <> 0 Тогда
			Объект.ТабСтартовыеНомера.Очистить();
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции_Прочие
#КонецОбласти 

// СлужебныеПроцедурыИФункции
#КонецОбласти
