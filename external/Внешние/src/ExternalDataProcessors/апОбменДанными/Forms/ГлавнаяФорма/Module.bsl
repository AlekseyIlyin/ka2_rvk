#Область ОписаниеПеременных

&НаКлиенте
Перем ГМодульК;  // общий клиентский модуль

// ОписаниеПеременных
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ТекОбъект = РеквизитФормыВЗначение("Объект");	
	
	//@skip-check structure-consructor-too-many-keys
	СтррКонтекст = Новый Структура("СтатусыСправочников,СтатусНастройкиОбмена,СтатусФоновогоЗадания,ОткрытиеПоНавигации,ЗакрытьФорму,Местоположение,ИнтерфейсТакси,ИсправитьРазмерыЭлементов,ВремяМестоположения");
	
	ТекОбъект.КонтекстФормыИнициализировать(СтррКонтекст, Параметры); 
	СтррКонтекст.Местоположение 	 = ТекОбъект.ПрочитатьЗначениеНастройки("МестоположениеДляИнформера"); // местоположение пользователя для виджета информера главного окна
	СтррКонтекст.ВремяМестоположения = ТекОбъект.ПрочитатьЗначениеНастройки("ВремяМестоположения"); // время определения последнего местоположения
	СтррКонтекст.Вставить("КлиентМетрикаИнформера", ТекОбъект.ПрочитатьЗначениеНастройки("КлиентМетрика"));
	
	// признаки автоматического отключения настроек "ВыгружатьХарактеристикиНоменклатуры", "ИспользоватьКонтрагентов", "ИспользоватьДоговорыИлиСоглашения"
	СтррКонтекст.Вставить("ОтключенаНастройкаВыгружатьХарактеристикиНоменклатуры", 	Ложь);
	СтррКонтекст.Вставить("ОтключенаНастройкаИспользоватьКонтрагентов", 			Ложь);
	СтррКонтекст.Вставить("ОтключенаНастройкаИспользоватьДоговорыИлиСоглашения", 	Ложь);
	// считываем настройки конфигурации для сравнения с настройками обмена модуля.
	КонстантаИспользоватьХарактеристикиНоменклатуры = Константы.ИспользоватьХарактеристикиНоменклатуры.Получить();
	КонстантаИспользоватьПартнеровИКонтрагентов		= Константы.ИспользоватьПартнеровИКонтрагентов.Получить();
	КонстантаИспользованиеСоглашенийСКлиентами		= Константы.ИспользованиеСоглашенийСКлиентами.Получить();
	КонстантаИспользоватьДоговорыСКлиентами			= Константы.ИспользоватьДоговорыСКлиентами.Получить();
	
	СтррЗначенияНастроек = ТекОбъект.ПрочитатьЗначенияНастроек("ИспользоватьХарактеристики,ИспользоватьКонтрагентов,ИспользоватьДоговорыИлиСоглашения", Ложь);
	
	Если НЕ КонстантаИспользоватьХарактеристикиНоменклатуры И СтррЗначенияНастроек.ИспользоватьХарактеристики Тогда 
		ТекОбъект.СохранитьЗначениеНастройки("ИспользоватьХарактеристики", Ложь); // отключаем настройку "ВыгружатьХарактеристикиНоменклатуры"
		СтррКонтекст.ОтключенаНастройкаВыгружатьХарактеристикиНоменклатуры = Истина;
	КонецЕсли;
	
	Если НЕ КонстантаИспользоватьПартнеровИКонтрагентов И СтррЗначенияНастроек.ИспользоватьКонтрагентов Тогда 
		ТекОбъект.СохранитьЗначениеНастройки("ИспользоватьКонтрагентов", Ложь); // отключаем настройку "ИспользоватьКонтрагентов"
		СтррКонтекст.ОтключенаНастройкаИспользоватьКонтрагентов = Истина;
	КонецЕсли;
	
	Если НЕ КонстантаИспользоватьДоговорыСКлиентами 
		И КонстантаИспользованиеСоглашенийСКлиентами = Перечисления.ИспользованиеСоглашенийСКлиентами.НеИспользовать 
		И Не ПустаяСтрока(СтррЗначенияНастроек.ИспользоватьДоговорыИлиСоглашения)
	Тогда 
		ТекОбъект.СохранитьЗначениеНастройки("ИспользоватьДоговорыИлиСоглашения", ""); // отключаем настройку "ИспользоватьДоговорыИлиСоглашения"
		СтррКонтекст.ОтключенаНастройкаИспользоватьДоговорыИлиСоглашения = Истина;
	КонецЕсли;
	
	СтррКонтекст.ЗакрытьФорму = Ложь; // используется для запрета запуска второго экземпляра обработки
	
	СтррКонтекст.Вставить("ИнформацияОбОбновлении", Неопределено); // строка информации об обновлении версии обработки
	
	Если СтррКонтекст.ВХОбщиеПараметры = Неопределено Тогда
		СтррПараметры = Новый Структура("КлючСеанса", Новый УникальныйИдентификатор);
		СтррКонтекст.ВХОбщиеПараметры = ПоместитьВоВременноеХранилище(СтррПараметры);
		СтррКонтекст.ОткрытиеПоНавигации = Ложь;
		СтррКонтекст.ИнформацияОбОбновлении = ТекОбъект.ИнициализироватьДанныеВХранилищеДляНовойВерсии();
	Иначе // значит форма открыта из другой формы обработки обмена данными по команде навигации
		СтррКонтекст.ОткрытиеПоНавигации = Истина;
	КонецЕсли; 
	
	//@skip-check undefined-function
	СтррКонтекст.ИнтерфейсТакси = ТекОбъект.КонфигурацияРазмерностьЭлементовФормыДляТакси() 
		И ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Такси;
	//@skip-check undefined-function
	СтррКонтекст.ИсправитьРазмерыЭлементов = ТекОбъект.КонфигурацияРежимСовместимости("<=8.3.6") 
		И ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Такси;
		
	СтррКонтекст.Вставить("АдресСтраницыПриветствия", "agentplus.online");
	
	ПрочестьНастройкиСервер();	
	ОбновитьГотовностьСправочников();
	ОбновитьГотовностьНастроекОбмена();
	ОбновитьГотовностьФоновогоЗадания();
	
	ЗаполнитьРазделы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если СтррКонтекст.ЗакрытьФорму Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли; 
	
	Заголовок = "Агент Плюс: Управление мобильной торговлей (ver. " + СтррКонтекст.Версия + ")";
	
	Цвета = СтррКонтекст.Цвета;
	Элементы.ГруппаРазделы.ЦветФона 		= Цвета.ПолеГлавноеОкно;
	Элементы.ГруппаКоманднаяПанель.ЦветФона = Цвета.ФонРаздела;	
	Элементы.Разделы.ЦветФона  			= Цвета.ПолеГлавноеОкно;
	Элементы.Разделы.ЦветРамки 			= Цвета.ПолеГлавноеОкно;
	
	ЕстьЗамечания = ОбновитьСтатусГотовности(Ложь);
	
	ПодключитьОбработчикОжидания("ОбновитьСтатусГотовностиПоТаймеру", 3600, Ложь);
	
	ИзмениласьВерсияОбработки = ЗначениеЗаполнено(СтррКонтекст.ИнформацияОбОбновлении);
	Если ИзмениласьВерсияОбработки Тогда // изменилась версия обработки
		ОчиститьСообщения();
		//@skip-check use-non-recommended-method
		Сообщить(СтррКонтекст.ИнформацияОбОбновлении);
		Элементы.ПоказатьИсторияВерсий_N1.Видимость = Истина;
	Иначе
		Элементы.ПоказатьИсторияВерсий_N1.Видимость = Ложь;		
	КонецЕсли;
	
	// отменяем автозапуск обмена, если изменилась версия обработки - на всякий случай, чтобы пользователь увидел сообщения об изменениях.	
	Если Объект.АвтозапускОбменаДанными И Не ЕстьЗамечания И Не ИзмениласьВерсияОбработки И Не СтррКонтекст.ОткрытиеПоНавигации Тогда
		Элементы.ДекорацияЗапускОбмена.Видимость = Истина;
		ТаймерЗапускаОбмена = 6; // таймаут на 5 секунд - секунда отнимется при прямом вызове обработчика ПоказатьОбменДаннымиПоТаймеру()
		ПоказатьОбменДаннымиПоТаймеру();
		ПодключитьОбработчикОжидания("ПоказатьОбменДаннымиПоТаймеру", 1, Ложь);
	Иначе
		Элементы.ДекорацияЗапускОбмена.Видимость = Ложь;
	КонецЕсли;
	
	Если Не СтррКонтекст.ОткрытиеПоНавигации Тогда
		Оповестить("АППроверкаУникальностиЗапускаОбработкиОбмена", СтррКонтекст.ВХОбщиеПараметры);	
	КонецЕсли; 
	
	Если Не СтррКонтекст.ИнтерфейсТакси Тогда
		Элементы.СтрокаБраузера.Ширина   = Элементы.СтрокаБраузера.Ширина * 2.6;
		Элементы.БыстраяНастройка.Ширина = 19;
		Элементы.БыстраяНастройка.Высота = 2;
	КонецЕсли; 
	
	Если СтррКонтекст.ИсправитьРазмерыЭлементов	Тогда // изменение высоты расширенных подсказок для более старых версий платформы 1С
		
		СтррЭлементы = Новый Структура;
		
		// Раздел "Обмен данными"
		СтррЭлементы.Вставить("НастройкаМодуляОбмена", 		2);
		СтррЭлементы.Вставить("ПоказатьНастройкиАгентов", 	3);
		
		// Раздел "Справочники"
		СтррЭлементы.Вставить("ПоказатьСпискиТорговыхТочек", 	3);
		СтррЭлементы.Вставить("ПоказатьКатегорииДокументов", 	3);
		СтррЭлементы.Вставить("ПоказатьСтатусыПартнеров", 		3);
		СтррЭлементы.Вставить("СправочникРезультатыПосещений", 	3);
		
		// Раздел "Документы"
		СтррЭлементы.Вставить("ДокументыПосещений", 		2);
		СтррЭлементы.Вставить("ДокументыМерчендайзинга", 	3);
		
		// Раздел "Планирование и контроль"
		СтррЭлементы.Вставить("ПоказатьСпискиТорговыхТочек_N1", 		3);
		СтррЭлементы.Вставить("ПоказатьОтчетВыполнениеПланаПосещений", 	2);
		СтррЭлементы.Вставить("ПоказатьРедакторМеток", 					2);
		
		// Раздел "Сервис"
		СтррЭлементы.Вставить("НастройкаМодуляОбменаСервис", 	2);
		СтррЭлементы.Вставить("ПоказатьЖурналОбменаДанными_N1", 2);
		СтррЭлементы.Вставить("БыстраяНастройкаВСервисе", 		2);
		
		Для каждого ЭлементСтруктуры Из СтррЭлементы Цикл
			Элементы[ЭлементСтруктуры.Ключ + "РасширеннаяПодсказка"].Высота = ЭлементСтруктуры.Значение;
		КонецЦикла; 
		
		Элементы.СкрыватьБыструюНастройкуСервис.Заголовок = НСтр("ru = 'Скрывать ""Быструю настройку""'");
		
	КонецЕсли; 
	// В информационное поле выводятся предупреждения для пользователя, в том случае когда настройки обмена могут конфликтовать с настройками конфигурации УС.
	Элементы.ИнформационноеПоле.Видимость = Ложь;
	
	ИтоговаяСтрока 	= Новый Массив;
	//@skip-check new-font
	ШрифтОбычный	= Новый Шрифт(,8);
	//@skip-check new-font
	ШрифтПолуЖирный = Новый Шрифт(,8, Истина);
	ПереводСтроки	= Новый ФорматированнаяСтрока(Символы.ПС);
	
	Если СтррКонтекст.ОтключенаНастройкаВыгружатьХарактеристикиНоменклатуры Тогда
		
		Элементы.ИнформационноеПоле.Видимость = Истина;
		Элементы.ИнформационноеПоле.Высота = 7;
		
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Предупреждение:'") + " ", ШрифтПолуЖирный));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Автоматически выключена настройка - ""Выгружать характеристики номенклатуры"" (перейти на закладку Обмен данными - Общие настройки -'") + " ", ШрифтОбычный));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Основные'"), ШрифтОбычный,,,"ПерейтиНаЗакладкуОсновныеХарактеристикиНоменклатуры")); // ссылка и ее строковое представление для обработчика "ИнформационноеПолеОбработкаНавигационнойСсылки"
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ').'") + " ", ШрифтОбычный));
		ИтоговаяСтрока.Добавить(ПереводСтроки);
		
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Причина: в настройках конфигурации выключена функциональная опция ""Использовать характеристики номенклатуры"" (перейти в раздел НСИ и Администрирование - Настройка НСИ -'") + " ", ШрифтОбычный));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Номенклатура'"), ШрифтОбычный,,,"НастройкиНоменклатуры")); // ссылка и ее строковое представление для обработчика "ИнформационноеПолеОбработкаНавигационнойСсылки"
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ').'"), ШрифтОбычный));
		ИтоговаяСтрока.Добавить(ПереводСтроки);
		
	КонецЕсли;
	
	Если СтррКонтекст.ОтключенаНастройкаИспользоватьКонтрагентов Тогда
		
		Элементы.ИнформационноеПоле.Видимость = Истина;
		Элементы.ИнформационноеПоле.Высота = Элементы.ИнформационноеПоле.Высота + 7;
		
		Если ИтоговаяСтрока.Количество() > 0 Тогда 
			ИтоговаяСтрока.Добавить(ПереводСтроки);	
		КонецЕсли;
		
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Предупреждение:'") + " ", ШрифтПолуЖирный));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Автоматически выключена настройка - ""Использовать контрагентов"" (перейти на закладку Обмен данными - Общие настройки -'") + " ", ШрифтОбычный));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Основные'"), ШрифтОбычный,,,"ПерейтиНаЗакладкуОсновныеИспользоватьКонтрагентов"));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ').'") + " ", ШрифтОбычный));
		ИтоговаяСтрока.Добавить(ПереводСтроки);
		
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Причина: в настройках конфигурации выключена функциональная опция ""Независимо вести партнеров и контрагентов"" (перейти в раздел НСИ и Администрирование - Настройка НСИ -'") + " ", ШрифтОбычный));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'CRM и маркетинг'"), ШрифтОбычный,,,"CRMИМаркетинг"));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ').'"), ШрифтОбычный));
		ИтоговаяСтрока.Добавить(ПереводСтроки);
		
	КонецЕсли;
	
	Если СтррКонтекст.ОтключенаНастройкаИспользоватьДоговорыИлиСоглашения Тогда
		
		Элементы.ИнформационноеПоле.Видимость = Истина;
		Элементы.ИнформационноеПоле.Высота = Элементы.ИнформационноеПоле.Высота + 7;
		
		Если ИтоговаяСтрока.Количество() > 0 Тогда 
			ИтоговаяСтрока.Добавить(ПереводСтроки);	
		КонецЕсли;

		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Предупреждение:'") + " ", ШрифтПолуЖирный));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Автоматически выключена настройка - ""Использовать договоры или соглашения"" (Обмен данными - Общие настройки -'") + " ", ШрифтОбычный));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Основные'"), ШрифтОбычный,,,"ПерейтиНаЗакладкуОсновныеИспользоватьДоговорыИлиСоглашения"));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ').'") + " ", ШрифтОбычный));
		ИтоговаяСтрока.Добавить(ПереводСтроки);
		
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Причина: в настройках конфигурации выключены функциональные опции ""Использовать договоры с клиентами"" и ""Использование соглашений с клиентами"" (перейти в раздел НСИ и Администрирование - Настройка НСИ -'") + " ", ШрифтОбычный));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Продажи'"), ШрифтОбычный,,,"Продажи"));
		ИтоговаяСтрока.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ').'"), ШрифтОбычный));
		
	КонецЕсли;
	
	ИнформационноеПоле = Новый ФорматированнаяСтрока(ИтоговаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "АПНастройкиПараметровОбменаИзмененияЗаписаны" Тогда
		
		ПрочестьВсеНастройкиИОбновитьСтатус();
		
	ИначеЕсли ИмяСобытия = "АПНастройкиАгентовИзмененияЗаписаны" 
		Или ИмяСобытия = "АПМобильныеУстройстваИзменениеСпискаМУ" 
		Или ИмяСобытия = "АПОбновленыНастройкиФоновогоЗадания" 
		Или ИмяСобытия = "Запись_РегламентныеЗадания" // оповещение от УТ о записи регламентного задания
	Тогда
		
		ОбновитьСтатусГотовности(Истина);
		
	ИначеЕсли ИмяСобытия = "АПНастройкиСброшены" Тогда
		
		Закрыть();
		
	ИначеЕсли ИмяСобытия = "АППроверкаУникальностиЗапускаОбработкиОбмена" Тогда
		
		Если Параметр <> СтррКонтекст.ВХОбщиеПараметры Тогда // второй экземпляр обработки справшивает - уже открыта обработка или нет
			Оповестить("АПЗакрытьОбработкуОбменДанными", Параметр); // шлем событие закрытия обработки с конкретным ключем сеанса
		КонецЕсли; 
		
	ИначеЕсли ИмяСобытия = "АПЗакрытьОбработкуОбменДанными" И Параметр = СтррКонтекст.ВХОбщиеПараметры Тогда
		
		Состояние("Обработка обмена данными уже запущена.");
		СтррКонтекст.ЗакрытьФорму = Истина;
		//@skip-check form-self-reference
		Если ЭтаФорма.Открыта() Тогда
			Закрыть();		
		КонецЕсли;  
		
	КонецЕсли;

КонецПроцедуры

// ОбработчикиСобытийФормы
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НадписьСтатусНажатие(Элемент)
	
	Если Элемент.Имя = "НадписьГотовностьАгентов" Тогда
		МодульК().КомандаВыполнить("ПоказатьНастройкиАгентов");
		Оповестить("АПНастройкиАгентов_Оповещение", Новый Структура("ВыделитьПервогоАгентаСОшибкой", Истина));
	ИначеЕсли Элемент.Имя = "НадписьГотовностьМУ" Тогда
		МодульК().КомандаВыполнить("ПоказатьМобильныеУстройства");
	ИначеЕсли Элемент.Имя = "НадписьГотовностьНастроекОбмена" Тогда
		ОткрытьНастройкиОбмена(Истина);		
	ИначеЕсли Элемент.Имя = "НадписьОбменДанными" Тогда
		МодульК().КомандаВыполнить("ПоказатьОбменДанными");
	ИначеЕсли Элемент.Имя = "НадписьГотовностьРегламентногоЗадания" Тогда
		МодульК().КомандаВыполнить("ПоказатьРегистрацияРегламентногоЗадания");
	Иначе
		ВызватьИсключение("Неизвестный элемент для обработки события ПриНажатии(): " + Элемент.Имя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаБраузераПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	Если ДанныеСобытия.Anchor <> Неопределено И ЗначениеЗаполнено(ДанныеСобытия.Anchor.href) Тогда
		СтандартнаяОбработка = Ложь;
		ЗапуститьПриложение(ДанныеСобытия.Anchor.href);
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура СкрыватьБыструюНастройкуПриИзменении(Элемент)
	
	СохранитьНастройкуСервер("СкрыватьБыструюНастройку");
	Если Объект.СкрыватьБыструюНастройку Тогда // если изменен флажок на домашней странице, его не скрываем
	    ОчиститьСообщения();
		//@skip-check use-non-recommended-method
		Сообщить(НСтр("ru = 'Изменения вступят в силу после перезапуска обработки.'"));
	Иначе
		ОбновитьСтатусГотовности(Ложь);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура АвтозапускОбменаДаннымиПриИзменении(Элемент)
	
	Элементы.АвтозапускОбменаДанными.Заголовок = НСтр("ru = 'Автозапуск обмена данными'");
	ОтключитьОбработчикОжидания("ПоказатьОбменДаннымиПоТаймеру");
	Элементы.ДекорацияЗапускОбмена.Видимость = Ложь;
	СохранитьНастройкуСервер("АвтозапускОбменаДанными");
	
	Если Объект.АвтозапускОбменаДанными Тогда
		Текст = НСтр("ru = 'Запустить обмен данными с мобильными устройствами?'");
		Оповещение = Новый ОписаниеОповещения("АвтозапускОбменаДаннымиЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыполнить(Команда)
	
	МодульК().КомандаВыполнить(Команда, ЭтотОбъект)
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНастройкиОбщие(Команда)
	
	ОткрытьНастройкиОбмена(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура КомандаПоказатьИсторияВерсий(Команда)
	
	ЗапуститьПриложение("https://agentplus.tech/x/hwGh"); 
	
КонецПроцедуры

&НаКлиенте
Процедура ИнформационноеПолеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиНаЗакладкуОсновныеХарактеристикиНоменклатуры" Тогда 
		
		МодульК().КомандаВыполнить("ПоказатьНастройкиМодуля");
		СтррПараметры = Новый Структура("Закладка,Сообщение", "ГруппаОсновные", "УказатьВыгружатьХарактеристикиНоменклатуры");
		Оповестить("АПНастройкиМодуля_ПоказатьНаФорме", СтррПараметры);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиНаЗакладкуОсновныеИспользоватьКонтрагентов" Тогда
		
		МодульК().КомандаВыполнить("ПоказатьНастройкиМодуля");
		СтррПараметры = Новый Структура("Закладка,Сообщение", "ГруппаОсновные", "УказатьИспользоватьКонтрагентов");
		Оповестить("АПНастройкиМодуля_ПоказатьНаФорме", СтррПараметры);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиНаЗакладкуОсновныеИспользоватьДоговорыИлиСоглашения" Тогда
		
		МодульК().КомандаВыполнить("ПоказатьНастройкиМодуля");
		СтррПараметры = Новый Структура("Закладка,Сообщение", "ГруппаОсновные", "УказатьИспользоватьДоговорыИлиСоглашения");
		Оповестить("АПНастройкиМодуля_ПоказатьНаФорме", СтррПараметры);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "НастройкиНоменклатуры"
		  Или НавигационнаяСсылкаФорматированнойСтроки = "CRMИМаркетинг"
		  Или НавигационнаяСсылкаФорматированнойСтроки = "Продажи" Тогда 
		  
		  //@skip-check form-self-reference
		  ОткрытьФорму(
				"Обработка.ПанельАдминистрированияУТ.Форма." + НавигационнаяСсылкаФорматированнойСтроки,
		  		Новый Структура,
				ЭтаФорма, 
				"Обработка.ПанельАдминистрированияУТ.Форма." + НавигационнаяСсылкаФорматированнойСтроки + ".ОтдельноеОкно");
	
	КонецЕсли;
	
КонецПроцедуры

// ОбработчикиСобытийЭлементовШапкиФормы
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРазделы

&НаКлиенте
Процедура РазделыПриАктивизацииСтроки(Элемент)
	
	СтрокаТ = Элемент.ТекущиеДанные;
	Если СтрокаТ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//@skip-check undefined-function
	Если Не ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Такси Тогда 
		Если СтрокаТ.ИконкаРаздела < 9 Тогда // подменяем иконку текущего раздела на белую
			СтрокаТ.ИконкаРаздела = СтрокаТ.ИконкаРаздела + 9;
		КонецЕсли;
		Для каждого стзТабРазделы Из тзРазделы Цикл
			Если стзТабРазделы <> СтрокаТ Тогда
			    Если стзТабРазделы.ИконкаРаздела > 9 Тогда
					стзТабРазделы.ИконкаРаздела = стзТабРазделы.ИконкаРаздела - 9;
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
	ПереключитьСтраницу(СтрокаТ.Раздел);
	
КонецПроцедуры

// ОбработчикиСобытийЭлементовТаблицыФормыРазделы
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаСправка(Команда)
	
	РазделСправки = Неопределено;
	
	СтрокаТ = Элементы.Разделы.ТекущиеДанные;
	Если СтрокаТ <> Неопределено Тогда
		Если СтрокаТ.Раздел = "Главная" Тогда // если находимся на "Домашней странице", то показываем "Общий раздел" справки.
			//@skip-check form-self-reference
			РазделСправки = ЭтаФорма.ИмяФормы;
		Иначе
			//@skip-check form-self-reference
			РазделСправки = ЭтаФорма.ИмяФормы + "_" + СтрокаТ.Раздел;
		КонецЕсли; 
	КонецЕсли;
	
	МодульК().КомандаСправка(РазделСправки);
	
КонецПроцедуры

#Область ОбработчикиКомандФормы_Обработки

&НаКлиенте
Процедура КомандаБыстраяНастройка(Команда)
	
	Текст = НСтр("ru = 'Запустить быструю настройку модуля обмена данными?'");
	Оповещение = Новый ОписаниеОповещения("КомандаБыстраяНастройкаПродолжить", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

// ОбработчикиКомандФормы_Обработки
#КонецОбласти

#Область ОбработчикиКомандФормы_Виджет

&НаКлиенте
Процедура КомандаУказатьМестоположение(Команда)
	
	СтррПараметры = Новый Структура("Местоположение", СтррКонтекст.Местоположение);
	Оповещение = Новый ОписаниеОповещения("КомандаУказатьМестоположениеПродолжить", ЭтотОбъект);
	МодульК().ОткрытьФормуОбработки("ВыборМестоположения", СтррПараметры,, Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура КомандаПерейти(Команда)
	
	Меню = Новый СписокЗначений;
	
	//@skip-check use-non-recommended-method
	МодульКарты = ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульКарты", СтррКонтекст);
	КодСтраны = МодульКарты.КодСтраныМестоположения(СтррКонтекст.Местоположение);
	Если КодСтраны = "UA" Тогда
		Меню.Добавить("Сайт_ua", "www.agentplus.com.ua",, Элементы.ДекорацияАгентПлюс.Картинка);
	Иначе
		Меню.Добавить("Сайт_ru", "www.agentplus.ru",, 	  Элементы.ДекорацияАгентПлюс.Картинка);	
	КонецЕсли; 
	Меню.Добавить("ЛичныйКабинет", НСтр("ru = 'Личный кабинет'"),, Элементы.ДекорацияЛК.Картинка);
	Оповещение = Новый ОписаниеОповещения("КомандаПерейтиПродолжить", ЭтотОбъект);	
	ПоказатьВыборИзМеню(Оповещение, Меню, Элементы.Перейти);

КонецПроцедуры

// ОбработчикиКомандФормы_Виджет
#КонецОбласти 

#Область ОбработчикиКомандФормы_Видео

&НаКлиенте
Процедура КомандаПоказатьВидео(Команда)
	
	СтвВидео = Новый Соответствие;
	СтвВидео.Вставить("ВидеоНастройкиМодуляГлавная", 	"https://youtu.be/AzV0kxY8IMw");	
	
	СтвВидео.Вставить("ВидеоКаналАгентПлюс", 			"https://www.youtube.com/user/agentplusinc");
	СтвВидео.Вставить("ВидеоНачальнаяНастройкаМодуля", 	"https://youtu.be/AzV0kxY8IMw");
	СтвВидео.Вставить("ВидеоМТРелиз20", 				"https://youtu.be/9xuXBQDa6Yg");

	//@skip-check form-self-reference
	ИмяЭлемента = ЭтаФорма.ТекущийЭлемент.Имя;
	Если СтвВидео.Получить(ИмяЭлемента) <> Неопределено Тогда
	    ЗапуститьПриложение(СтвВидео.Получить(ИмяЭлемента)); 
	Иначе
		ВызватьИсключение("Неизвестный элемент формы для запуска видео: " + ИмяЭлемента);
	КонецЕсли; 
	
КонецПроцедуры

// ОбработчикиКомандФормы_Видео
#КонецОбласти

&НаКлиенте
Процедура КомандаТест(Команда)
	
	КомандаТестСервер("Проверяем сообщение");
	
КонецПроцедуры

// ОбработчикиКомандФормы
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункции_СовместимостьСПлатформой_8_3_5

// Подставляет параметры в строку. Максимально возможное число параметров - 5.
// Параметры в строке задаются как %<номер параметра>. Нумерация параметров начинается с единицы.
//
// Параметры:
//  СтрокаПодстановки  - Строка - шаблон строки с параметрами (вхождениями вида "%ИмяПараметра");
//  Параметр1 - Строка - подставляемый параметр.
//  Параметр2 - Строка - подставляемый параметр.  
//  Параметр3 - Строка - подставляемый параметр.
//  Параметр4 - Строка - подставляемый параметр.
//  Параметр5 - Строка - подставляемый параметр.
//  Параметр6 - Строка - подставляемый параметр.
//
// Возвращаемое значение:
//  Строка   - текстовая строка с подставленными параметрами.
//
//@skip-check method-too-many-params
&НаКлиентеНаСервереБезКонтекста
Функция СтрШаблон_(СтрокаПодстановки,
	Параметр1, Параметр2 = Неопределено, Параметр3 = Неопределено, Параметр4 = Неопределено, Параметр5 = Неопределено, Параметр6 = Неопределено)
	
	Результат = СтрЗаменить(СтрокаПодстановки, "%1", Параметр1);
	Результат = СтрЗаменить(Результат, "%2", Параметр2);
	Результат = СтрЗаменить(Результат, "%3", Параметр3);
	Результат = СтрЗаменить(Результат, "%4", Параметр4);
	Результат = СтрЗаменить(Результат, "%5", Параметр5);
	
	Возврат Результат;
	
КонецФункции

// СлужебныеПроцедурыИФункции_СовместимостьСПлатформой_8_3_5
#КонецОбласти

#Область СлужебныеПроцедурыИФункции_Форма

&НаКлиенте
Процедура ПереключитьСтраницу(ИмяСтраницы)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["Группа" + ИмяСтраницы];
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьРазделы()
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 1; // 2 - иконка домика с восклицательным знаком
	СтрокаТ.ПредставлениеРаздела = НСтр("ru = 'Домашняя страница'");
	СтрокаТ.Раздел 				 = "Главная";
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 3;
	СтрокаТ.ПредставлениеРаздела = НСтр("ru = 'Обмен данными'");
	СтрокаТ.Раздел 				 = "ОбменДанными";
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 4;
	СтрокаТ.ПредставлениеРаздела = НСтр("ru = 'Справочники'");
	СтрокаТ.Раздел 				 = "Справочники";
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 5;
	СтрокаТ.ПредставлениеРаздела = НСтр("ru = 'Документы'");
	СтрокаТ.Раздел 				 = "Документы";
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 6;
	СтрокаТ.ПредставлениеРаздела = НСтр("ru = 'Планирование и контроль'");
	СтрокаТ.Раздел 				 = "ПланированиеИКонтроль";
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 7;
	СтрокаТ.ПредставлениеРаздела = НСтр("ru = 'Сервис'");
	СтрокаТ.Раздел 				 = "Сервис";
	
	СтрокаТ = тзРазделы.Добавить();
	СтрокаТ.ИконкаРаздела 		 = 8;
	СтрокаТ.ПредставлениеРаздела = НСтр("ru = 'Помощь'");
	СтрокаТ.Раздел 				 = "Помощь";
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции_Форма
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_ХранилищеНастроек

&НаСервере
Процедура ПрочестьНастройкиСервер()

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.ВосстановитьЗначенияНастроекОбработки(НастройкиФормы());
	ЗначениеВРеквизитФормы(ТекОбъект, "Объект");

КонецПроцедуры

&НаСервере
Функция НастройкиФормы()
	Возврат "ВерсияНастроек,АвтозапускОбменаДанными,СкрыватьБыструюНастройку,ПроверятьРегламентноеЗадание";
КонецФункции // НастройкиФормы()

&НаСервере
Процедура СохранитьЗначениеНастройки(ИмяНастройки, Значение)
	РеквизитФормыВЗначение("Объект").СохранитьЗначениеНастройки(ИмяНастройки, Значение);
КонецПроцедуры

&НаСервере
Процедура ОбновитьГотовностьСправочников()
	СтррКонтекст.СтатусыСправочников 
		= РеквизитФормыВЗначение("Объект").ГотовностьСправочников("НастройкиАгентов,МобильныеУстройства");
КонецПроцедуры
	
&НаСервере
Процедура ОбновитьГотовностьНастроекОбмена()
	СтррКонтекст.СтатусНастройкиОбмена = РеквизитФормыВЗначение("Объект").ГотовностьНастроекОбмена();
КонецПроцедуры

&НаСервере
Процедура ОбновитьГотовностьФоновогоЗадания()
	
	Если Объект.ПроверятьРегламентноеЗадание Тогда
		СтррКонтекст.СтатусФоновогоЗадания = РеквизитФормыВЗначение("Объект").ГотовностьФоновогоЗадания();
	Иначе
		СтррКонтекст.СтатусФоновогоЗадания = Неопределено;
	КонецЕсли; 
	
КонецПроцедуры

//&НаСервере
//Процедура ПрочестьСписокАгентовИзНастроек()
//
//	ТекОбъект = РеквизитФормыВЗначение("Объект");
//	ТекОбъект.ВосстановитьЗначенияНастроекОбработки("НастройкиАгентов");
//	ЗначениеВРеквизитФормы(ТекОбъект, "Объект");
//
//КонецПроцедуры

&НаКлиенте
Процедура ПрочестьВсеНастройкиИОбновитьСтатус()
	ПрочестьНастройкиСервер();
	ОбновитьГотовностьНастроекОбмена();
	ОбновитьГотовностьФоновогоЗадания();
	ОбновитьСтатусГотовности(Ложь);
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкуСервер(ИмяНастройки)
	
	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.СохранитьЗначенияНастроекОбработки(ИмяНастройки); 
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции_ХранилищеНастроек
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_Статусы

// Функция проверяет готовность модуля к работе (готовность настроек модуля) и выводит
// на форме информацию о его готовности.
&НаКлиенте
Функция ОбновитьСтатусГотовности(ПрочитатьИзНастроек)
	
	Если ПрочитатьИзНастроек Тогда
		Попытка
			ОбновитьГотовностьСправочников();
		Исключение
			ПоказатьПредупреждение(Неопределено, "Запущен второй экземпляр обработки в фоновом режиме. Закройте текущую обработку или ее фоновый сеанс (отмените регламентное задание).");
		КонецПопытки;
		ОбновитьГотовностьНастроекОбмена();
		ОбновитьГотовностьФоновогоЗадания();
	КонецЕсли;
	
	ЕстьЗамечания = Ложь;
	Элементы.ДекорацияПриветствие.Заголовок = ПолучитьПриветствие();
	СтрокаБраузера = ПолучитьАдресСтраницыПриветствия();
	
	////////////////////////////////////////////////
	// обновляем статус готовности справочников
	
	СтррСтатус = СтррКонтекст.СтатусыСправочников.НастройкиАгентов;
	Если СтррСтатус.Готово Тогда
		Элементы.НадписьГотовностьАгентов.Видимость = Ложь;
	Иначе
		ЕстьЗамечания = Истина;
		Элементы.НадписьГотовностьАгентов.Видимость = Истина;
		Элементы.НадписьГотовностьАгентов.Заголовок = СтррСтатус.Описание;
	КонецЕсли;
	
	СтррСтатус = СтррКонтекст.СтатусыСправочников.МобильныеУстройства;
	Если СтррСтатус.Готово Тогда
		Элементы.НадписьГотовностьМУ.Видимость = Ложь;
	Иначе
		ЕстьЗамечания = Истина;
		Элементы.НадписьГотовностьМУ.Видимость = Истина;
		Элементы.НадписьГотовностьМУ.Заголовок = СтррСтатус.Описание;
	КонецЕсли;
	
	////////////////////////////////////////////////
	// обновляем статус настроек обмена
	
	СтррСтатус = СтррКонтекст.СтатусНастройкиОбмена;
	Если СтррСтатус.Готово Тогда
		Элементы.НадписьГотовностьНастроекОбмена.Видимость = Ложь;
	Иначе
		ЕстьЗамечания = Истина;
		Элементы.НадписьГотовностьНастроекОбмена.Видимость = Истина;
		Элементы.НадписьГотовностьНастроекОбмена.Заголовок = СтррСтатус.Описание;
	КонецЕсли;
	
	////////////////////////////////////////////////
	// обновляем статус готовности фонового задания
	
	ВидимостьБыстройНастройки = Неопределено; // быстрая настройка не исправит проблемы регистраци фонового задания, но может  исправить другие замечания (если они есть)
	
	СтррСтатус = СтррКонтекст.СтатусФоновогоЗадания;
	Если СтррСтатус = Неопределено Тогда
		Элементы.НадписьГотовностьРегламентногоЗадания.Видимость = Ложь;
	ИначеЕсли СтррСтатус.РегистрацияОбработки.Готово И СтррСтатус.РегламетноеЗадание.Готово Тогда
		Элементы.НадписьГотовностьРегламентногоЗадания.Видимость = Ложь;
	Иначе
		Если Не ЕстьЗамечания Тогда
			ВидимостьБыстройНастройки = Ложь; // это единственное замечание к настройкам обрабтки (регистрация как фонового задания), скрываем предложение быстрой настройки
		КонецЕсли; 
		ЕстьЗамечания = Истина;
		Элементы.НадписьГотовностьРегламентногоЗадания.Заголовок = НСтр("ru = 'Есть замечания к настройке регламентного задания.'");
		Элементы.НадписьГотовностьРегламентногоЗадания.Видимость = Истина;
	КонецЕсли; 
	
	Элементы.ДекорацияСтатус.Заголовок = ?(ЕстьЗамечания, 
				НСтр("ru = 'Пожалуйста, примите меры:'"), 
				НСтр("ru = 'Все готово к работе. Замечаний нет.'"));
				
	Элементы.ДекорацияЗамечанияОтступ.Видимость	   = ЕстьЗамечания;					
	Элементы.ДекорацияЗамечанияОтступ1.Видимость   = ЕстьЗамечания;
	
	Если ВидимостьБыстройНастройки = Неопределено Тогда
		ВидимостьБыстройНастройки = Не Объект.СкрыватьБыструюНастройку И ЕстьЗамечания;		
	КонецЕсли; 
	Элементы.ДекорацияБыстраяНастройка.Видимость   = ВидимостьБыстройНастройки;
	Элементы.ГруппаБыстраяНастрокаКнопка.Видимость = ВидимостьБыстройНастройки;
				
	Элементы.НадписьОбменДанными.Видимость = Не ЕстьЗамечания;
	Элементы.АвтозапускОбменаДанными.Видимость = Не ЕстьЗамечания;
	
	тзРазделы[0].ИконкаРаздела = ?(ЕстьЗамечания, 2, 1);
	
	Возврат ЕстьЗамечания;

КонецФункции

&НаКлиенте
Процедура ОбновитьСтатусГотовностиПоТаймеру()
	
	ОбновитьСтатусГотовности(Истина);
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции_Статусы
#КонецОбласти 

#Область СлужебныеПроцедурыИФункции_Приветствие

&НаКлиенте
Функция ПолучитьВремяСуток()
	
	//@skip-check use-non-recommended-method
	ТекДата = ТекущаяДата();
	Час = Час(ТекДата) + Минута(ТекДата) / 60;
	
	Если (Час >= 22  И Час <= 24) Или (Час >= 0 И Час <= 5) Тогда
		Результат = "night";
	ИначеЕсли (Час > 5) И (Час <= 11) Тогда
		Результат = "morning";
	ИначеЕсли (Час > 11) И (Час <= 15.5) Тогда
		Результат = "day";
	Иначе
		Результат = "evening";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ИмяПользователяПолучить()
	
	Имя = СокрЛП(ПараметрыСеанса.ТекущийПользователь);
	// пытаемся убрать фамилию
	//@skip-check use-non-recommended-method
	Поз = Найти(Имя, " ");
	Если Поз = 0 Тогда
	    Возврат Имя;
	Иначе
		ИмяОтчество = СокрЛ(Прав(Имя, СтрДлина(Имя) - Поз));
		Если СтрДлина(ИмяОтчество) < 5 Тогда
		    Возврат Имя;
		Иначе
			Возврат ИмяОтчество;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ПолучитьАдресСтраницыПриветствия()
	
	Адрес 		= "https://%1/Widget?time=%2&city=%3&cityen=%4&application=%5&client=%6";
	Город 		= "Москва";
	City  		= "Moscow";
	Application = "ut11epf";
	ClientMtx	= СтррКонтекст.КлиентМетрикаИнформера;
	
	Если ТипЗнч(СтррКонтекст.Местоположение) <> Тип("Структура") Тогда // местоположение пользователя еще не задано
		
		ПолучитьМестоположение = Ложь;
		//@skip-check use-non-recommended-method
		Если ТипЗнч(СтррКонтекст.ВремяМестоположения) <> Тип("Дата") Тогда
			ПолучитьМестоположение = Истина;
		ИначеЕсли НачалоДня(СтррКонтекст.ВремяМестоположения) <> НачалоДня(ТекущаяДата()) Тогда
			ПолучитьМестоположение = Истина;
		КонецЕсли; 
		
		Если ПолучитьМестоположение Тогда
			// определяем местоположение по IP-адресу
			//@skip-check use-non-recommended-method
			МодульКарты = ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульКарты", СтррКонтекст);
			CтррМестоположение = МодульКарты.МестоположениеПользователяПолучить();
			Если CтррМестоположение.Свойство("error") Тогда
				//@skip-check use-non-recommended-method
				Сообщить(НСтр("ru = 'Не удалось определить местоположение для информации о погоде. Причина:'") + Символы.ПС + CтррМестоположение.error);
			Иначе
				СтррКонтекст.Местоположение = CтррМестоположение;
			    СохранитьЗначениеНастройки("МестоположениеДляИнформера", СтррКонтекст.Местоположение);
			КонецЕсли; 
			//@skip-check use-non-recommended-method
			СтррКонтекст.ВремяМестоположения = ТекущаяДата();
			СохранитьЗначениеНастройки("ВремяМестоположения", СтррКонтекст.ВремяМестоположения);
		КонецЕсли; 
		
	КонецЕсли; 
	
	Если ТипЗнч(СтррКонтекст.Местоположение) = Тип("Структура") Тогда
		Если СтррКонтекст.Местоположение.Свойство("city_ru") И ЗначениеЗаполнено(СтррКонтекст.Местоположение.city_ru) Тогда
			Город = СтррКонтекст.Местоположение.city_ru;
		КонецЕсли;
		Если СтррКонтекст.Местоположение.Свойство("city_en") И ЗначениеЗаполнено(СтррКонтекст.Местоположение.city_en) Тогда
			City = СтррКонтекст.Местоположение.city_en;
		КонецЕсли;		
	КонецЕсли;
	            	
	Если Не ЗначениеЗаполнено(Город) И ЗначениеЗаполнено(City) Тогда
		Город = City;
	КонецЕсли; 
	
	Адрес = СтрШаблон_(Адрес, СтррКонтекст.АдресСтраницыПриветствия, ПолучитьВремяСуток(), Город, City, Application, ClientMtx);
	Адрес = КодироватьСтрокуСервер(Адрес);  
	                          
	Возврат Адрес;
КонецФункции

&НаКлиенте
Функция ПолучитьПриветствие() 
	
	ВремяСуток = ПолучитьВремяСуток();

	Если ВремяСуток = "night" Тогда
	    Приветствие = НСтр("ru = 'Доброй ночи'");
	ИначеЕсли ВремяСуток = "morning" Тогда
		Приветствие = НСтр("ru = 'Доброе утро'");
	ИначеЕсли ВремяСуток = "day" Тогда
		Приветствие = НСтр("ru = 'Добрый день'");
	Иначе
		Приветствие = НСтр("ru = 'Добрый вечер'");
	КонецЕсли;
	
	Имя = ИмяПользователяПолучить();
	Если Не ПустаяСтрока(Имя) Тогда
		Приветствие = Приветствие + ", " + Имя;    
	КонецЕсли;	
	Приветствие = Приветствие + "!";
	
	Возврат Приветствие;
	
КонецФункции

// СлужебныеПроцедурыИФункции_Приветствие
#КОнецОбласти 

#Область СлужебныеПроцедурыИФункции_Прочие

&НаКлиенте
Функция МодульК()

	Если ГМодульК = Неопределено Тогда
	    //@skip-check use-non-recommended-method
	    ГМодульК = ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульОбщий", СтррКонтекст);
	КонецЕсли; 
	
	Возврат ГМодульК;

КонецФункции 

&НаКлиенте
Процедура ОткрытьНастройкиОбмена(УчестьСтатусНастроек)
	
	МодульК().КомандаВыполнить("ПоказатьНастройкиМодуля");
	
	Если УчестьСтатусНастроек Тогда
		СтррСтатус = СтррКонтекст.СтатусНастройкиОбмена;
		Если Не СтррСтатус.Готово Тогда
			//@skip-check use-non-recommended-method
			Если 0 <> Найти(СтррСтатус.КодСостояния, "Каталог") Тогда
				СтррПараметры = Новый Структура("Закладка,Сообщение", "ГруппаКаталоги", "УказатьКаталог");
				Оповестить("АПНастройкиМодуля_ПоказатьНаФорме", СтррПараметры);
			КонецЕсли; 
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция КодироватьСтрокуСервер(Стр)
	Возврат КодироватьСтроку(Стр, СпособКодированияСтроки.URLВКодировкеURL);
КонецФункции

&НаКлиенте
Процедура АвтозапускОбменаДаннымиЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		МодульК().КомандаВыполнить("ПоказатьОбменДанными");
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаБыстраяНастройкаПродолжить(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		МодульК().ОткрытьФормуОбработки("БыстраяНастройка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУказатьМестоположениеПродолжить(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			СтррКонтекст.Местоположение = Результат;
			СтрокаБраузера = ПолучитьАдресСтраницыПриветствия();
			СохранитьЗначениеНастройки("МестоположениеДляИнформера", СтррКонтекст.Местоположение);
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПерейтиПродолжить(Результат, ДополнительныеПараметры) Экспорт

	Если Результат <> Неопределено Тогда
		Если Результат.Значение = "Сайт_ru" Тогда
			ЗапуститьПриложение("http://www.agentplus.ru/");
		ИначеЕсли Результат.Значение = "Сайт_ua" Тогда
			ЗапуститьПриложение("http://www.agentplus.com.ua/");
		ИначеЕсли Результат.Значение = "ЛичныйКабинет" Тогда
			ЗапуститьПриложение("https://agentplus.online/");
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура КомандаТестСервер(ТекстСообщения)
	
	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.БотОтправитьСообщение(ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОбменДаннымиПоТаймеру()
	
	ТаймерЗапускаОбмена = ТаймерЗапускаОбмена - 1;
	Текст = НСтр("ru = 'Автозапуск обмена данными через %1 сек.'");
	Элементы.АвтозапускОбменаДанными.Заголовок = СтрШаблон_(Текст, ТаймерЗапускаОбмена);
	Если ТаймерЗапускаОбмена = 0 Тогда
		Элементы.ДекорацияЗапускОбмена.Видимость = Ложь;	
		Если Объект.АвтозапускОбменаДанными Тогда // флажок пользователь мог снять сразу после открытия формы
			МодульК().КомандаВыполнить("ПоказатьОбменДанными");
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции_Прочие
#КонецОбласти 

// СлужебныеПроцедурыИФункции
#КонецОбласти
