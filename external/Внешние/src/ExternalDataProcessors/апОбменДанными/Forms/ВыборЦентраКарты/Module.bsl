#Область ОписаниеПеременных

&НаКлиенте
Перем МодульКарты; // общий клиентский модуль работы с картами

// ОписаниеПеременных
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтррКонтекст = Новый Структура("Ключ,Реквизиты,Автокоординаты");
	ТекОбъект = РеквизитФормыВЗначение("Объект");		
	ТекОбъект.КонтекстФормыИнициализировать(СтррКонтекст, Параметры);
	
	СтррРеквизиты = Новый Структура; // пары значений - "<Реквизит формы>, <Имя элемента формы>"
	СтррРеквизиты.Вставить("СпособАвтоопределение", "Автоадрес");
	СтррРеквизиты.Вставить("СпособПоАдресу", 		"АдресПоиска");
	СтррРеквизиты.Вставить("СпособУказатьНаКарте",  "ДекорацияУказатьНаКарте");
	
	СтррКонтекст.Реквизиты = СтррРеквизиты;	

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//@skip-check use-non-recommended-method
	//@skip-check form-self-reference
	МодульКарты	= ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульКарты", СтррКонтекст, ЭтаФорма, "АгентПлюсМодульКартыКлиент");
	СтррКоординаты = МодульКарты.ПолучитьКоординатыПользователя();
	
	// Определяем первый доступный способ указания центра карты и запоминаем его в переменной Способ
	
	Если СтррКоординаты <> Неопределено Тогда
		Автоадрес = СтррКоординаты.Город + ", " + СтррКоординаты.Регион;
		СтррКонтекст.Автокоординаты = СтррКоординаты; // запоминаем координаты, возвращенные сервисом по ip-адресу
		Способ = "СпособАвтоопределение";		
	Иначе // веб-сервис определения координат недоступен
		Автоадрес = НСтр("ru = '<Сервис недоступен>'");
		Элементы.СпособАвтоопределение.Доступность = Ложь;
		Способ = "СпособУказатьНаКарте";
	КонецЕсли; 
	
	// Если есть сохраненное значение способа, устанавливаем его, но при условии, что значение не "СпособАвтоопределение" -
	// в этом случае используем первый доступный способ.
	
	Если ЗначениеСпособаСохраняемое <> "СпособАвтоопределение" И Не ПустаяСтрока(ЗначениеСпособаСохраняемое) Тогда
		Способ = ЗначениеСпособаСохраняемое; // Если ЗначениеСпособаСохраняемое = "СпособАвтоопределение" и сервис недоступен, то
	КонецЕсли; 
	
	ПриИзмененииСпособа(Способ);
	
КонецПроцедуры

// ОбработчикиСобытийФормы
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособПриИзменении(Элемент)
	
	ПриИзмененииСпособа(Элемент.Имя);
	
КонецПроцедуры

// ОбработчикиСобытийЭлементовФормы
#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если СпособАвтоопределение Тогда
		
		ЗакрытьФормуИВернутьРезультат(СтррКонтекст.Автокоординаты, "СпособАвтоопределение");
		
	ИначеЕсли СпособПоАдресу Тогда // нужно получить координаты по адресу
		
		Если ПустаяСтрока(АдресПоиска) Тогда
			Текст = НСтр("ru = 'Укажите название города или адрес'");
			//@skip-check object-deprecated
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст,, "АдресПоиска");
		Иначе // ищем координаты по указанному адресу
			СЗКоординаты = МодульКарты.ПолуичтьКоординатыОтВебСервиса(АдресПоиска, Ложь);
			Если СЗКоординаты = Неопределено Тогда // сервис недоступен, МодульКарты об этом сообщил.
			ИначеЕсли СЗКоординаты.Количество() = 1 Тогда
				ЗакрытьФормуИВернутьРезультат(ПолучитьКоординатыИзЭлементаСписка(СЗКоординаты[0]), "СпособПоАдресу");
			Иначе
				Оповещение = Новый ОписаниеОповещения("КоординатыОбработкаВыбораИзСписка", ЭтотОбъект);
				СЗКоординаты.ПоказатьВыборЭлемента(Оповещение, Нстр("ru = 'Выберите координаты по адресу'"));
			КонецЕсли; 
		КонецЕсли; 
		
	ИначеЕсли СпособУказатьНаКарте Тогда // выбор координат на карте
		
		ЗакрытьФормуИВернутьРезультат("ВыбратьНаКарте", "СпособУказатьНаКарте");
		
	Иначе
		
		ВызватьИсключение("Неизвестный способ указания центра карты!");
		
	КонецЕсли; 

КонецПроцедуры

// ОбработчикиКомандФормы
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункции_СовместимостьСПлатформой_8_3_5

// Разбивает строку на несколько строк по разделителю. Разделитель может иметь любую длину.
//
// Параметры:
//  Строка                 - Строка - текст с разделителями;
//  Разделитель            - Строка - разделитель строк текста, минимум 1 символ;
//  ПропускатьПустыеСтроки - Булево - признак необходимости включения в результат пустых строк.
//    Если параметр не задан, то функция работает в режиме совместимости со своей предыдущей версией:
//       т.е. для разделителя-пробела пустые строки не включаются в результат, для остальных разделителей пустые строки
//       включаются в результат.
//    Если параметр Строка не содержит значащих символов или не содержит ни одного символа (пустая строка), то в
//       случае разделителя-пробела результатом функции будет массив, содержащий одно значение "" (пустая строка), а
//       при других разделителях результатом функции будет пустой массив.
//  СокращатьНепечатаемыеСимволы - Булево - сокращать непечатаемые символы по краям каждой из найденных подстрок.
//
// Возвращаемое значение:
//  Массив Из Строка - массив строк.
&НаКлиентеНаСервереБезКонтекста 
Функция СтрРазделить_(Знач Строка, Знач Разделитель = ",", Знач ПропускатьПустыеСтроки = Неопределено, СокращатьНепечатаемыеСимволы = Ложь)
	
	Результат = Новый Массив;
	
	// Для обеспечения обратной совместимости.
	Если ПропускатьПустыеСтроки = Неопределено Тогда
		ПропускатьПустыеСтроки = ?(Разделитель = " ", Истина, Ложь);
		Если ПустаяСтрока(Строка) Тогда 
			Если Разделитель = " " Тогда
				Результат.Добавить("");
			КонецЕсли;
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	//
	
	//@skip-check use-non-recommended-method
	Позиция = Найти(Строка, Разделитель);
	Пока Позиция > 0 Цикл
		Подстрока = Лев(Строка, Позиция - 1);
		Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Подстрока) Тогда
			Если СокращатьНепечатаемыеСимволы Тогда
				Результат.Добавить(СокрЛП(Подстрока));
			Иначе
				Результат.Добавить(Подстрока);
			КонецЕсли;
		КонецЕсли;
		Строка = Сред(Строка, Позиция + СтрДлина(Разделитель));
		//@skip-check use-non-recommended-method
		Позиция = Найти(Строка, Разделитель);
	КонецЦикла;
	
	Если Не ПропускатьПустыеСтроки Или Не ПустаяСтрока(Строка) Тогда
		Если СокращатьНепечатаемыеСимволы Тогда
			Результат.Добавить(СокрЛП(Строка));
		Иначе
			Результат.Добавить(Строка);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// СлужебныеПроцедурыИФункции_СовместимостьСПлатформой_8_3_5
#КонецОбласти

// Процедура закрывает форму с возвратом результата выбора.
// 
// Параметры:
// 		Результат - Строка, Структура - результат выбора - зависит от выбранного пользователем способа указания центра карты.
// 		СпособВыбораЦентраКарты - Строка - сохраняемый способ выбора центра карты при закрытии формы.
&НаКлиенте
Процедура ЗакрытьФормуИВернутьРезультат(Результат, СпособВыбораЦентраКарты)
	
	ЗначениеСпособаСохраняемое = СпособВыбораЦентраКарты;
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура КоординатыОбработкаВыбораИзСписка(ЭлементСписка, ДополнительныеПараметры) Экспорт
	
	Если ЭлементСписка <> Неопределено Тогда
		ЗакрытьФормуИВернутьРезультат(ПолучитьКоординатыИзЭлементаСписка(ЭлементСписка), "СпособПоАдресу");
	КонецЕсли;

КонецПроцедуры	

&НаКлиенте
Функция ПолучитьКоординатыИзЭлементаСписка(ЭлементСписка)
	
	СтррРезультат = Новый Структура("Широта,Долгота,Город");
	СтррРезультат.Город = СтрЗаменить(ЭлементСписка.Представление, "*", "");
	МКоординаты = СтрРазделить_(ЭлементСписка.Значение, " ");
	СтррРезультат.Широта   = Число(СтрЗаменить(МКоординаты[0], " ", ""));
	СтррРезультат.Долгота  = Число(СтрЗаменить(МКоординаты[1], " ", ""));
	
	Возврат СтррРезультат;
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииСпособа(Имя)
	
	СтррРеквизиты = СтррКонтекст.Реквизиты;
	Для Каждого Реквизит Из СтррРеквизиты Цикл
		ЭтотОбъект[Реквизит.Ключ] = (Реквизит.Ключ = Имя);
		Если Реквизит.Значение <> Неопределено Тогда
			Элементы[Реквизит.Значение].Доступность = ЭтотОбъект[Реквизит.Ключ];
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции
#КонецОбласти 
