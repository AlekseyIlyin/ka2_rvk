#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьЖурналДокументы();	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ПараметрыВыбора") Тогда
		ВызватьИсключение("Ожидается свойство 'Параметры.ПараметрыВыбора' при создании формы.");
	КонецЕсли;
	
	//@skip-check unknown-form-parameter-access
	СтррКонтекст = Параметры.ПараметрыВыбора;	
	ТекОбъект = РеквизитФормыВЗначение("Объект");
	ТекОбъект.КонтекстФормыИнициализировать(СтррКонтекст, Параметры);
	
КонецПроцедуры

// ОбработчикиСобытийФормы
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЖурналДокументы

&НаКлиенте
Процедура ЖурналДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	КомандаВыбрать(Элемент.Имя);
КонецПроцедуры

// ОбработчикиСобытийЭлементовТаблицыФормыЖурналДокументы
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)

	Если Элементы.ЖурналДокументы.ТекущиеДанные = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Документ не выбран.'");
		//@skip-check object-deprecated
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
		
	CтррРезультат = Новый Структура("Документ, ДокументПредставление", Элементы.ЖурналДокументы.ТекущиеДанные.Документ, Элементы.ЖурналДокументы.ТекущиеДанные.ДокументПредставление);
	Закрыть(CтррРезультат);
	
КонецПроцедуры

// ОбработчикиКомандФормы
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьЖурналДокументы()
	
	ЖурналДокументы.Очистить();
	
	ТекОбъект = РеквизитФормыВЗначение("Объект");
	
	//1. Типовые
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДополнительныеСведения.Объект КАК Ссылка
	               |ИЗ
	               |	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |ГДЕ
	               |	ДополнительныеСведения.Свойство = &СвойствоВремяНачала";
	
	СвойствоВремяНачала = ТекОбъект.ДополнительноеСвойствоПоНаименованию(ТекОбъект.ИмяСвойстваДокументВремяНачала());

	Запрос.УстановитьПараметр("СвойствоВремяНачала", СвойствоВремяНачала);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СТЗЖурналДокументы = ЖурналДокументы.Добавить();
		СТЗЖурналДокументы.Документ 				= Выборка.Ссылка;
		СТЗЖурналДокументы.ДокументПредставление 	= Строка(Выборка.Ссылка);
	КонецЦикла;
	
	//2. Виртуальные
	//2.1 _Посещение
	ЖурналДокументыДобавитьВО("_Посещение");
		
	//2.2 _Мерчендайзинг
	ЖурналДокументыДобавитьВО("_Мерчендайзинг");

КонецПроцедуры

&НаСервере
Процедура ЖурналДокументыДобавитьВО(ИмяВО)

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	
	ТЗЖурнала = ТекОбъект.ВОТЗЗагрузить(ИмяВО, Ложь);
	Для Каждого стзЖурнала Из ТЗЖурнала Цикл
		//@skip-check structure-consructor-too-many-keys
		СсылкаВДок = Новый Структура("ID, ВидДокумента, Проведен, ПометкаУдаления");
		СсылкаВДок.ID				= стзЖурнала.ID;
		СсылкаВДок.ВидДокумента   	= ИмяВО;
		СсылкаВДок.Проведен       	= (стзЖурнала.Статус = 1);
		СсылкаВДок.ПометкаУдаления 	= (стзЖурнала.Статус = 2);	
		
		стзЖурналДокументы = ЖурналДокументы.Добавить();
		стзЖурналДокументы.Документ 				= СсылкаВДок;
		стзЖурналДокументы.ДокументПредставление 	= ТекОбъект.ВДокПредставление(СсылкаВДок.ВидДокумента, стзЖурнала);
	КонецЦикла; 
	
КонецПроцедуры

// СлужебныеПроцедурыИФункции
#КонецОбласти 
