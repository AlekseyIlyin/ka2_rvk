#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	СтррКонтекст = Новый Структура("Конфигурация");	
	ТекОбъект = РеквизитФормыВЗначение("Объект");		
	ТекОбъект.КонтекстФормыИнициализировать(СтррКонтекст, Параметры);

	Если Параметры.Свойство("Расписание") Тогда
		//@skip-check unknown-form-parameter-access
		Если ТекОбъект.СтруктураЕстьСвойства(Параметры.Расписание, "ДниНедели,ВремяНачала,ВремяОкончания,ПериодЗаписи") Тогда
			Расписание = Параметры.Расписание;			
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ПеренестиНаФорму();
	
КонецПроцедуры

// ОбработчикиСобытийФормы
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПрименить(Команда)

	СтрДни = "";
	Для Сч = 1 По 7 Цикл
		//@skip-check form-self-reference
		Если ЭтаФорма["День" + Сч] Тогда
			СтрДни = СтрДни + ?(СтрДни = "", "", ",") + Строка(Сч);
		КонецЕсли;
	КонецЦикла;
	
	СтррРезультат = Новый Структура;
	СтррРезультат.Вставить("ДниНедели", 	 СтрДни);
	СтррРезультат.Вставить("ВремяНачала", 	 Формат(ВремяНачала, 	"ДФ='HH:mm'"));
	СтррРезультат.Вставить("ВремяОкончания", Формат(ВремяОкончания, "ДФ='HH:mm'"));
	СтррРезультат.Вставить("ПериодЗаписи", 	 ПериодЗаписи);
	
	//@skip-check form-self-reference
	Оповестить("АПИзменениеРасписанияВеденияТрека", СтррРезультат, ЭтаФорма);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПоУмолчанию(Команда)
	
	Если СтррКонтекст.Конфигурация = "УТ_АКФ" Тогда
		//@skip-check structure-consructor-too-many-keys
		Расписание = Новый Структура("ДниНедели,ВремяНачала,ВремяОкончания,ПериодЗаписи",
			"1,2,3,4,5,6,7", "01:00", "23:00", 1);
	Иначе
		//@skip-check structure-consructor-too-many-keys
		Расписание = Новый Структура("ДниНедели,ВремяНачала,ВремяОкончания,ПериодЗаписи",
			"1,2,3,4,5", "09:00", "18:00", 60);
	КонецЕсли;
	ПеренестиНаФорму();
	
КонецПроцедуры

// ОбработчикиКомандФормы
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура переносит свойства расписания на форму.
&НаКлиенте
Процедура ПеренестиНаФорму()
	
	Если ТипЗнч(Расписание) = Тип("Структура") Тогда
		
		ВремяНачала 	= ПолучитьДатуВремяИзСтрокиВремени(Расписание.ВремяНачала);
		ВремяОкончания 	= ПолучитьДатуВремяИзСтрокиВремени(Расписание.ВремяОкончания);
		ПериодЗаписи 	= Расписание.ПериодЗаписи;
		
		Для Сч = 1 По 7 Цикл
			СтрСч = Строка(Сч);
			//@skip-check form-self-reference
			//@skip-check use-non-recommended-method
			ЭтаФорма["День"+СтрСч] = 0 <> Найти(Расписание.ДниНедели, СтрСч);
		КонецЦикла;
			
	КонецЕсли;
		
	Если ПериодЗаписи = 0 Тогда
		ПериодЗаписи = 60;
	КонецЕсли;
	
КонецПроцедуры

// Функция вовзвращает значение типа "Дата" из строки в формате "HH:MM".
// Используется для вывода на форме в поле типа "Дата" только времени.
&НаКлиенте
Функция ПолучитьДатуВремяИзСтрокиВремени(СтрВремя)
	
	Если ТипЗнч(СтрВремя) <> Тип("Строка") Или СтрДлина(СтрВремя) <> 5 Тогда
		СтрВремя = "00:00";
	КонецЕсли;
	
	Возврат Дата(2016, 07, 22, Число(Лев(СтрВремя, 2)), Число(Прав(СтрВремя, 2)), 0);	
	
КонецФункции

// СлужебныеПроцедурыИФункции
#КонецОбласти
