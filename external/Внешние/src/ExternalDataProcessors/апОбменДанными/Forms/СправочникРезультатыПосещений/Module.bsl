#Область ОписаниеПеременных

&НаКлиенте
Перем МодульМТ; // Общий клиентский модуль со спецификой мобильной торговли

// ОписаниеПеременных
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	
	СтррКонтекст = Новый Структура; // общие значения модуля формы
	ТекОбъект.КонтекстФормыИнициализировать(СтррКонтекст, Параметры);		
	ТекОбъект.ВОКонтекстФормыДополнить(СтррКонтекст, "_РезультатыПосещений", Истина);
	ТекОбъект.ВОПриСозданииФормыСписка(ЭтотОбъект);
	
	ПрочестьОбъектИзХранилища();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Загружаем общий клиентский модуль "МодульОбщийМТ". В параметре "Параметры" важно передавать структуру с заполненным
	// свойством "ВХОбщиеПараметры" - оно используется для предотвращения повторной загрузки текущей обработки.
	//@skip-check use-non-recommended-method
	//@skip-check form-self-reference
	МодульМТ = ПолучитьФорму(СтррКонтекст.ПутьКФорме + "МодульОбщийМТ", СтррКонтекст, ЭтаФорма, "АгентПлюсМодульОбщийМТ"); // в СтррКонтекст есть заполненное свойство "ВХОбщиеПараметры"
	МодульМТ.ВОПриОткрытии(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если МодульМТ.ОбработкаОповещенияФормы(ЭтотОбъект, ИмяСобытия, Параметр, Источник) Тогда
	ИначеЕсли ИмяСобытия = "АПЭлементСправочникаЗаписан_" + СтррКонтекст.ВО.ВидОбъекта Тогда
		ПрочестьОбъектИзХранилища();
		МодульМТ.ВОЭлементыВыделить(ЭтотОбъект, Параметр);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)

	//@skip-check form-self-reference
	Если ЭтаФорма.Модифицированность Тогда
		СохранитьВсе();
	КонецЕсли;

КонецПроцедуры

// ОбработчикиСобытийФормы
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТПЭлементы

&НаКлиенте
Процедура ТПЭлементыПриИзменении(Элемент)
	
	// Внимание!!! На данной форме это событие происходит только при смене порядка строк. Если будет расширено поведение поля СпискаТТ
	// и это событие будет вызываться при других изменениях списка, то нужно сохранять объект в хранилище только после изменения порядка строк.
	СохранитьОбъектВХранилище(); 

КонецПроцедуры

&НаКлиенте
Процедура ТПЭлементыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	МодульМТ.ВООткрытьФормуИзФормыСписка(ЭтотОбъект, Истина, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура ТПЭлементыПередУдалением(Элемент, Отказ)

	МодульМТ.ВОЭлементыПередПометкойУдаления(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ТПЭлементыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;	
	МодульМТ.ВОЭлементыВыбор(ЭтотОбъект);
	
КонецПроцедуры

// ОбработчикиСобытийЭлементовТаблицыФормыТПЭлементы
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыполнить(Команда)
	
	МодульМТ.КомандаВыполнить(Команда, ЭтотОбъект)
	
КонецПроцедуры

// ОбработчикиКомандФормы
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныеПроцедурыИФункции_Хранилище

&НаСервере
Процедура ПрочестьОбъектИзХранилища()

	ТекОбъект = РеквизитФормыВЗначение("Объект");
	
	ТЗн = ТекОбъект.ВОТЗЗагрузить(СтррКонтекст.ВО);
	ТекОбъект[СтррКонтекст.ВО.РеквизитОбработки] = ТЗн;
	
	ЗначениеВРеквизитФормы(ТекОбъект, "Объект");

КонецПроцедуры

&НаКлиенте
Процедура СохранитьВсе()

	//@skip-check form-self-reference
	ЭтаФорма.Модифицированность = Ложь;
	СохранитьОбъектВХранилище();
	Оповестить("АПЭлементыСправочникаЗаписаны_" + СтррКонтекст.ВО.ВидОбъекта, Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьОбъектВХранилище()
	
	СтррВО = СтррКонтекст.ВО;
	РеквизитФормыВЗначение("Объект").СохранитьЗначениеНастройки(СтррВО.РеквизитОбработки, Объект[СтррВО.РеквизитОбработки].Выгрузить());

КонецПроцедуры

// СлужебныеПроцедурыИФункции_Хранилище
#КонецОбласти 

#КонецОбласти