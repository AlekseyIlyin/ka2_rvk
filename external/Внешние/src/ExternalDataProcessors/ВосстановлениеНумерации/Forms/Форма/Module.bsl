
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Для Каждого МетаданныеДокумента Из Метаданные.Документы Цикл
		Если МетаданныеДокумента.ДлинаНомера > 0 Тогда
			ЭтаФорма.Элементы.ТипОбъекта.СписокВыбора.Добавить(МетаданныеДокумента.Имя, МетаданныеДокумента.Синоним);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицуДокументовНаСервере()
	
	Объект.ОбъектыДляОбработки.Очистить();
	
	Если ЗначениеЗаполнено(Объект.Организация) И ЗначениеЗаполнено(Объект.ТипОбъекта) Тогда
	
		Запрос = Новый Запрос;
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	Документ.Ссылка КАК Ссылка,
			|	Документ.Номер КАК Номер,
			|	НАЧАЛОПЕРИОДА(Документ.Дата, ДЕНЬ) КАК Дата
			|ИЗ
			|	Документ." + Объект.ТипОбъекта + " КАК Документ
			|ГДЕ
			|	Документ.Организация = &Организация //ОтборПоПериоду
			|
			|УПОРЯДОЧИТЬ ПО
			|	Дата,
			|	Номер";
		
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
		
		Если ЗначениеЗаполнено(Объект.Период) Тогда
			Запрос.УстановитьПараметр("ДатаНачала", Объект.Период.ДатаНачала);
			Запрос.УстановитьПараметр("ДатаОкончания", Объект.Период.ДатаОкончания);
			СтрокаУсловияОтбораПоПериоду = "И Документ.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания";
		Иначе
			СтрокаУсловияОтбораПоПериоду = "";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ОтборПоПериоду", СтрокаУсловияОтбораПоПериоду);
		
		Запрос.Текст = ТекстЗапроса;
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Год = 0;
		НомерПоВозрастанию = 0;
		Префикс = "";
		РазмерПрефикса = 0;
		РазмерВсегоНомера = 0;
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			НоваяСтрока = Объект.ОбъектыДляОбработки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
			
			НомерТекущейСтрокиБезПрефикса = ПрефиксацияОбъектовКлиентСервер.УдалитьПрефиксыИзНомераОбъекта(НоваяСтрока.Номер,Истина,Истина);
			ГодДаты = Год(НоваяСтрока.Дата);
			Если Год <> ГодДаты Тогда
				Год = ГодДаты;
				РазмерВсегоНомера = СтрДлина(НоваяСтрока.Номер);
				НоваяСтрока.НарушенПорядок = Ложь;
				НомерПоВозрастанию = Число(НомерТекущейСтрокиБезПрефикса);
				Префикс = СтрЗаменить(НоваяСтрока.Номер, НомерТекущейСтрокиБезПрефикса, "");
				РазмерПрефикса = СтрДлина(Префикс);
			КонецЕсли;
			
			ПредставлениеЗначимойЧасти = Формат(НомерПоВозрастанию,"ЧДЦ=0; ЧГ=");
			РазмерЗначимойЧасти = СтрДлина(ПредставлениеЗначимойЧасти);
			НоваяСтрока.НомерНовый = Префикс + Лев("0000000000", РазмерВсегоНомера - РазмерПрефикса - РазмерЗначимойЧасти) + ПредставлениеЗначимойЧасти;
			
			НоваяСтрока.НарушенПорядок = НоваяСтрока.Номер <> НоваяСтрока.НомерНовый;
			
			НомерПоВозрастанию = НомерПоВозрастанию + 1;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОставитьТолькоПроблемныеНаКлиенте()
	Если ЗначениеЗаполнено(Объект.ОбъектыДляОбработки) Тогда
		
		СписокСтрокДляУдаления = Новый Массив;
		
		Для Каждого СтрокаОбъектаПеренумерации Из Объект.ОбъектыДляОбработки Цикл
			Если НЕ СтрокаОбъектаПеренумерации.НарушенПорядок Тогда
				СписокСтрокДляУдаления.Добавить(СтрокаОбъектаПеренумерации);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого СтрокаДляУдаления Из СписокСтрокДляУдаления Цикл
			Объект.ОбъектыДляОбработки.Удалить(СтрокаДляУдаления);
		КонецЦикла;
		
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	ПроверитьЗаполнение();
	ОбновитьТаблицуДокументовНаСервере();
	Если Объект.ПоказыватьТолькоПроблемные Тогда
		ОставитьТолькоПроблемныеНаКлиенте();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьНомерНаСервере(ДокументСсылка, НовыйНомер)
	
	Результат = Новый Структура("Отказ,Сообщение", Ложь, "");
	
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	ДокументОбъект.Номер = НовыйНомер;
	ДокументОбъект.ОбменДанными.Загрузка = Истина;
	Попытка
		ДокументОбъект.Записать();
		Результат.Вставить("Номер", ДокументОбъект.Номер);
	Исключение
		Результат.Отказ = Истина;
		Результат.Сообщение = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура Перенумеровать(Команда)
	ИдентификаторыВыделенныхСтрок = ЭтаФорма.Элементы.ОбъектыДляОбработки.ВыделенныеСтроки;
	Если ЗначениеЗаполнено(ИдентификаторыВыделенныхСтрок) Тогда
		Счетчик = 1;
		ВсегоСтрок = ИдентификаторыВыделенныхСтрок.Количество();
		Для Каждого ИдентификаторВыделеннойСтроки Из ИдентификаторыВыделенныхСтрок Цикл
			Состояние(СтрШаблон("Обрабатываются объекты %1 из %2", Счетчик, ВсегоСтрок), (Счетчик / ВсегоСтрок) * 100, "Перенумерация");
			ДанныеВыделеннойСтроки = Объект.ОбъектыДляОбработки.НайтиПоИдентификатору(ИдентификаторВыделеннойСтроки);
			Результат = УстановитьНомерНаСервере(ДанныеВыделеннойСтроки.Ссылка, ДанныеВыделеннойСтроки.НомерНовый);
			Если Результат.Отказ Тогда
				Сообщить(Результат.Сообщение);
				Прервать;
			Иначе
				ДанныеВыделеннойСтроки.Номер = Результат.Номер;
				ДанныеВыделеннойСтроки.НарушенПорядок = ДанныеВыделеннойСтроки.Номер <> ДанныеВыделеннойСтроки.НомерНовый;
			КонецЕсли;
			Счетчик = Счетчик + 1;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УдалитьСтрокиСКорректнымКодом(Команда)
	ОставитьТолькоПроблемныеНаКлиенте();
КонецПроцедуры
